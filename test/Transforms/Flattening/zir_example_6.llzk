// RUN: llzk-opt -split-input-file -llzk-flatten %s | FileCheck --enable-var-scope %s

#mapDiv = affine_map<()[s0, s1] -> (s0 floordiv s1)>
#mapAdd = affine_map<()[s0, s1] -> (s0 + s1)>
module attributes {veridise.lang = "llzk"} {
  struct.def @Foo<[@Aff]> {
    struct.field @bar: !struct.struct<@Bar<[@Aff]>>
    function.def @compute(%arg: !struct.struct<@Bar<[@Aff]>>) -> !struct.struct<@Foo<[@Aff]>> {
      %self = struct.new : !struct.struct<@Foo<[@Aff]>>
      struct.writef %self[@bar] = %arg : !struct.struct<@Foo<[@Aff]>>, !struct.struct<@Bar<[@Aff]>>
      function.return %self : !struct.struct<@Foo<[@Aff]>>
    }
    function.def @constrain(%self: !struct.struct<@Foo<[@Aff]>>, %arg: !struct.struct<@Bar<[@Aff]>>) {
      function.return
    }
  }
  struct.def @Bar<[@Aff]> {
    function.def @compute() -> !struct.struct<@Bar<[@Aff]>> {
      %self = struct.new : !struct.struct<@Bar<[@Aff]>>
      function.return %self : !struct.struct<@Bar<[@Aff]>>
    }
    function.def @constrain(%arg0: !struct.struct<@Bar<[@Aff]>>) {
      function.return
    }
  }
  struct.def @Unpack<[@N, @P, @Aff]> {
    struct.field @f: !struct.struct<@Bar<[#mapAdd]>>
    struct.field @g: !array.array<@Aff x !felt.felt>
    function.def @compute(%arg0: !array.array<@Aff x !felt.felt>) -> !struct.struct<@Unpack<[@N, @P, @Aff]>> {
      %self = struct.new : !struct.struct<@Unpack<[@N, @P, @Aff]>>
      struct.writef %self[@g] = %arg0 : !struct.struct<@Unpack<[@N, @P, @Aff]>>, !array.array<@Aff x !felt.felt>

      %n = llzk.read_const @N : index
      %p = llzk.read_const @P : index
      %0 = function.call @Bar::@compute() {()[%n,%p]} : () -> !struct.struct<@Bar<[#mapAdd]>>
      struct.writef %self[@f] = %0 : !struct.struct<@Unpack<[@N, @P, @Aff]>>, !struct.struct<@Bar<[#mapAdd]>>

      %1 = function.call @Foo::@compute(%0) {()[%n,%p]} : (!struct.struct<@Bar<[#mapAdd]>>) -> !struct.struct<@Foo<[#mapAdd]>>

      function.return %self : !struct.struct<@Unpack<[@N, @P, @Aff]>>
    }
    function.def @constrain(%arg0: !struct.struct<@Unpack<[@N, @P, @Aff]>>, %arg1: !array.array<@Aff x !felt.felt>) {
      function.return
    }
  }
  struct.def @User1 {
    function.def @compute() -> !struct.struct<@User1> {
      %0 = felt.const 42
      %1 = felt.const 99
      %2 = felt.const 42
      %3 = felt.const 99

      %arr = array.new %0, %1, %2, %3 : !array.array<4 x !felt.felt>
      %a = arith.constant 8 : index
      %b = arith.constant 2 : index
      %s = function.call @Unpack::@compute(%arr) {()[%a, %b]}
        : (!array.array<4 x !felt.felt>) -> !struct.struct<@Unpack<[8, 2, #mapDiv]>>
      %self = struct.new : !struct.struct<@User1>
      function.return %self : !struct.struct<@User1>
    }
    function.def @constrain(%arg0: !struct.struct<@User1>) {
      function.return
    }
  }
}
// CHECK-LABEL: struct.def @Foo_10 {
// CHECK-NEXT:    struct.field @bar : !struct.struct<@Bar_10>
// CHECK-NEXT:    function.def @compute(%[[V0:[0-9a-zA-Z_\.]+]]: !struct.struct<@Bar_10>) -> !struct.struct<@Foo_10> attributes {function.allow_witness} {
// CHECK-NEXT:      %[[V1:[0-9a-zA-Z_\.]+]] = struct.new : <@Foo_10>
// CHECK-NEXT:      struct.writef %[[V1]][@bar] = %[[V0]] : <@Foo_10>, !struct.struct<@Bar_10>
// CHECK-NEXT:      function.return %[[V1]] : !struct.struct<@Foo_10>
// CHECK-NEXT:    }
// CHECK-NEXT:    function.def @constrain(%[[V2:[0-9a-zA-Z_\.]+]]: !struct.struct<@Foo_10>, %[[V3:[0-9a-zA-Z_\.]+]]: !struct.struct<@Bar_10>) attributes {function.allow_constraint} {
// CHECK-NEXT:      function.return
// CHECK-NEXT:    }
// CHECK-NEXT:  }
//
// CHECK-LABEL: struct.def @Bar_10 {
// CHECK-NEXT:    function.def @compute() -> !struct.struct<@Bar_10> attributes {function.allow_witness} {
// CHECK-NEXT:      %[[V0:[0-9a-zA-Z_\.]+]] = struct.new : <@Bar_10>
// CHECK-NEXT:      function.return %[[V0]] : !struct.struct<@Bar_10>
// CHECK-NEXT:    }
// CHECK-NEXT:    function.def @constrain(%[[V1:[0-9a-zA-Z_\.]+]]: !struct.struct<@Bar_10>) attributes {function.allow_constraint} {
// CHECK-NEXT:      function.return
// CHECK-NEXT:    }
// CHECK-NEXT:  }
//
// CHECK-LABEL: struct.def @Unpack_8_2_4 {
// CHECK-NEXT:    struct.field @f : !struct.struct<@Bar_10>
// CHECK-NEXT:    struct.field @g : !array.array<4 x !felt.felt>
// CHECK-NEXT:    function.def @compute(%[[V0:[0-9a-zA-Z_\.]+]]: !array.array<4 x !felt.felt>) -> !struct.struct<@Unpack_8_2_4> attributes {function.allow_witness} {
// CHECK-NEXT:      %[[V1:[0-9a-zA-Z_\.]+]] = struct.new : <@Unpack_8_2_4>
// CHECK-NEXT:      struct.writef %[[V1]][@g] = %[[V0]] : <@Unpack_8_2_4>, !array.array<4 x !felt.felt>
// CHECK-NEXT:      %[[V2:[0-9a-zA-Z_\.]+]] = function.call @Bar_10::@compute() : () -> !struct.struct<@Bar_10>
// CHECK-NEXT:      struct.writef %[[V1]][@f] = %[[V2]] : <@Unpack_8_2_4>, !struct.struct<@Bar_10>
// CHECK-NEXT:      %[[V3:[0-9a-zA-Z_\.]+]] = function.call @Foo_10::@compute(%[[V2]]) : (!struct.struct<@Bar_10>) -> !struct.struct<@Foo_10>
// CHECK-NEXT:      function.return %[[V1]] : !struct.struct<@Unpack_8_2_4>
// CHECK-NEXT:    }
// CHECK-NEXT:    function.def @constrain(%[[V4:[0-9a-zA-Z_\.]+]]: !struct.struct<@Unpack_8_2_4>, %[[V5:[0-9a-zA-Z_\.]+]]: !array.array<4 x !felt.felt>) attributes {function.allow_constraint} {
// CHECK-NEXT:      function.return
// CHECK-NEXT:    }
// CHECK-NEXT:  }
//
// CHECK-LABEL: struct.def @User1 {
// CHECK-NEXT:    function.def @compute() -> !struct.struct<@User1> attributes {function.allow_witness} {
// CHECK-NEXT:      %[[V0:[0-9a-zA-Z_\.]+]] = felt.const 42
// CHECK-NEXT:      %[[V1:[0-9a-zA-Z_\.]+]] = felt.const 99
// CHECK-NEXT:      %[[V2:[0-9a-zA-Z_\.]+]] = array.new %[[V0]], %[[V1]], %[[V0]], %[[V1]] : <4 x !felt.felt>
// CHECK-NEXT:      %[[V3:[0-9a-zA-Z_\.]+]] = function.call @Unpack_8_2_4::@compute(%[[V2]]) : (!array.array<4 x !felt.felt>) -> !struct.struct<@Unpack_8_2_4>
// CHECK-NEXT:      %[[V4:[0-9a-zA-Z_\.]+]] = struct.new : <@User1>
// CHECK-NEXT:      function.return %[[V4]] : !struct.struct<@User1>
// CHECK-NEXT:    }
// CHECK-NEXT:    function.def @constrain(%[[V5:[0-9a-zA-Z_\.]+]]: !struct.struct<@User1>) attributes {function.allow_constraint} {
// CHECK-NEXT:      function.return
// CHECK-NEXT:    }
// CHECK-NEXT:  }
