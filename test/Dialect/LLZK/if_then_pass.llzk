// RUN: llzk-opt -split-input-file %s 2>&1 | FileCheck --enable-var-scope %s

module attributes {veridise.lang = "llzk"} {
  // A struct that stores if the input is 0 or not
  // to showcase `scf.if`.
  // Only constraints that the stored value is a boolean.
  // A real circuit would have more stuff
  struct.def @IsZero {
    struct.field @bit : !felt.felt

    function.def @compute(%a: !felt.felt) -> !struct.struct<@IsZero> {
      %self = struct.new : !struct.struct<@IsZero>
      %z = felt.const 0
      %b = bool.cmp eq(%a, %z)
      %0 = scf.if %b -> !felt.felt {
        scf.yield %z : !felt.felt
      } else {
        %2 = felt.const 1
        scf.yield %2 : !felt.felt
      }
      struct.writef %self[@bit] = %0 : !struct.struct<@IsZero>, !felt.felt
      function.return %self : !struct.struct<@IsZero>
    }

    function.def @constrain(%self : !struct.struct<@IsZero>, %a: !felt.felt) {
      %0 = struct.readf %self[@bit] : !struct.struct<@IsZero>, !felt.felt
      %1 = felt.const 1
      %2 = felt.sub %0, %1
      %3 = felt.mul %0, %2
      %4 = felt.const 0
      constrain.eq %3, %4 : !felt.felt
      function.return
    }
  }
}

// CHECK-LABEL:  struct.def @IsZero {
// CHECK-NEXT:     struct.field @bit : !felt.felt
// CHECK-NEXT:     function.def @compute(%[[A0:[0-9a-zA-Z_.]+]]: !felt.felt) -> !struct.struct<@IsZero> attributes {function.allow_witness} {
// CHECK-NEXT:       %[[SELF:[0-9a-zA-Z_.]+]] = struct.new : <@IsZero>
// CHECK-NEXT:       %[[Z:[0-9a-zA-Z_.]+]] = felt.const 0
// CHECK-NEXT:       %[[T0:[0-9a-zA-Z_.]+]] = bool.cmp eq(%[[A0]], %[[Z]])
// CHECK-NEXT:       %[[T1:[0-9a-zA-Z_.]+]] = scf.if %[[T0]] -> (!felt.felt) {
// CHECK-NEXT:         scf.yield %[[Z]] : !felt.felt
// CHECK-NEXT:       } else {
// CHECK-NEXT:         %[[T2:[0-9a-zA-Z_.]+]] = felt.const 1
// CHECK-NEXT:         scf.yield %[[T2]] : !felt.felt
// CHECK-NEXT:       }
// CHECK-NEXT:       struct.writef %[[SELF]][@bit] = %[[T1]] : <@IsZero>, !felt.felt
// CHECK-NEXT:       function.return %[[SELF]]
// CHECK-NEXT:     }
// CHECK-NEXT:     function.def @constrain(%[[SELF:[0-9a-zA-Z_.]+]]: !struct.struct<@IsZero>, %[[A0:[0-9a-zA-Z_.]+]]: !felt.felt) attributes {function.allow_constraint} {
// CHECK-NEXT:       %[[T0:[0-9a-zA-Z_.]+]] = struct.readf %[[SELF]][@bit] : <@IsZero>, !felt.felt
// CHECK-NEXT:       %[[T1:[0-9a-zA-Z_.]+]] = felt.const 1
// CHECK-NEXT:       %[[T2:[0-9a-zA-Z_.]+]] = felt.sub %[[T0]], %[[T1]]
// CHECK-NEXT:       %[[T3:[0-9a-zA-Z_.]+]] = felt.mul %[[T0]], %[[T2]]
// CHECK-NEXT:       %[[T4:[0-9a-zA-Z_.]+]] = felt.const 0
// CHECK-NEXT:       constrain.eq %[[T3]], %[[T4]] : !felt.felt, !felt.felt
// CHECK-NEXT:       function.return
// CHECK-NEXT:     }
// CHECK-NEXT: }
// -----

module attributes {veridise.lang = "llzk"} {
  // This test case showcases that you don't need to add `scf.yield`
  // if the `scf.if` operation does not function.return a value.
  struct.def @C {
    struct.field @data : !felt.felt

    function.def @compute(%a: !felt.felt, %b: !felt.felt) -> !struct.struct<@C> {
      %self = struct.new : !struct.struct<@C>
      %z = felt.const 0
      %0 = bool.cmp ne(%a, %z)
      scf.if %0 {
        struct.writef %self[@data] = %b : !struct.struct<@C>, !felt.felt
      }
      function.return %self : !struct.struct<@C>
    }

    function.def @constrain(%self : !struct.struct<@C>, %a: !felt.felt, %b: !felt.felt) {
      function.return
    }
  }
}

// CHECK-LABEL:  struct.def @C {
// CHECK-NEXT:    struct.field @data : !felt.felt
// CHECK-NEXT:    function.def @compute(%[[A0:[0-9a-zA-Z_.]+]]: !felt.felt, %[[A1:[0-9a-zA-Z_.]+]]: !felt.felt) -> !struct.struct<@C> attributes {function.allow_witness} {
// CHECK-NEXT:     %[[SELF:[0-9a-zA-Z_.]+]] = struct.new : <@C>
// CHECK-NEXT:     %[[T0:[0-9a-zA-Z_.]+]] = felt.const 0
// CHECK-NEXT:     %[[T1:[0-9a-zA-Z_.]+]] = bool.cmp ne(%[[A0]], %[[T0]])
// CHECK-NEXT:     scf.if %[[T1]] {
// CHECK-NEXT:       struct.writef %[[SELF]][@data] = %[[A1]] : <@C>, !felt.felt
// CHECK-NEXT:     }
// CHECK-NEXT:     function.return %[[SELF]]
// CHECK-NEXT:   }
// CHECK-NEXT:   function.def @constrain(
// CHECK-SAME:        %[[SELF:[0-9a-zA-Z_.]+]]: !struct.struct<@C>,
// CHECK-SAME:        %[[A0:[0-9a-zA-Z_.]+]]: !felt.felt,
// CHECK-SAME:        %[[A1:[0-9a-zA-Z_.]+]]: !felt.felt) attributes {function.allow_constraint} {
// CHECK-NEXT:     function.return
// CHECK-NEXT:   }
// CHECK-NEXT: }

