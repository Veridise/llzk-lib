// RUN: llzk-opt %s -split-input-file -verify-diagnostics

function.def @only_one_index() -> index {
  %a = array.new : !array.array<2,3 x !felt.felt>
  %0 = arith.constant 0 : index
  // expected-error@+1 {{expected ':'}}
  %x = array.len %a, %0, %0 : !array.array<2,3 x !felt.felt>
  function.return %x: index
}
// -----

function.def @not_array_type_01(%a: !felt.felt) -> index { // expected-note {{prior use here}}
  %0 = arith.constant 0 : index
  // expected-error@+1 {{use of value '%a' expects different type than prior uses: '!array.array<2,3 x !felt.felt>' vs '!felt.felt'}}
  %x = array.len %a, %0 : !array.array<2,3 x !felt.felt>
  function.return %x: index
}
// -----

function.def @not_array_type_02(%a: !felt.felt) -> index {
  %0 = arith.constant 0 : index
  // expected-error@+1 {{custom op 'array.len' invalid kind of Type specified}}
  %x = array.len %a, %0 : !felt.felt
  function.return %x: index
}
// -----

function.def @wrong_result_type(%a: !array.array<2,3 x !felt.felt>) -> i1 {
  %0 = arith.constant 0 : index
  %x = array.len %a, %0 : !array.array<2,3 x !felt.felt> // expected-note {{prior use here}}
  // expected-error@+1 {{use of value '%x' expects different type than prior uses: 'i1' vs 'index'}}
  function.return %x: i1
}
// -----

function.def @wrong_index_type(%a: !array.array<2,3 x !felt.felt>) -> index {
  %0 = felt.const 0 // expected-note {{prior use here}}
  // expected-error@+1 {{use of value '%0' expects different type than prior uses: 'index' vs '!felt.felt'}}
  %x = array.len %a, %0 : !array.array<2,3 x !felt.felt>
  function.return %x: index
}
