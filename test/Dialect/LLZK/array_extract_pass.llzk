// RUN: llzk-opt -split-input-file %s 2>&1 | FileCheck --enable-var-scope %s

function.def @constant_index() -> !llzk.array<2 x index> {
  %a = arith.constant 45 : index
  %b = arith.constant 214 : index
  %c = arith.constant 2 : index
  %d = arith.constant 52 : index
  %r = array.new %a, %b, %c, %d : !llzk.array<2,2 x index>
  %i = arith.constant 1 : index
  %s = array.extract %r[%i] : !llzk.array<2,2 x index>
  function.return %s: !llzk.array<2 x index>
}
//CHECK-LABEL: function.def @constant_index() -> !llzk.array<2 x index> {
//CHECK-NEXT:    %[[T1:[0-9a-zA-Z_\.]+]] = arith.constant 45 : index
//CHECK-NEXT:    %[[T2:[0-9a-zA-Z_\.]+]] = arith.constant 214 : index
//CHECK-NEXT:    %[[T3:[0-9a-zA-Z_\.]+]] = arith.constant 2 : index
//CHECK-NEXT:    %[[T4:[0-9a-zA-Z_\.]+]] = arith.constant 52 : index
//CHECK-NEXT:    %[[T5:[0-9a-zA-Z_\.]+]] = array.new %[[T1]], %[[T2]], %[[T3]], %[[T4]] : <2,2 x index>
//CHECK-NEXT:    %[[T6:[0-9a-zA-Z_\.]+]] = arith.constant 1 : index
//CHECK-NEXT:    %[[T7:[0-9a-zA-Z_\.]+]] = array.extract %[[T5]][%[[T6]]] : <2,2 x index>
//CHECK-NEXT:    function.return %[[T7]] : !llzk.array<2 x index>
//CHECK-NEXT:  }
// -----

function.def @empty_index() -> !llzk.array<2,2 x index> {
  %a = arith.constant 45 : index
  %b = arith.constant 214 : index
  %c = arith.constant 2 : index
  %d = arith.constant 52 : index
  %r = array.new %a, %b, %c, %d : !llzk.array<2,2 x index>
  %s = array.extract %r[] : !llzk.array<2,2 x index>
  function.return %s: !llzk.array<2,2 x index>
}
//CHECK-LABEL: function.def @empty_index() -> !llzk.array<2,2 x index> {
//CHECK-NEXT:    %[[T1:[0-9a-zA-Z_\.]+]] = arith.constant 45 : index
//CHECK-NEXT:    %[[T2:[0-9a-zA-Z_\.]+]] = arith.constant 214 : index
//CHECK-NEXT:    %[[T3:[0-9a-zA-Z_\.]+]] = arith.constant 2 : index
//CHECK-NEXT:    %[[T4:[0-9a-zA-Z_\.]+]] = arith.constant 52 : index
//CHECK-NEXT:    %[[T5:[0-9a-zA-Z_\.]+]] = array.new %[[T1]], %[[T2]], %[[T3]], %[[T4]] : <2,2 x index>
//CHECK-NEXT:    %[[T7:[0-9a-zA-Z_\.]+]] = array.extract %[[T5]][] : <2,2 x index>
//CHECK-NEXT:    function.return %[[T7]] : !llzk.array<2,2 x index>
//CHECK-NEXT:  }
// -----

function.def @trival() -> !llzk.array<0 x index> {
  %r = array.new : !llzk.array<0 x index>
  %s = array.extract %r[] : !llzk.array<0 x index>
  function.return %s: !llzk.array<0 x index>
}
//CHECK-LABEL: function.def @trival() -> !llzk.array<0 x index> {
//CHECK-NEXT:    %[[T5:[0-9a-zA-Z_\.]+]] = array.new : <0 x index>
//CHECK-NEXT:    %[[T7:[0-9a-zA-Z_\.]+]] = array.extract %[[T5]][] : <0 x index>
//CHECK-NEXT:    function.return %[[T7]] : !llzk.array<0 x index>
//CHECK-NEXT:  }
// -----

module attributes {veridise.lang = "llzk"} {
  llzk.struct @Component01 {
    field @f : !llzk.array<2 x !llzk.felt>

    function.def @compute(%0: !llzk.array<2 x !llzk.felt>) -> !llzk.struct<@Component01> {
      %self = new_struct : !llzk.struct<@Component01>
      writef %self[@f] = %0 : !llzk.struct<@Component01>, !llzk.array<2 x !llzk.felt>
      function.return %self : !llzk.struct<@Component01>
    }
    function.def @constrain(%self: !llzk.struct<@Component01>, %0: !llzk.array<2 x !llzk.felt>) {
      %b = readf %self[@f] : !llzk.struct<@Component01>, !llzk.array<2 x !llzk.felt>
      emit_eq %0, %b : !llzk.array<2 x !llzk.felt>
      function.return
    }
  }

  llzk.struct @Component02<[@N]> {
    field @g : !llzk.array<@N x !llzk.struct<@Component01>>

    function.def @compute(%in: !llzk.array<@N,2 x !llzk.felt>) -> !llzk.struct<@Component02<[@N]>> {
      %self = new_struct : !llzk.struct<@Component02<[@N]>>
      %a = array.new : !llzk.array<@N x !llzk.struct<@Component01>>

      %lb = arith.constant 0 : index
      %up = read_const @N : index
      %step = arith.constant 1 : index
      scf.for %iv = %lb to %up step %step {
        %p = array.extract %in[%iv] : !llzk.array<@N,2 x !llzk.felt>
        %c = function.call @Component01::@compute(%p) : (!llzk.array<2 x !llzk.felt>) -> !llzk.struct<@Component01>
        array.write %a[%iv] = %c : !llzk.array<@N x !llzk.struct<@Component01>>, !llzk.struct<@Component01>
      }

      writef %self[@g] = %a : !llzk.struct<@Component02<[@N]>>, !llzk.array<@N x !llzk.struct<@Component01>>
      function.return %self : !llzk.struct<@Component02<[@N]>>
    }

    function.def @constrain(%self: !llzk.struct<@Component02<[@N]>>, %in: !llzk.array<@N,2 x !llzk.felt>) {
      %a = readf %self[@g] : !llzk.struct<@Component02<[@N]>>, !llzk.array<@N x !llzk.struct<@Component01>>

      %lb = arith.constant 0 : index
      %up = read_const @N : index
      %step = arith.constant 1 : index
      scf.for %iv = %lb to %up step %step {
        %p = array.extract %in[%iv] : !llzk.array<@N,2 x !llzk.felt>
        %c = array.read %a[%iv] : !llzk.array<@N x !llzk.struct<@Component01>>, !llzk.struct<@Component01>
        function.call @Component01::@constrain(%c, %p) : (!llzk.struct<@Component01>, !llzk.array<2 x !llzk.felt>) -> ()
      }
      function.return
    }
  }
}
//CHECK-LABEL: llzk.struct @Component01 {
//CHECK-NEXT:    field @f : !llzk.array<2 x !llzk.felt>
//CHECK-NEXT:    function.def @compute(%[[A0:[0-9a-zA-Z_\.]+]]: !llzk.array<2 x !llzk.felt>) -> !llzk.struct<@Component01> {
//CHECK-NEXT:      %[[SELF:[0-9a-zA-Z_\.]+]] = new_struct : <@Component01>
//CHECK-NEXT:      writef %[[SELF]][@f] = %[[A0]] : <@Component01>, !llzk.array<2 x !llzk.felt>
//CHECK-NEXT:      function.return %[[SELF]] : !llzk.struct<@Component01>
//CHECK-NEXT:    }
//CHECK-NEXT:    function.def @constrain(%[[SELF:[0-9a-zA-Z_\.]+]]: !llzk.struct<@Component01>, %[[A1:[0-9a-zA-Z_\.]+]]: !llzk.array<2 x !llzk.felt>) {
//CHECK-NEXT:      %[[T0:[0-9a-zA-Z_\.]+]] = readf %[[SELF]][@f] : <@Component01>, !llzk.array<2 x !llzk.felt>
//CHECK-NEXT:      emit_eq %[[A1]], %[[T0]] : !llzk.array<2 x !llzk.felt>, !llzk.array<2 x !llzk.felt>
//CHECK-NEXT:      function.return
//CHECK-NEXT:    }
//CHECK-NEXT:  }
//CHECK-LABEL: llzk.struct @Component02<[@N]> {
//CHECK-NEXT:    field @g : !llzk.array<@N x !llzk.struct<@Component01>>
//CHECK-NEXT:    function.def @compute(%[[A0:[0-9a-zA-Z_\.]+]]: !llzk.array<@N,2 x !llzk.felt>) -> !llzk.struct<@Component02<[@N]>> {
//CHECK-NEXT:      %[[SELF:[0-9a-zA-Z_\.]+]] = new_struct : <@Component02<[@N]>>
//CHECK-NEXT:      %array = array.new  : <@N x !llzk.struct<@Component01>>
//CHECK-NEXT:      %[[T3:[0-9a-zA-Z_\.]+]] = arith.constant 0 : index
//CHECK-NEXT:      %[[T0:[0-9a-zA-Z_\.]+]] = read_const @N : index
//CHECK-NEXT:      %[[T4:[0-9a-zA-Z_\.]+]] = arith.constant 1 : index
//CHECK-NEXT:      scf.for %[[IV:[0-9a-zA-Z_\.]+]] = %[[T3]] to %[[T0]] step %[[T4]] {
//CHECK-NEXT:        %[[T1:[0-9a-zA-Z_\.]+]] = array.extract %[[A0]][%[[IV]]] : <@N,2 x !llzk.felt>
//CHECK-NEXT:        %[[T2:[0-9a-zA-Z_\.]+]] = function.call @Component01::@compute(%[[T1]]) : (!llzk.array<2 x !llzk.felt>) -> !llzk.struct<@Component01>
//CHECK-NEXT:        array.write %array[%[[IV]]] = %[[T2]] : <@N x !llzk.struct<@Component01>>, !llzk.struct<@Component01>
//CHECK-NEXT:      }
//CHECK-NEXT:      writef %[[SELF]][@g] = %array : <@Component02<[@N]>>, !llzk.array<@N x !llzk.struct<@Component01>>
//CHECK-NEXT:      function.return %[[SELF]] : !llzk.struct<@Component02<[@N]>>
//CHECK-NEXT:    }
//CHECK-NEXT:    function.def @constrain(%[[SELF:[0-9a-zA-Z_\.]+]]: !llzk.struct<@Component02<[@N]>>, %[[A1:[0-9a-zA-Z_\.]+]]: !llzk.array<@N,2 x !llzk.felt>) {
//CHECK-NEXT:      %[[T0:[0-9a-zA-Z_\.]+]] = readf %[[SELF]][@g] : <@Component02<[@N]>>, !llzk.array<@N x !llzk.struct<@Component01>>
//CHECK-NEXT:      %[[T3:[0-9a-zA-Z_\.]+]] = arith.constant 0 : index
//CHECK-NEXT:      %[[T1:[0-9a-zA-Z_\.]+]] = read_const @N : index
//CHECK-NEXT:      %[[T4:[0-9a-zA-Z_\.]+]] = arith.constant 1 : index
//CHECK-NEXT:      scf.for %[[IV:[0-9a-zA-Z_\.]+]] = %[[T3]] to %[[T1]] step %[[T4]] {
//CHECK-NEXT:        %[[T5:[0-9a-zA-Z_\.]+]] = array.extract %[[A1]][%[[IV]]] : <@N,2 x !llzk.felt>
//CHECK-NEXT:        %[[T6:[0-9a-zA-Z_\.]+]] = array.read %[[T0]][%[[IV]]] : <@N x !llzk.struct<@Component01>>, !llzk.struct<@Component01>
//CHECK-NEXT:        function.call @Component01::@constrain(%[[T6]], %[[T5]]) : (!llzk.struct<@Component01>, !llzk.array<2 x !llzk.felt>) -> ()
//CHECK-NEXT:      }
//CHECK-NEXT:      function.return
//CHECK-NEXT:    }
//CHECK-NEXT:  }
