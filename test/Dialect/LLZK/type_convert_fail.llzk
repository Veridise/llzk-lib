// RUN: llzk-opt %s -split-input-file -verify-diagnostics

function.def @string_to_index(
    %a: !llzk.string // expected-note {{prior use here}}
  ) -> index {
  // expected-error@+1 {{use of value '%a' expects different type than prior uses: '!llzk.felt' vs '!llzk.string'}}
  %b = cast.toindex %a
  function.return %b: index
}
// -----

function.def @array_to_index(
    %a: !array.array<2 x !llzk.felt> // expected-note {{prior use here}}
  ) -> index {
  // expected-error@+1 {{use of value '%a' expects different type than prior uses: '!llzk.felt' vs '!array.array<2 x !llzk.felt>'}}
  %b = cast.toindex %a
  function.return %b: index
}
// -----

function.def @index_to_index(
    %a: index // expected-note {{prior use here}}
  ) -> index {
  // expected-error@+1 {{use of value '%a' expects different type than prior uses: '!llzk.felt' vs 'index'}}
  %b = cast.toindex %a
  function.return %b: index
}
// -----

function.def @i1_to_index(
    %a: i1 // expected-note {{prior use here}}
  ) -> index {
  // expected-error@+1 {{use of value '%a' expects different type than prior uses: '!llzk.felt' vs 'i1'}}
  %b = cast.toindex %a
  function.return %b: index
}
// -----

module attributes {veridise.lang = "llzk"} {
  llzk.struct @C1 {
    function.def @compute() -> !llzk.struct<@C1> {
      %self = new_struct : !llzk.struct<@C1>
      function.return %self : !llzk.struct<@C1>
    }
    function.def @constrain(%self: !llzk.struct<@C1>) { function.return }
  }
  function.def @struct_to_index(
        %a: !llzk.struct<@C1> // expected-note {{prior use here}}
    ) -> index {
    // expected-error@+1 {{use of value '%a' expects different type than prior uses: '!llzk.felt' vs '!llzk.struct<@C1>'}}
    %b = cast.toindex %a
    function.return %b: index
  }
}
// -----

function.def @string_to_felt(%a: !llzk.string) -> !llzk.felt {
  // expected-error@+1 {{'cast.tofelt' op operand #0 must be 1-bit signless integer or index, but got '!llzk.string'}}
  %b = cast.tofelt %a : !llzk.string
  function.return %b: !llzk.felt
}
// -----

function.def @array_to_felt(%a: !array.array<7 x i1>) -> !llzk.felt {
  // expected-error@+1 {{'cast.tofelt' op operand #0 must be 1-bit signless integer or index, but got '!array.array<7 x i1>'}}
  %b = cast.tofelt %a : !array.array<7 x i1>
  function.return %b: !llzk.felt
}
// -----

module attributes {veridise.lang = "llzk"} {
  llzk.struct @C2 {
    function.def @compute() -> !llzk.struct<@C2> {
      %self = new_struct : !llzk.struct<@C2>
      function.return %self : !llzk.struct<@C2>
    }
    function.def @constrain(%self: !llzk.struct<@C2>) { function.return }
  }
  function.def @struct_to_felt(%a: !llzk.struct<@C2>) -> !llzk.felt {
    // expected-error@+1 {{'cast.tofelt' op operand #0 must be 1-bit signless integer or index, but got '!llzk.struct<@C2>'}}
    %b = cast.tofelt %a : !llzk.struct<@C2>
    function.return %b: !llzk.felt
  }
}
// -----

function.def @check_type_restriction_on_pram(
    %a: i1 // expected-note {{prior use here}}
  ) -> !llzk.felt {
  // expected-error@+1 {{use of value '%a' expects different type than prior uses: 'index' vs 'i1'}}
  %b = cast.tofelt %a : index
  function.return %b: !llzk.felt
}
