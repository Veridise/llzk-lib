// RUN: llzk-opt -split-input-file -llzk-duplicate-read-write-elim %s 2>&1 | FileCheck %s

module attributes {veridise.lang = "llzk"} {

  struct.def @Subcomponent {
    function.def @compute() -> !struct.struct<@Subcomponent> {
      %self = struct.new : !struct.struct<@Subcomponent>
      function.return %self : !struct.struct<@Subcomponent>
    }
    function.def @constrain(%self: !struct.struct<@Subcomponent>) { function.return }
  }

  struct.def @Component {
    struct.field @sub1 : !struct.struct<@Subcomponent>
    struct.field @sub2 : !struct.struct<@Subcomponent>

    function.def @compute() -> !struct.struct<@Component> {
      %self = struct.new : !struct.struct<@Component>
      // All these reads are unused, so they will be removed
      %sub1 = struct.readf %self[@sub1] : !struct.struct<@Component>, !struct.struct<@Subcomponent>
      %sub2 = struct.readf %self[@sub2] : !struct.struct<@Component>, !struct.struct<@Subcomponent>
      %sub3 = struct.readf %self[@sub2] : !struct.struct<@Component>, !struct.struct<@Subcomponent>
      function.return %self : !struct.struct<@Component>
    }
    function.def @constrain(%self: !struct.struct<@Component>) { function.return }
  }
}

// CHECK-LABEL: struct.def @Subcomponent {
// CHECK-NEXT:    function.def @compute() -> !struct.struct<@Subcomponent> attributes {function.allow_witness} {
// CHECK-NEXT:      %[[VAL_0:.*]] = struct.new : <@Subcomponent>
// CHECK-NEXT:      function.return %[[VAL_0]] : !struct.struct<@Subcomponent>
// CHECK-NEXT:    }
// CHECK-NEXT:    function.def @constrain(%[[VAL_1:.*]]: !struct.struct<@Subcomponent>) attributes {function.allow_constraint} {
// CHECK-NEXT:      function.return
// CHECK-NEXT:    }
// CHECK-NEXT:  }

// CHECK-LABEL: struct.def @Component {
// CHECK-NEXT:    struct.field @sub1 : !struct.struct<@Subcomponent>
// CHECK-NEXT:    struct.field @sub2 : !struct.struct<@Subcomponent>
// CHECK-NEXT:    function.def @compute() -> !struct.struct<@Component> attributes {function.allow_witness} {
// CHECK-NEXT:      %[[VAL_0:.*]] = struct.new : <@Component>
// CHECK-NEXT:      function.return %[[VAL_0]] : !struct.struct<@Component>
// CHECK-NEXT:    }
// CHECK-NEXT:    function.def @constrain(%[[VAL_1:.*]]: !struct.struct<@Component>) attributes {function.allow_constraint} {
// CHECK-NEXT:      function.return
// CHECK-NEXT:    }
// CHECK-NEXT:  }

// -----

module attributes {veridise.lang = "llzk"} {
  struct.def @Component {
    struct.field @f : !felt.felt

    function.def @compute(%arg : !felt.felt) -> !struct.struct<@Component> {
      %self = struct.new : !struct.struct<@Component>
      struct.writef %self[@f] = %arg: !struct.struct<@Component>, !felt.felt
      // redundant write
      struct.writef %self[@f] = %arg: !struct.struct<@Component>, !felt.felt
      function.return %self : !struct.struct<@Component>
    }
    function.def @constrain(%self: !struct.struct<@Component>, %arg : !felt.felt) { function.return }
  }
}

// CHECK-LABEL: struct.def @Component {
// CHECK-NEXT:    struct.field @f : !felt.felt
// CHECK-NEXT:    function.def @compute(%[[VAL_0:.*]]: !felt.felt) -> !struct.struct<@Component> attributes {function.allow_witness} {
// CHECK-NEXT:      %[[VAL_1:.*]] = struct.new : <@Component>
// CHECK-NEXT:      struct.writef %[[VAL_1]][@f] = %[[VAL_0]] : <@Component>, !felt.felt
// CHECK-NEXT:      function.return %[[VAL_1]] : !struct.struct<@Component>
// CHECK-NEXT:    }
// CHECK-NEXT:    function.def @constrain(%[[VAL_2:.*]]: !struct.struct<@Component>, %[[VAL_3:.*]]: !felt.felt) attributes {function.allow_constraint} {
// CHECK-NEXT:      function.return
// CHECK-NEXT:    }
// CHECK-NEXT:  }

// -----

module attributes {veridise.lang = "llzk"} {
  struct.def @Component {
    struct.field @arr : !array.array<3 x !felt.felt>

    function.def @compute(%arg : !felt.felt) -> !struct.struct<@Component> {
      %self = struct.new : !struct.struct<@Component>
      %arr = struct.readf %self[@arr] : !struct.struct<@Component>, !array.array<3 x !felt.felt>
      %idx = arith.constant 1 : index
      array.write %arr[%idx] = %arg : !array.array<3 x !felt.felt>, !felt.felt
      %newarr = array.new %arg, %arg, %arg : !array.array<3 x !felt.felt> // ends up being unused after removing the below ops, can also be removed
      %elem = array.read %newarr[%idx] : !array.array<3 x !felt.felt>, !felt.felt // this is just %arg, redundant
      array.write %arr[%idx] = %elem : !array.array<3 x !felt.felt>, !felt.felt // redundant write, this idx is already %arg
      function.return %self : !struct.struct<@Component>
    }
    function.def @constrain(%self: !struct.struct<@Component>, %arg : !felt.felt) { function.return }
  }
}

// CHECK-LABEL: struct.def @Component {
// CHECK-NEXT:    struct.field @arr : !array.array<3 x !felt.felt>
// CHECK-NEXT:    function.def @compute(%[[VAL_0:.*]]: !felt.felt) -> !struct.struct<@Component> attributes {function.allow_witness} {
// CHECK-NEXT:      %[[VAL_1:.*]] = struct.new : <@Component>
// CHECK-NEXT:      %[[VAL_2:.*]] = struct.readf %[[VAL_1]][@arr] : <@Component>, !array.array<3 x !felt.felt>
// CHECK-NEXT:      %[[VAL_3:.*]] = arith.constant 1 : index
// CHECK-NEXT:      array.write %[[VAL_2]]{{\[}}%[[VAL_3]]] = %[[VAL_0]] : <3 x !felt.felt>, !felt.felt
// CHECK-NEXT:      function.return %[[VAL_1]] : !struct.struct<@Component>
// CHECK-NEXT:    }
// CHECK-NEXT:    function.def @constrain(%[[VAL_4:.*]]: !struct.struct<@Component>, %[[VAL_5:.*]]: !felt.felt) attributes {function.allow_constraint} {
// CHECK-NEXT:      function.return
// CHECK-NEXT:    }
// CHECK-NEXT:  }

// -----

module attributes {veridise.lang = "llzk"} {
  struct.def @Component {
    struct.field @matrix : !array.array<7,3 x !felt.felt>

    function.def @compute(%inputArr : !array.array<3 x !felt.felt>) -> !struct.struct<@Component> {
      %self = struct.new : !struct.struct<@Component>
      %mat = struct.readf %self[@matrix] : !struct.struct<@Component>, !array.array<7,3 x !felt.felt>
      %idx1 = arith.constant 1 : index
      %idx4 = arith.constant 4 : index
      array.insert %mat[%idx1] = %inputArr : !array.array<7,3 x !felt.felt>, !array.array<3 x !felt.felt>
      %arr1 = array.extract %mat[%idx1] : !array.array<7,3 x !felt.felt> // redundant, replace with %inputArr
      array.insert %mat[%idx4] = %arr1 : !array.array<7,3 x !felt.felt>, !array.array<3 x !felt.felt> // replace %arr1 with %inputArr
      array.insert %mat[%idx1] = %arr1 : !array.array<7,3 x !felt.felt>, !array.array<3 x !felt.felt> // redundant
      function.return %self : !struct.struct<@Component>
    }
    function.def @constrain(%self: !struct.struct<@Component>, %inputArr : !array.array<3 x !felt.felt>) {
      %mat = struct.readf %self[@matrix] : !struct.struct<@Component>, !array.array<7,3 x !felt.felt>
      %i = arith.constant 2 : index
      %j = arith.constant 3 : index
      %k = arith.constant 3 : index // redundant constant
      %arr_i = array.extract %mat[%i] : !array.array<7,3 x !felt.felt>
      %arr_j = array.extract %mat[%j] : !array.array<7,3 x !felt.felt>
      %arr_k = array.extract %mat[%k] : !array.array<7,3 x !felt.felt> // redundant extract
      constrain.eq %arr_i, %arr_j : !array.array<3 x !felt.felt>
      constrain.eq %arr_j, %arr_k : !array.array<3 x !felt.felt> // this constraint is redundant, but will not be removed in this pass
      constrain.eq %arr_k, %arr_i : !array.array<3 x !felt.felt> // same with above
      function.return
    }
  }
}

// CHECK-LABEL: struct.def @Component {
// CHECK-NEXT:    struct.field @matrix : !array.array<7,3 x !felt.felt>
// CHECK-NEXT:    function.def @compute(%[[VAL_0:.*]]: !array.array<3 x !felt.felt>) -> !struct.struct<@Component> attributes {function.allow_witness} {
// CHECK-NEXT:      %[[VAL_1:.*]] = struct.new : <@Component>
// CHECK-NEXT:      %[[VAL_2:.*]] = struct.readf %[[VAL_1]][@matrix] : <@Component>, !array.array<7,3 x !felt.felt>
// CHECK-NEXT:      %[[VAL_3:.*]] = arith.constant 1 : index
// CHECK-NEXT:      %[[VAL_4:.*]] = arith.constant 4 : index
// CHECK-NEXT:      array.insert %[[VAL_2]]{{\[}}%[[VAL_3]]] = %[[VAL_0]] : <7,3 x !felt.felt>, <3 x !felt.felt>
// CHECK-NEXT:      array.insert %[[VAL_2]]{{\[}}%[[VAL_4]]] = %[[VAL_0]] : <7,3 x !felt.felt>, <3 x !felt.felt>
// CHECK-NEXT:      function.return %[[VAL_1]] : !struct.struct<@Component>
// CHECK-NEXT:    }
// CHECK-NEXT:    function.def @constrain(%[[VAL_5:.*]]: !struct.struct<@Component>, %[[VAL_6:.*]]: !array.array<3 x !felt.felt>) attributes {function.allow_constraint} {
// CHECK-NEXT:      %[[VAL_7:.*]] = struct.readf %[[VAL_5]][@matrix] : <@Component>, !array.array<7,3 x !felt.felt>
// CHECK-NEXT:      %[[VAL_8:.*]] = arith.constant 2 : index
// CHECK-NEXT:      %[[VAL_9:.*]] = arith.constant 3 : index
// CHECK-NEXT:      %[[VAL_10:.*]] = arith.constant 3 : index
// CHECK-NEXT:      %[[VAL_11:.*]] = array.extract %[[VAL_7]]{{\[}}%[[VAL_8]]] : <7,3 x !felt.felt>
// CHECK-NEXT:      %[[VAL_12:.*]] = array.extract %[[VAL_7]]{{\[}}%[[VAL_9]]] : <7,3 x !felt.felt>
// CHECK-NEXT:      constrain.eq %[[VAL_11]], %[[VAL_12]] : !array.array<3 x !felt.felt>, !array.array<3 x !felt.felt>
// CHECK-NEXT:      constrain.eq %[[VAL_12]], %[[VAL_12]] : !array.array<3 x !felt.felt>, !array.array<3 x !felt.felt>
// CHECK-NEXT:      constrain.eq %[[VAL_12]], %[[VAL_11]] : !array.array<3 x !felt.felt>, !array.array<3 x !felt.felt>
// CHECK-NEXT:      function.return
// CHECK-NEXT:    }
// CHECK-NEXT:  }

// -----

module attributes {veridise.lang = "llzk"} {
  struct.def @Component {
    struct.field @arr : !array.array<3 x !felt.felt>

    function.def @compute(%arg : !felt.felt, %inputIdx : index) -> !struct.struct<@Component> {
      %self = struct.new : !struct.struct<@Component>
      %arr = struct.readf %self[@arr] : !struct.struct<@Component>, !array.array<3 x !felt.felt>
      %idx = arith.constant 1 : index
      %idx2 = arith.constant 2 : index
      array.write %arr[%idx] = %arg : !array.array<3 x !felt.felt>, !felt.felt
      array.write %arr[%inputIdx] = %arg : !array.array<3 x !felt.felt>, !felt.felt
      // this read is necessary in the case that %inputIdx == %idx
      %elem1 = array.read %arr[%idx] : !array.array<3 x !felt.felt>, !felt.felt
      %elem2 = array.read %arr[%inputIdx] : !array.array<3 x !felt.felt>, !felt.felt
      array.write %arr[%idx] = %elem2 : !array.array<3 x !felt.felt>, !felt.felt
      array.write %arr[%idx2] = %elem1 : !array.array<3 x !felt.felt>, !felt.felt
      function.return %self : !struct.struct<@Component>
    }
    function.def @constrain(%self: !struct.struct<@Component>, %arg : !felt.felt, %inputIdx : index) { function.return }
  }
}

// CHECK-LABEL: struct.def @Component {
// CHECK-NEXT:    struct.field @arr : !array.array<3 x !felt.felt>
// CHECK-NEXT:    function.def @compute(%[[VAL_0:.*]]: !felt.felt, %[[VAL_1:.*]]: index) -> !struct.struct<@Component> attributes {function.allow_witness} {
// CHECK-NEXT:      %[[VAL_2:.*]] = struct.new : <@Component>
// CHECK-NEXT:      %[[VAL_3:.*]] = struct.readf %[[VAL_2]][@arr] : <@Component>, !array.array<3 x !felt.felt>
// CHECK-NEXT:      %[[VAL_4:.*]] = arith.constant 1 : index
// CHECK-NEXT:      %[[VAL_5:.*]] = arith.constant 2 : index
// CHECK-NEXT:      array.write %[[VAL_3]]{{\[}}%[[VAL_4]]] = %[[VAL_0]] : <3 x !felt.felt>, !felt.felt
// CHECK-NEXT:      array.write %[[VAL_3]]{{\[}}%[[VAL_1]]] = %[[VAL_0]] : <3 x !felt.felt>, !felt.felt
// CHECK-NEXT:      %[[VAL_6:.*]] = array.read %[[VAL_3]]{{\[}}%[[VAL_4]]] : <3 x !felt.felt>, !felt.felt
// CHECK-NEXT:      array.write %[[VAL_3]]{{\[}}%[[VAL_4]]] = %[[VAL_0]] : <3 x !felt.felt>, !felt.felt
// CHECK-NEXT:      array.write %[[VAL_3]]{{\[}}%[[VAL_5]]] = %[[VAL_6]] : <3 x !felt.felt>, !felt.felt
// CHECK-NEXT:      function.return %[[VAL_2]] : !struct.struct<@Component>
// CHECK-NEXT:    }
// CHECK-NEXT:    function.def @constrain(%[[VAL_7:.*]]: !struct.struct<@Component>, %[[VAL_8:.*]]: !felt.felt, %[[VAL_9:.*]]: index) attributes {function.allow_constraint} {
// CHECK-NEXT:      function.return
// CHECK-NEXT:    }
// CHECK-NEXT:  }

// -----

module attributes {veridise.lang = "llzk"} {
  // A modified version of IsZero that includes some redundancy
  struct.def @IsZero {
    struct.field @bit : !felt.felt
    struct.field @zero : !felt.felt
    struct.field @one : !felt.felt

    function.def @compute(%a: !felt.felt) -> !struct.struct<@IsZero> {
      %self = struct.new : !struct.struct<@IsZero>
      %z = felt.const 0
      %o = felt.const 1
      struct.writef %self[@zero] = %z : !struct.struct<@IsZero>, !felt.felt
      struct.writef %self[@one] = %o : !struct.struct<@IsZero>, !felt.felt
      %b = llzk.cmp eq(%a, %z)
      %0 = scf.if %b -> !felt.felt {
        %z1 = struct.readf %self[@zero] : !struct.struct<@IsZero>, !felt.felt
        scf.yield %z1 : !felt.felt
      } else {
        %o1 = struct.readf %self[@one] : !struct.struct<@IsZero>, !felt.felt
        scf.yield %o1 : !felt.felt
      }
      struct.writef %self[@bit] = %0 : !struct.struct<@IsZero>, !felt.felt
      function.return %self : !struct.struct<@IsZero>
    }

    function.def @constrain(%self : !struct.struct<@IsZero>, %a: !felt.felt) {
      %0 = struct.readf %self[@bit] : !struct.struct<@IsZero>, !felt.felt
      %1 = felt.const 1
      %2 = felt.sub %0, %1
      %3 = felt.mul %0, %2
      %4 = felt.const 0
      llzk.emit_eq %3, %4 : !felt.felt
      %z1 = struct.readf %self[@zero] : !struct.struct<@IsZero>, !felt.felt
      %o1 = struct.readf %self[@one] : !struct.struct<@IsZero>, !felt.felt
      llzk.emit_eq %z1, %4 : !felt.felt
      llzk.emit_eq %o1, %1 : !felt.felt
      function.return
    }
  }
}

// CHECK-LABEL: struct.def @IsZero {
// CHECK-NEXT:    struct.field @bit : !felt.felt
// CHECK-NEXT:    struct.field @zero : !felt.felt
// CHECK-NEXT:    struct.field @one : !felt.felt
// CHECK-NEXT:    function.def @compute(%[[VAL_0:.*]]: !felt.felt) -> !struct.struct<@IsZero> attributes {function.allow_witness} {
// CHECK-NEXT:      %[[VAL_1:.*]] = struct.new : <@IsZero>
// CHECK-NEXT:      %[[VAL_2:.*]] = felt.const  0
// CHECK-NEXT:      %[[VAL_3:.*]] = felt.const  1
// CHECK-NEXT:      struct.writef %[[VAL_1]][@zero] = %[[VAL_2]] : <@IsZero>, !felt.felt
// CHECK-NEXT:      struct.writef %[[VAL_1]][@one] = %[[VAL_3]] : <@IsZero>, !felt.felt
// CHECK-NEXT:      %[[VAL_4:.*]] = llzk.cmp eq(%[[VAL_0]], %[[VAL_2]])
// CHECK-NEXT:      %[[VAL_5:.*]] = scf.if %[[VAL_4]] -> (!felt.felt) {
// CHECK-NEXT:        scf.yield %[[VAL_2]] : !felt.felt
// CHECK-NEXT:      } else {
// CHECK-NEXT:        scf.yield %[[VAL_3]] : !felt.felt
// CHECK-NEXT:      }
// CHECK-NEXT:      struct.writef %[[VAL_1]][@bit] = %[[VAL_5]] : <@IsZero>, !felt.felt
// CHECK-NEXT:      function.return %[[VAL_1]] : !struct.struct<@IsZero>
// CHECK-NEXT:    }
// CHECK-NEXT:    function.def @constrain(%[[VAL_6:.*]]: !struct.struct<@IsZero>, %[[VAL_7:.*]]: !felt.felt) attributes {function.allow_constraint} {
// CHECK-NEXT:      %[[VAL_8:.*]] = struct.readf %[[VAL_6]][@bit] : <@IsZero>, !felt.felt
// CHECK-NEXT:      %[[VAL_9:.*]] = felt.const  1
// CHECK-NEXT:      %[[VAL_10:.*]] = felt.sub %[[VAL_8]], %[[VAL_9]] : !felt.felt, !felt.felt
// CHECK-NEXT:      %[[VAL_11:.*]] = felt.mul %[[VAL_8]], %[[VAL_10]] : !felt.felt, !felt.felt
// CHECK-NEXT:      %[[VAL_12:.*]] = felt.const  0
// CHECK-NEXT:      llzk.emit_eq %[[VAL_11]], %[[VAL_12]] : !felt.felt, !felt.felt
// CHECK-NEXT:      %[[VAL_13:.*]] = struct.readf %[[VAL_6]][@zero] : <@IsZero>, !felt.felt
// CHECK-NEXT:      %[[VAL_14:.*]] = struct.readf %[[VAL_6]][@one] : <@IsZero>, !felt.felt
// CHECK-NEXT:      llzk.emit_eq %[[VAL_13]], %[[VAL_12]] : !felt.felt, !felt.felt
// CHECK-NEXT:      llzk.emit_eq %[[VAL_14]], %[[VAL_9]] : !felt.felt, !felt.felt
// CHECK-NEXT:      function.return
// CHECK-NEXT:    }
// CHECK-NEXT:  }

// -----

module attributes {veridise.lang = "llzk"} {
  struct.def @Component {
    struct.field @matrix : !array.array<7,3 x !felt.felt>

    function.def @compute(%inputArr : !array.array<3 x !felt.felt>, %cond : i1) -> !struct.struct<@Component> {
      %self = struct.new : !struct.struct<@Component>
      %mat = struct.readf %self[@matrix] : !struct.struct<@Component>, !array.array<7,3 x !felt.felt>
      %idx1 = arith.constant 1 : index
      %idx4 = arith.constant 4 : index

      scf.if %cond {
        array.insert %mat[%idx1] = %inputArr : !array.array<7,3 x !felt.felt>, !array.array<3 x !felt.felt>
      } else {
        %arr1 = array.extract %mat[%idx1] : !array.array<7,3 x !felt.felt> // This load is necessary, as the value is unknown in this branch
        array.insert %mat[%idx4] = %arr1 : !array.array<7,3 x !felt.felt>, !array.array<3 x !felt.felt>
        array.insert %mat[%idx1] = %arr1 : !array.array<7,3 x !felt.felt>, !array.array<3 x !felt.felt> // this store is still unnecessary
      }

      function.return %self : !struct.struct<@Component>
    }
    function.def @constrain(%self: !struct.struct<@Component>, %inputArr : !array.array<3 x !felt.felt>, %cond : i1) {
      function.return
    }
  }
}

// CHECK-LABEL: struct.def @Component {
// CHECK-NEXT:    struct.field @matrix : !array.array<7,3 x !felt.felt>
// CHECK-NEXT:    function.def @compute(%[[VAL_0:.*]]: !array.array<3 x !felt.felt>, %[[VAL_1:.*]]: i1) -> !struct.struct<@Component> attributes {function.allow_witness} {
// CHECK-NEXT:      %[[VAL_2:.*]] = struct.new : <@Component>
// CHECK-NEXT:      %[[VAL_3:.*]] = struct.readf %[[VAL_2]][@matrix] : <@Component>, !array.array<7,3 x !felt.felt>
// CHECK-NEXT:      %[[VAL_4:.*]] = arith.constant 1 : index
// CHECK-NEXT:      %[[VAL_5:.*]] = arith.constant 4 : index
// CHECK-NEXT:      scf.if %[[VAL_1]] {
// CHECK-NEXT:        array.insert %[[VAL_3]]{{\[}}%[[VAL_4]]] = %[[VAL_0]] : <7,3 x !felt.felt>, <3 x !felt.felt>
// CHECK-NEXT:      } else {
// CHECK-NEXT:        %[[VAL_6:.*]] = array.extract %[[VAL_3]]{{\[}}%[[VAL_4]]] : <7,3 x !felt.felt>
// CHECK-NEXT:        array.insert %[[VAL_3]]{{\[}}%[[VAL_5]]] = %[[VAL_6]] : <7,3 x !felt.felt>, <3 x !felt.felt>
// CHECK-NEXT:      }
// CHECK-NEXT:      function.return %[[VAL_2]] : !struct.struct<@Component>
// CHECK-NEXT:    }
// CHECK-NEXT:    function.def @constrain(%[[VAL_7:.*]]: !struct.struct<@Component>, %[[VAL_8:.*]]: !array.array<3 x !felt.felt>, %[[VAL_9:.*]]: i1) attributes {function.allow_constraint} {
// CHECK-NEXT:      function.return
// CHECK-NEXT:    }
// CHECK-NEXT:  }
