// RUN: llzk-opt -I %S -split-input-file -llzk-inline-includes -llzk-print-interval-analysis %s 2>&1 | FileCheck %s

module attributes {veridise.lang = "llzk"} {
  struct.def @ConstantConstraint {
    function.def @compute(%a: !felt.felt) -> !struct.type<@ConstantConstraint> {
      %self = struct.new : !struct.type<@ConstantConstraint>
      function.return %self : !struct.type<@ConstantConstraint>
    }

    function.def @constrain(%self: !struct.type<@ConstantConstraint>, %const: !felt.felt) {
      %a = felt.const 1
      %b = felt.const 2
      %c = felt.add %a, %b
      constrain.eq %const, %c : !felt.felt
      function.return
    }
  }
}

// CHECK-LABEL: @ConstantConstraint StructIntervals {
// CHECK-NEXT:     %arg1 in Degenerate(3)
// CHECK-NEXT: }

// -----

module attributes {veridise.lang = "llzk"} {
  struct.def @CmpConstraint {
    function.def @compute(%a: !felt.felt, %b: !felt.felt) -> !struct.type<@CmpConstraint> {
      %self = struct.new : !struct.type<@CmpConstraint>
      function.return %self : !struct.type<@CmpConstraint>
    }

    function.def @constrain(%self: !struct.type<@CmpConstraint>, %a: !felt.felt, %b: !felt.felt) {
      %x = felt.const 1
      %y = felt.const 2
      %cmpA = bool.cmp gt(%a, %x)
      bool.assert %cmpA
      %cmpB = bool.cmp le(%b, %y)
      bool.assert %cmpB
      function.return
    }
  }
}

// CHECK-LABEL: @CmpConstraint StructIntervals {
// CHECK-NEXT:     %arg1 in TypeC:[ 2, 21888242871839275222246405745257275088696311157297823662689037894645226208582 ]
// CHECK-NEXT:     %arg2 in TypeA:[ 0, 2 ]
// CHECK-NEXT: }

// -----

module attributes {veridise.lang = "llzk"} {
  struct.def @FieldRangeConstraint {

    struct.field @foo : !felt.felt
    struct.field @bar : !felt.felt

    function.def @compute(%a: !felt.felt, %b: !felt.felt) -> !struct.type<@FieldRangeConstraint> {
      %self = struct.new : !struct.type<@FieldRangeConstraint>
      function.return %self : !struct.type<@FieldRangeConstraint>
    }

    function.def @constrain(%self: !struct.type<@FieldRangeConstraint>, %a: !felt.felt, %b: !felt.felt) {
      %x = felt.const 1
      %y = felt.const 2
      %cmpA = bool.cmp gt(%a, %x)
      bool.assert %cmpA
      %cmpB = bool.cmp le(%b, %y)
      bool.assert %cmpB
      %foo = struct.readf %self[@foo] : !struct.type<@FieldRangeConstraint>, !felt.felt
      constrain.eq %foo, %x : !felt.felt
      %bar = struct.readf %self[@bar] : !struct.type<@FieldRangeConstraint>, !felt.felt
      constrain.eq %bar, %b : !felt.felt
      function.return
    }
  }
}

// CHECK-LABEL: @FieldRangeConstraint StructIntervals {
// CHECK-NEXT:     %arg0[@foo] in Degenerate(1)
// CHECK-NEXT:     %arg0[@bar] in TypeA:[ 0, 2 ]
// CHECK-NEXT:     %arg1 in TypeC:[ 2, 21888242871839275222246405745257275088696311157297823662689037894645226208582 ]
// CHECK-NEXT:     %arg2 in TypeA:[ 0, 2 ]
// CHECK-NEXT: }

// -----

module attributes {veridise.lang = "llzk"} {
  struct.def @ArithConstraint {

    function.def @compute(%a: !felt.felt, %b: !felt.felt) -> !struct.type<@ArithConstraint> {
      %self = struct.new : !struct.type<@ArithConstraint>
      function.return %self : !struct.type<@ArithConstraint>
    }

    function.def @constrain(%self: !struct.type<@ArithConstraint>, %a: !felt.felt, %b: !felt.felt) {
      %5 = felt.const 5
      %7 = felt.const 7
      %11 = felt.const 11
      %cmp1 = bool.cmp ge(%a, %5)
      bool.assert %cmp1
      %cmp2 = bool.cmp le(%a, %7)
      bool.assert %cmp2
      %cmp3 = bool.cmp gt(%b, %7)
      bool.assert %cmp3
      %cmp4 = bool.cmp le(%b, %11)
      bool.assert %cmp4

      %mul = felt.mul %a, %5
      %sub = felt.sub %mul, %5
      %1 = felt.div %11, %11
      %mod = felt.mod %sub, %b
      %neg = felt.neg %mod
      %final = felt.add %mul, %neg

      %0 = felt.const 0
      %notzero = bool.cmp ne(%final, %0)
      bool.assert %notzero

      function.return
    }
  }
}

// CHECK-LABEL: @ArithConstraint StructIntervals {
// CHECK-NEXT:     %arg1 in TypeA:[ 5, 7 ]
// CHECK-NEXT:     %arg2 in TypeA:[ 8, 11 ]
// CHECK-NEXT: }

// -----

module attributes {veridise.lang = "llzk"} {
  struct.def @ArrayConstraint {

    struct.field @arr : !array.type<3 x !felt.felt>

    function.def @compute() -> !struct.type<@ArrayConstraint> {
      %self = struct.new : !struct.type<@ArrayConstraint>
      function.return %self : !struct.type<@ArrayConstraint>
    }

    function.def @constrain(%self: !struct.type<@ArrayConstraint>) {
      %5 = felt.const 5
      %7 = felt.const 7
      %11 = felt.const 11
      %arr = struct.readf %self[@arr] : !struct.type<@ArrayConstraint>, !array.type<3 x !felt.felt>
      %i0 = arith.constant 0 : index
      %i1 = arith.constant 1 : index
      %i2 = arith.constant 2 : index
      %a = array.read %arr[%i0] : !array.type<3 x !felt.felt>, !felt.felt
      %b = array.read %arr[%i1] : !array.type<3 x !felt.felt>, !felt.felt
      %c = array.read %arr[%i2] : !array.type<3 x !felt.felt>, !felt.felt
      %cmp1 = bool.cmp ge(%a, %5)
      bool.assert %cmp1
      %cmp2 = bool.cmp le(%a, %7)
      bool.assert %cmp2
      %cmp3 = bool.cmp ge(%b, %7)
      bool.assert %cmp3
      %cmp4 = bool.cmp le(%b, %11)
      bool.assert %cmp4

      constrain.eq %c, %a : !felt.felt

      function.return
    }
  }
}

// CHECK-LABEL: @ArrayConstraint StructIntervals {
// CHECK-NEXT:     %arg0[@arr][0] in TypeA:[ 5, 7 ]
// CHECK-NEXT:     %arg0[@arr][1] in TypeA:[ 7, 11 ]
// CHECK-NEXT:     %arg0[@arr][2] in TypeA:[ 5, 7 ]
// CHECK-NEXT: }

// -----

module attributes {veridise.lang = "llzk"} {
  struct.def @Div<[]> {
    struct.field @"$super" : !felt.felt
    struct.field @reciprocal : !felt.felt
    function.def @compute(%arg0: !felt.felt, %arg1: !felt.felt) -> !struct.type<@Div<[]>> {
      %self = struct.new : <@Div<[]>>
      %0 = felt.inv %arg1 : !felt.felt
      struct.writef %self[@reciprocal] = %0 : <@Div<[]>>, !felt.felt
      %1 = struct.readf %self[@reciprocal] : <@Div<[]>>, !felt.felt
      %2 = felt.mul %1, %arg1 : !felt.felt, !felt.felt
      %felt_const_1 = felt.const  1
      %3 = felt.mul %1, %arg0 : !felt.felt, !felt.felt
      struct.writef %self[@"$super"] = %3 : <@Div<[]>>, !felt.felt
      function.return %self : !struct.type<@Div<[]>>
    }
    function.def @constrain(%arg0: !struct.type<@Div<[]>>, %arg1: !felt.felt, %arg2: !felt.felt) {
      %0 = struct.readf %arg0[@reciprocal] : <@Div<[]>>, !felt.felt
      %1 = felt.mul %0, %arg2 : !felt.felt, !felt.felt
      %felt_const_1 = felt.const  1
      // Since reciprocal * arg2 === 1, reciprocal nor arg2 can be 0
      constrain.eq %1, %felt_const_1 : !felt.felt, !felt.felt
      function.return
    }
  }
}

// CHECK-LABEL: @Div StructIntervals {
// CHECK-NEXT:     %arg0[@$super] in Entire
// CHECK-NEXT:     %arg0[@reciprocal] in TypeC:[ 1, 21888242871839275222246405745257275088696311157297823662689037894645226208582 ]
// CHECK-NEXT:     %arg1 in Entire
// CHECK-NEXT:     %arg2 in TypeC:[ 1, 21888242871839275222246405745257275088696311157297823662689037894645226208582 ]
// CHECK-NEXT: }

// -----

module attributes {veridise.lang = "llzk"} {
  // A correctly constrained byte decomposition checker for a 16 bit val.
  struct.def @ByteDecompCorrect {
    struct.field @b1 : !felt.felt
    struct.field @b0 : !felt.felt

    function.def @compute(%u16 : !felt.felt) -> !struct.type<@ByteDecompCorrect> {
      %self = struct.new : !struct.type<@ByteDecompCorrect>
      function.return %self : !struct.type<@ByteDecompCorrect>
    }

    function.def @constrain(%self : !struct.type<@ByteDecompCorrect>, %u16 : !felt.felt) {
      %b0 = struct.readf %self[@b0] : !struct.type<@ByteDecompCorrect>, !felt.felt
      %b1 = struct.readf %self[@b1] : !struct.type<@ByteDecompCorrect>, !felt.felt
      %256 = felt.const 256
      %b0_is_byte = bool.cmp lt(%b0, %256)
      %b1_is_byte = bool.cmp lt(%b1, %256)
      bool.assert %b0_is_byte
      bool.assert %b1_is_byte
      %b1_mul = felt.mul %b1, %256
      %reconstructed = felt.add %b1_mul, %b0
      constrain.eq %reconstructed, %u16 : !felt.felt
      function.return
    }
  }

  // An incorrectly constrained byte decomposition checker for a 16 bit val.
  struct.def @ByteDecompIncorrect {
    struct.field @b1 : !felt.felt
    struct.field @b0 : !felt.felt

    function.def @compute(%u16 : !felt.felt) -> !struct.type<@ByteDecompIncorrect> {
      %self = struct.new : !struct.type<@ByteDecompIncorrect>
      function.return %self : !struct.type<@ByteDecompIncorrect>
    }

    function.def @constrain(%self : !struct.type<@ByteDecompIncorrect>, %u16 : !felt.felt) {
      %b0 = struct.readf %self[@b0] : !struct.type<@ByteDecompIncorrect>, !felt.felt
      %b1 = struct.readf %self[@b1] : !struct.type<@ByteDecompIncorrect>, !felt.felt
      %256 = felt.const 256
      %b1_mul = felt.mul %b1, %256
      %reconstructed = felt.add %b1_mul, %b0
      // b0 and b1 are not constrained to be a byte, so these values could be anything
      constrain.eq %reconstructed, %u16 : !felt.felt
      function.return
    }
  }

  // Another improperly constrained byte decomposition checker for a 16 bit val.
  struct.def @ByteDecompPartial {
    struct.field @b1 : !felt.felt
    struct.field @b0 : !felt.felt

    function.def @compute(%u16 : !felt.felt) -> !struct.type<@ByteDecompPartial> {
      %self = struct.new : !struct.type<@ByteDecompPartial>
      function.return %self : !struct.type<@ByteDecompPartial>
    }

    function.def @constrain(%self : !struct.type<@ByteDecompPartial>, %u16 : !felt.felt) {
      %b0 = struct.readf %self[@b0] : !struct.type<@ByteDecompPartial>, !felt.felt
      %b1 = struct.readf %self[@b1] : !struct.type<@ByteDecompPartial>, !felt.felt
      %256 = felt.const 256
      %u16upper = felt.mul %256, %256
      %is_u16 = bool.cmp lt(%u16, %u16upper)
      bool.assert %is_u16
      %b1_mul = felt.mul %b1, %256
      %reconstructed = felt.add %b1_mul, %b0
      // u16 is constrained to be 16 bites, but b0 and b1 aren't guaranteed to
      // by bytes, so they could be any value (even above 16-bits, if b0 and b1)
      // are carefully selected to overflow into 16 bits).
      constrain.eq %reconstructed, %u16 : !felt.felt
      function.return
    }
  }
}

// CHECK-LABEL: @ByteDecompCorrect StructIntervals {
// CHECK-NEXT:     %arg0[@b1] in TypeA:[ 0, 255 ]
// CHECK-NEXT:     %arg0[@b0] in TypeA:[ 0, 255 ]
// CHECK-NEXT:     %arg1 in TypeA:[ 0, 65535 ]
// CHECK-NEXT: }
// CHECK-NEXT: @ByteDecompIncorrect StructIntervals {
// CHECK-NEXT:     %arg0[@b1] in Entire
// CHECK-NEXT:     %arg0[@b0] in Entire
// CHECK-NEXT:     %arg1 in Entire
// CHECK-NEXT: }
// CHECK-NEXT: @ByteDecompPartial StructIntervals {
// CHECK-NEXT:     %arg0[@b1] in Entire
// CHECK-NEXT:     %arg0[@b0] in Entire
// CHECK-NEXT:     %arg1 in TypeA:[ 0, 65535 ]
// CHECK-NEXT: }

// -----

// Ensure constraints on Signals or on the Signal's internal reg propagate.
!Signal = !struct.type<@Signal>
module attributes {veridise.lang = "llzk"} {
  // pre-defined
  struct.def @Signal {
    struct.field @reg : !felt.felt {llzk.pub}

    function.def @compute(%v: !felt.felt) -> !struct.type<@Signal> {
      %self = struct.new : !struct.type<@Signal>
      struct.writef %self[@reg] = %v : !struct.type<@Signal>, !felt.felt
      function.return %self : !struct.type<@Signal>
    }

    function.def @constrain(%self: !struct.type<@Signal>, %v: !felt.felt) {
      function.return
    }
  }

  struct.def @Component00 {
    struct.field @f : !Signal

    function.def @compute(%0: !Signal) -> !struct.type<@Component00> {
      %self = struct.new : !struct.type<@Component00>
      struct.writef %self[@f] = %0 : !struct.type<@Component00>, !Signal
      function.return %self : !struct.type<@Component00>
    }
    function.def @constrain(%self: !struct.type<@Component00>, %0: !Signal) {
      %b = struct.readf %self[@f] : !struct.type<@Component00>, !Signal
      constrain.eq %b, %0 : !Signal
      %reg = struct.readf %0[@reg] : !Signal, !felt.felt
      %c0 = felt.const 0
      constrain.eq %reg, %c0 : !felt.felt
      function.return
    }
  }
}

// CHECK-LABEL: @Component00 StructIntervals {
// CHECK-NEXT:     %arg0[@f] in Degenerate(0)
// CHECK-NEXT:     %arg1 in Degenerate(0)
// CHECK-NEXT: }
