// RUN: llzk-opt -split-input-file -verify-diagnostics %s

module attributes {veridise.lang = "llzk"} {
  // expected-error@+2 {{expected attribute value}}
  // expected-error@+1 {{failed to parse LLZK_TypeVarType parameter 'nameRef' which is to be a `::mlir::FlatSymbolRefAttr`}}
  function.def private @p(%x: !llzk.tvar<>)
}
// -----

module attributes {veridise.lang = "llzk"} {
  // expected-error@+1 {{expected '<'}}
  function.def private @q(%x: !llzk.tvar)
}
// -----

module attributes {veridise.lang = "llzk"} {
  // expected-error@+2 {{invalid kind of attribute specified}}
  // expected-error@+1 {{failed to parse LLZK_TypeVarType parameter 'nameRef' which is to be a `::mlir::FlatSymbolRefAttr`}}
  function.def private @r(%x: !llzk.tvar<5>)
}
// -----

module attributes {veridise.lang = "llzk"} {
  // expected-error@+2 {{invalid kind of attribute specified}}
  // expected-error@+1 {{failed to parse LLZK_TypeVarType parameter 'nameRef' which is to be a `::mlir::FlatSymbolRefAttr`}}
  function.def private @s(%x: !llzk.tvar<index>)
}
// -----

module attributes {veridise.lang = "llzk"} {
  // expected-error@+2 {{invalid kind of attribute specified}}
  // expected-error@+1 {{failed to parse LLZK_TypeVarType parameter 'nameRef' which is to be a `::mlir::FlatSymbolRefAttr`}}
  function.def private @t(%x: !llzk.tvar<!llzk.tvar<index>>)
}
// -----

module attributes {veridise.lang = "llzk"} {
  // expected-error@+1 {{'function.def' op references unknown symbol "@A"}}
  function.def private @u(%x: !llzk.tvar<@A>)
}

// -----
module attributes {veridise.lang = "llzk"} {
  llzk.struct @ComponentA {
    // expected-error@+1 {{'llzk.field' op references unknown symbol "@T"}}
    field @f3 : !llzk.tvar<@T>

    function.def @compute() -> !llzk.struct<@ComponentA> {
      %self = new_struct : !llzk.struct<@ComponentA>
      function.return %self : !llzk.struct<@ComponentA>
    }

    function.def @constrain(%self: !llzk.struct<@ComponentA>) { function.return }
  }
}

// -----
module attributes {veridise.lang = "llzk"} {
  llzk.struct @ComponentB1<[@T]> {
    function.def @compute() -> !llzk.struct<@ComponentB1<[@T]>> {
      %self = new_struct : !llzk.struct<@ComponentB1<[@T]>>
      function.return %self : !llzk.struct<@ComponentB1<[@T]>>
    }

    function.def @constrain(%self: !llzk.struct<@ComponentB1<[@T]>>) { function.return }
  }

  llzk.struct @ComponentB2 {
    // Note: If @X were defined (see ComponentB* tests in types_pass.llzk), using
    //  "!llzk.tvar<@X>" is redundant. Just "@X" for the parameter would be equivalent.
    //
    // expected-error@+1 {{'function.def' op references unknown symbol "@X"}}
    function.def @compute(%0: !llzk.struct<@ComponentB1<[!llzk.tvar<@X>]>>) -> !llzk.struct<@ComponentB2> {
      %self = new_struct : !llzk.struct<@ComponentB2>
      function.return %self : !llzk.struct<@ComponentB2>
    }

    function.def @constrain(%self: !llzk.struct<@ComponentB2>, %0: !llzk.struct<@ComponentB1<[!llzk.tvar<@X>]>>) { function.return }
  }
}
// -----

// TypeVarType in CreateArrayOp (`array.new`), non-exact match via unification of `tvar`
// See description of LLZK_CreateArrayOp for full explanation of why this fails.
module attributes {veridise.lang = "llzk"} {
  llzk.struct @ComponentC1<[@T]> {
    function.def @compute() -> !llzk.struct<@ComponentC1<[@T]>> {
      %self = new_struct : !llzk.struct<@ComponentC1<[@T]>>
      function.return %self : !llzk.struct<@ComponentC1<[@T]>>
    }
    function.def @constrain(%self: !llzk.struct<@ComponentC1<[@T]>>) { function.return }
  }

  llzk.struct @ComponentC2<[@T]> {
    field @f : !array.array<2 x !llzk.tvar<@T>>

    function.def @compute() -> !llzk.struct<@ComponentC2<[@T]>> {
      %self = new_struct : !llzk.struct<@ComponentC2<[@T]>>
      %a1 = function.call @ComponentC1::@compute() : () -> !llzk.struct<@ComponentC1<[5]>> // expected-note {{prior use here}}
      %a2 = function.call @ComponentC1::@compute() : () -> !llzk.struct<@ComponentC1<[7]>>
      // expected-error@+1 {{use of value '%a1' expects different type than prior uses: '!llzk.tvar<@T>' vs '!llzk.struct<@ComponentC1<[5]>>'}}
      %arr = array.new %a1, %a2 : !array.array<2 x !llzk.tvar<@T>>
      writef %self[@f] = %arr : !llzk.struct<@ComponentC2<[@T]>>, !array.array<2 x !llzk.tvar<@T>>
      function.return %self : !llzk.struct<@ComponentC2<[@T]>>
    }

    function.def @constrain(%self: !llzk.struct<@ComponentC2<[@T]>>) { function.return }
  }
}
