// RUN: llzk-opt -split-input-file -verify-diagnostics %s

// Several tests refer back to this:
//  NOTE: Cannot directly test the unknown symbol check in `verifySymbolUses()` for some ops
//  because the check producing "different type than prior uses" (which uses strict equality
//  rather than type unification) always occurs before `verifySymbolUses()` and there is no
//  (or at least shouldn't be a) way to create an SSA value with a symbol-parameterized type
//  using an unknown symbol due to sufficient `verifySymbolUses()` checks in all other ops.
//  We have tests for these to ensure no regression comes up in the future.

module attributes {veridise.lang = "llzk"} {
  // expected-error@+2 {{'function.def' op references unknown symbol "@Unknown"}}
  // expected-error@+1 {{could not find 'struct.def' named "@Unknown"}}
  function.def private @in_return_struct() -> !struct.struct<@Unknown>
}
// -----
module attributes {veridise.lang = "llzk"} {
  // expected-error@+1 {{'function.def' op references unknown symbol "@Unknown"}}
  function.def private @in_return_array_of_scalar() -> !array.array<@Unknown x index>
}
// -----
module attributes {veridise.lang = "llzk"} {
  // expected-error@+2 {{'function.def' op references unknown symbol "@Unknown"}}
  // expected-error@+1 {{could not find 'struct.def' named "@Unknown"}}
  function.def private @in_return_array_of_struct() -> !array.array<2 x !struct.struct<@Unknown>>
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @A<[@T]> {
    function.def @compute() -> !struct.struct<@A<[@T]>> {
      %self = struct.new : !struct.struct<@A<[@T]>>
      function.return %self : !struct.struct<@A<[@T]>>
    }
    function.def @constrain(%self: !struct.struct<@A<[@T]>>) { function.return }
  }

  // expected-error@+1 {{'function.def' op references unknown symbol "@Unknown"}}
  function.def private @in_return_deep() ->
    !array.array<2 x !struct.struct<@A<[
      !array.array<2 x !struct.struct<@A<[
        !struct.struct<@A<[
          !array.array<2 x !struct.struct<@A<[
            !struct.struct<@A<[!llzk.tvar<@Unknown>]>>
          ]>>>
        ]>>
      ]>>>
    ]>>>
}
// -----
module attributes {veridise.lang = "llzk"} {
  // expected-error@+2 {{'function.def' op references unknown symbol "@Unknown"}}
  // expected-error@+1 {{could not find 'struct.def' named "@Unknown"}}
  function.def private @in_arg_struct(!struct.struct<@Unknown>) -> i1
}
// -----
module attributes {veridise.lang = "llzk"} {
  // expected-error@+1 {{'function.def' op references unknown symbol "@Unknown"}}
  function.def private @in_arg_array_of_scalar(!array.array<@Unknown x index>) -> i1
}
// -----
module attributes {veridise.lang = "llzk"} {
  // expected-error@+2 {{'function.def' op references unknown symbol "@Unknown"}}
  // expected-error@+1 {{could not find 'struct.def' named "@Unknown"}}
  function.def private @in_arg_array_of_struct(!array.array<2 x !struct.struct<@Unknown>>) -> i1
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @A<[@T]> {
    function.def @compute() -> !struct.struct<@A<[@T]>> {
      %self = struct.new : !struct.struct<@A<[@T]>>
      function.return %self : !struct.struct<@A<[@T]>>
    }
    function.def @constrain(%self: !struct.struct<@A<[@T]>>) { function.return }
  }

  // expected-error@+1 {{'function.def' op references unknown symbol "@Unknown"}}
  function.def private @in_arg_deep(
    !array.array<2 x !struct.struct<@A<[
      !struct.struct<@A<[
        !array.array<2 x !struct.struct<@A<[
          !array.array<2 x !struct.struct<@A<[
            !struct.struct<@A<[!llzk.tvar<@Unknown>]>>
          ]>>>
        ]>>>
      ]>>
    ]>>>) -> i1

}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @InComputeReturnStruct<[@N]> {
    // expected-error@+1 {{'function.def' op references unknown symbol "@Unknown"}}
    function.def @compute() -> !struct.struct<@InComputeReturnStruct<[@Unknown]>> {
      %self = struct.new : !struct.struct<@InComputeReturnStruct<[@N]>>
      function.return %self : !struct.struct<@InComputeReturnStruct<[@N]>>
    }
    function.def @constrain(%self: !struct.struct<@InComputeReturnStruct<[@N]>>) { function.return }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @InComputeReturnArrayOfScalar<[@N]> {
    // expected-error@+1 {{'function.def' op references unknown symbol "@Unknown"}}
    function.def @compute() -> !struct.struct<@InComputeReturnArrayOfScalar<[!array.array<@Unknown x i1>]>> {
      %self = struct.new : !struct.struct<@InComputeReturnArrayOfScalar<[@N]>>
      function.return %self : !struct.struct<@InComputeReturnArrayOfScalar<[@N]>>
    }
    function.def @constrain(%self: !struct.struct<@InComputeReturnArrayOfScalar<[@N]>>) { function.return }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @InComputeReturnArrayOfStruct<[@N]> {
    // expected-error@+2 {{'function.def' op references unknown symbol "@Unknown"}}
    // expected-error@+1 {{could not find 'struct.def' named "@Unknown"}}
    function.def @compute() -> !struct.struct<@InComputeReturnArrayOfStruct<[!array.array<1 x !struct.struct<@Unknown>>]>> {
      %self = struct.new : !struct.struct<@InComputeReturnArrayOfStruct<[@N]>>
      function.return %self : !struct.struct<@InComputeReturnArrayOfStruct<[@N]>>
    }
    function.def @constrain(%self: !struct.struct<@InComputeReturnArrayOfStruct<[@N]>>) { function.return }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @InComputeArgStruct {
    // expected-error@+2 {{'function.def' op references unknown symbol "@Unknown"}}
    // expected-error@+1 {{could not find 'struct.def' named "@Unknown"}}
    function.def @compute(%0: !struct.struct<@Unknown>) -> !struct.struct<@InComputeArgStruct> {
      %self = struct.new : !struct.struct<@InComputeArgStruct>
      function.return %self : !struct.struct<@InComputeArgStruct>
    }
    function.def @constrain(%self: !struct.struct<@InComputeArgStruct>) { function.return }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @InComputeArgArrayOfScalar {
    // expected-error@+1 {{'function.def' op references unknown symbol "@Unknown"}}
    function.def @compute(%0: !array.array<@Unknown x index>) -> !struct.struct<@InComputeArgArrayOfScalar> {
      %self = struct.new : !struct.struct<@InComputeArgArrayOfScalar>
      function.return %self : !struct.struct<@InComputeArgArrayOfScalar>
    }
    function.def @constrain(%self: !struct.struct<@InComputeArgArrayOfScalar>) { function.return }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @InComputeArgArrayOfStruct {
    // expected-error@+2 {{'function.def' op references unknown symbol "@Unknown"}}
    // expected-error@+1 {{could not find 'struct.def' named "@Unknown"}}
    function.def @compute(%0: !array.array<2 x !struct.struct<@Unknown>>) -> !struct.struct<@InComputeArgArrayOfStruct> {
      %self = struct.new : !struct.struct<@InComputeArgArrayOfStruct>
      function.return %self : !struct.struct<@InComputeArgArrayOfStruct>
    }
    function.def @constrain(%self: !struct.struct<@InComputeArgArrayOfStruct>) { function.return }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @InConstrainArgStruct {
    function.def @compute() -> !struct.struct<@InConstrainArgStruct> {
      %self = struct.new : !struct.struct<@InConstrainArgStruct>
      function.return %self : !struct.struct<@InConstrainArgStruct>
    }
    // expected-error@+2 {{'function.def' op references unknown symbol "@Unknown"}}
    // expected-error@+1 {{could not find 'struct.def' named "@Unknown"}}
    function.def @constrain(%self: !struct.struct<@InConstrainArgStruct>, %0: !struct.struct<@Unknown>) { function.return }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @InConstrainArgArrayOfScalar {
    function.def @compute() -> !struct.struct<@InConstrainArgArrayOfScalar> {
      %self = struct.new : !struct.struct<@InConstrainArgArrayOfScalar>
      function.return %self : !struct.struct<@InConstrainArgArrayOfScalar>
    }
    // expected-error@+1 {{'function.def' op references unknown symbol "@Unknown"}}
    function.def @constrain(%self: !struct.struct<@InConstrainArgArrayOfScalar>, %0: !array.array<@Unknown x index>) { function.return }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @InConstrainArgArrayOfStruct {
    function.def @compute() -> !struct.struct<@InConstrainArgArrayOfStruct> {
      %self = struct.new : !struct.struct<@InConstrainArgArrayOfStruct>
      function.return %self : !struct.struct<@InConstrainArgArrayOfStruct>
    }
    // expected-error@+2 {{'function.def' op references unknown symbol "@Unknown"}}
    // expected-error@+1 {{could not find 'struct.def' named "@Unknown"}}
    function.def @constrain(%self: !struct.struct<@InConstrainArgArrayOfStruct>, %0: !array.array<2 x !struct.struct<@Unknown>>) { function.return }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @InReturnOp {
    function.def @compute() -> !struct.struct<@InReturnOp> {
      %self = struct.new : !struct.struct<@InReturnOp> // expected-note {{prior use here}}
      // See NOTE on line 4.
      // expected-error@+1 {{use of value '%self' expects different type than prior uses: '!struct.struct<@Unknown>' vs '!struct.struct<@InReturnOp>'}}
      function.return %self : !struct.struct<@Unknown>
    }
    function.def @constrain(%self: !struct.struct<@InReturnOp>) { function.return }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @InReturnOpAndTypeAttr {
    function.def @compute() -> !struct.struct<@InReturnOpAndTypeAttr> {
      %self = struct.new : !struct.struct<@Unknown>
      // expected-error@+1 {{type of return operand 0 ('!struct.struct<@Unknown>') doesn't match function result type ('!struct.struct<@InReturnOpAndTypeAttr>') in function @compute}}
      function.return %self : !struct.struct<@Unknown>
    }
    function.def @constrain(%self: !struct.struct<@InReturnOpAndTypeAttr>) { function.return }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  function.def private @id(%a: !felt.felt) -> !felt.felt
  function.def @callop_unknown_in_return_type(%a: !felt.felt) {
    // expected-error@+1 {{'function.call' op references unknown symbol "@Unknown"}}
    %sum = function.call @id(%a) : (!felt.felt) -> (!llzk.tvar<@Unknown>)
    function.return
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  function.def private @id(%a: !felt.felt) -> !felt.felt
  function.def @callop_unknown_in_return_type(%a: !felt.felt) { // expected-note {{prior use here}}
    // See NOTE on line 4.
    // expected-error@+1 {{use of value '%a' expects different type than prior uses: '!llzk.tvar<@Unknown>' vs '!felt.felt'}}
    %sum = function.call @id(%a) : (!llzk.tvar<@Unknown>) -> (!felt.felt)
    function.return
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @InNewStruct {
    function.def @compute() -> !struct.struct<@InNewStruct> {
      %self = struct.new : !struct.struct<@InNewStruct>
      // expected-error@+2 {{'struct.new' op references unknown symbol "@Unknown"}}
      // expected-error@+1 {{could not find 'struct.def' named "@Unknown"}}
      %u = struct.new : !struct.struct<@Unknown>
      function.return %self : !struct.struct<@InNewStruct>
    }
    function.def @constrain(%self: !struct.struct<@InNewStruct>) { function.return }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @InNewStructParam<[@A]> {
    function.def @compute() -> !struct.struct<@InNewStructParam<[@A]>> {
      %self = struct.new : !struct.struct<@InNewStructParam<[@A]>>
      // expected-error@+1 {{'struct.new' op references unknown symbol "@Unknown"}}
      %u = struct.new : !struct.struct<@InNewStructParam<[@Unknown]>>
      function.return %self : !struct.struct<@InNewStructParam<[@A]>>
    }
    function.def @constrain(%self: !struct.struct<@InNewStructParam<[@A]>>) { function.return }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @InNewStructArrayParam<[@A]> {
    function.def @compute() -> !struct.struct<@InNewStructArrayParam<[@A]>> {
      %self = struct.new : !struct.struct<@InNewStructArrayParam<[@A]>>
      // expected-error@+1 {{'struct.new' op references unknown symbol "@Unknown"}}
      %u = struct.new : !struct.struct<@InNewStructArrayParam<[!array.array<@Unknown x i1>]>>
      function.return %self : !struct.struct<@InNewStructArrayParam<[@A]>>
    }
    function.def @constrain(%self: !struct.struct<@InNewStructArrayParam<[@A]>>) { function.return }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @FieldDefOpWithUnknownScalarArray {
    // expected-error@+1 {{'struct.field' op references unknown symbol "@Unknown"}}
    struct.field @ret: !array.array<@Unknown x !felt.felt>
    function.def @compute() -> !struct.struct<@FieldDefOpWithUnknownScalarArray> {
      %self = struct.new : !struct.struct<@FieldDefOpWithUnknownScalarArray>
      function.return %self : !struct.struct<@FieldDefOpWithUnknownScalarArray>
    }
    function.def @constrain(%self: !struct.struct<@FieldDefOpWithUnknownScalarArray>) { function.return }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @FieldDefOpWithUnknownTypeVar {
    // expected-error@+1 {{'struct.field' op references unknown symbol "@Unknown"}}
    struct.field @ret: !llzk.tvar<@Unknown>
    function.def @compute() -> !struct.struct<@FieldDefOpWithUnknownTypeVar> {
      %self = struct.new : !struct.struct<@FieldDefOpWithUnknownTypeVar>
      function.return %self : !struct.struct<@FieldDefOpWithUnknownTypeVar>
    }
    function.def @constrain(%self: !struct.struct<@FieldDefOpWithUnknownTypeVar>) { function.return }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @ConstReadWithUnknown<[@T]> {
    function.def @compute() -> !struct.struct<@ConstReadWithUnknown<[@T]>> {
      %self = struct.new : !struct.struct<@ConstReadWithUnknown<[@T]>>
      function.return %self : !struct.struct<@ConstReadWithUnknown<[@T]>>
    }
    function.def @constrain(%self: !struct.struct<@ConstReadWithUnknown<[@T]>>) {
      // expected-error@+1 {{'llzk.read_const' op references unknown symbol "@Unknown"}}
      %0 = llzk.read_const @T : !llzk.tvar<@Unknown>
      function.return
    }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @FieldReadWithUnknownInBase<[@A]> {
    struct.field @f: !felt.felt
    function.def @compute() -> !struct.struct<@FieldReadWithUnknownInBase<[@A]>> {
      %self = struct.new : !struct.struct<@FieldReadWithUnknownInBase<[@A]>>
      function.return %self : !struct.struct<@FieldReadWithUnknownInBase<[@A]>>
    }
    function.def @constrain(%self: !struct.struct<@FieldReadWithUnknownInBase<[@A]>>) { // expected-note {{prior use here}}
      // See NOTE on line 4.
      // expected-error@+1 {{use of value '%self' expects different type than prior uses: '!struct.struct<@FieldReadWithUnknownInBase<[@Unknown]>>' vs '!struct.struct<@FieldReadWithUnknownInBase<[@A]>>'}}
      %0 = struct.readf %self[@f] : !struct.struct<@FieldReadWithUnknownInBase<[@Unknown]>>, !felt.felt
      function.return
    }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @FieldReadWithUnknownInResultTypeVar {
    struct.field @f: index
    function.def @compute() -> !struct.struct<@FieldReadWithUnknownInResultTypeVar> {
      %self = struct.new : !struct.struct<@FieldReadWithUnknownInResultTypeVar>
      function.return %self : !struct.struct<@FieldReadWithUnknownInResultTypeVar>
    }
    function.def @constrain(%self: !struct.struct<@FieldReadWithUnknownInResultTypeVar>) {
      // expected-error@+1 {{'struct.readf' op references unknown symbol "@Unknown"}}
      %0 = struct.readf %self[@f] : !struct.struct<@FieldReadWithUnknownInResultTypeVar>, !llzk.tvar<@Unknown>
      function.return
    }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @FieldReadWithUnknownInResultArr<[@A]> {
    struct.field @f: !array.array<@A x i1>
    function.def @compute() -> !struct.struct<@FieldReadWithUnknownInResultArr<[@A]>> {
      %self = struct.new : !struct.struct<@FieldReadWithUnknownInResultArr<[@A]>>
      function.return %self : !struct.struct<@FieldReadWithUnknownInResultArr<[@A]>>
    }
    function.def @constrain(%self: !struct.struct<@FieldReadWithUnknownInResultArr<[@A]>>) {
      // expected-error@+1 {{'struct.readf' op references unknown symbol "@Unknown"}}
      %0 = struct.readf %self[@f] : !struct.struct<@FieldReadWithUnknownInResultArr<[@A]>>, !array.array<@Unknown x i1>
      function.return
    }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @FieldWriteWithUnknownInBase<[@A]> {
    struct.field @f: !felt.felt
    function.def @compute(%0: i1) -> !struct.struct<@FieldWriteWithUnknownInBase<[@A]>> {
      %self = struct.new : !struct.struct<@FieldWriteWithUnknownInBase<[@A]>>
      function.return %self : !struct.struct<@FieldWriteWithUnknownInBase<[@A]>>
    }
    function.def @constrain(%self: !struct.struct<@FieldWriteWithUnknownInBase<[@A]>>, %0: i1) { // expected-note {{prior use here}}
      // See NOTE on line 4.
      // expected-error@+1 {{use of value '%self' expects different type than prior uses: '!struct.struct<@FieldWriteWithUnknownInBase<[@Unknown]>>' vs '!struct.struct<@FieldWriteWithUnknownInBase<[@A]>>'}}
      struct.writef %self[@f] = %0 : !struct.struct<@FieldWriteWithUnknownInBase<[@Unknown]>>, i1
      function.return
    }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @FieldWriteWithUnknownInRHSTypeVar<[@A]> {
    struct.field @f: !llzk.tvar<@A>
    function.def @compute(
      %0: !llzk.tvar<@A> // expected-note {{prior use here}}
    ) -> !struct.struct<@FieldWriteWithUnknownInRHSTypeVar<[@A]>> {
      %self = struct.new : !struct.struct<@FieldWriteWithUnknownInRHSTypeVar<[@A]>>
      // See NOTE on line 4.
      // expected-error@+1 {{use of value '%0' expects different type than prior uses: '!llzk.tvar<@Unknown>' vs '!llzk.tvar<@A>'}}
      struct.writef %self[@f] = %0 : !struct.struct<@FieldWriteWithUnknownInRHSTypeVar<[@A]>>, !llzk.tvar<@Unknown>
      function.return %self : !struct.struct<@FieldWriteWithUnknownInRHSTypeVar<[@A]>>
    }
    function.def @constrain(%self: !struct.struct<@FieldWriteWithUnknownInRHSTypeVar<[@A]>>) {
      function.return
    }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @FieldWriteWithUnknownInRHSArr<[@A]> {
    struct.field @f: !array.array<@A x i1>
    function.def @compute(
      %0: !array.array<@A x i1> // expected-note {{prior use here}}
    ) -> !struct.struct<@FieldWriteWithUnknownInRHSArr<[@A]>> {
      %self = struct.new : !struct.struct<@FieldWriteWithUnknownInRHSArr<[@A]>>
      // See NOTE on line 4.
      // expected-error@+1 {{use of value '%0' expects different type than prior uses: '!array.array<@Unknown x i1>' vs '!array.array<@A x i1>'}}
      struct.writef %self[@f] = %0 : !struct.struct<@FieldWriteWithUnknownInRHSArr<[@A]>>, !array.array<@Unknown x i1>
      function.return %self : !struct.struct<@FieldWriteWithUnknownInRHSArr<[@A]>>
    }
    function.def @constrain(%self: !struct.struct<@FieldWriteWithUnknownInRHSArr<[@A]>>) {
      function.return
    }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  function.def @create_array_op_unknown() {
    // expected-error@+1 {{'array.new' op references unknown symbol "@Unknown"}}
    %arr = array.new : !array.array<@Unknown x !felt.felt>
    function.return
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  function.def @ReadArrayOp_unknown_base(
    %arr: !array.array<5 x !felt.felt>, // expected-note {{prior use here}}
    %k: index
  ) {
    // See NOTE on line 4.
    // expected-error@+1 {{use of value '%arr' expects different type than prior uses: '!array.array<@Unknown x !felt.felt>' vs '!array.array<5 x !felt.felt>'}}
    %v = array.read %arr[%k] : !array.array<@Unknown x !felt.felt>, !felt.felt
    function.return
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  function.def @ReadArrayOp_unknown_result_tvar(%arr: !array.array<5 x !felt.felt>, %k: index) {
    // expected-error@+1 {{'array.read' op references unknown symbol "@Unknown"}}
    %v = array.read %arr[%k] : !array.array<5 x !felt.felt>, !llzk.tvar<@Unknown>
    function.return
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @ReadArrayOpWithUnknownInResultTypeArrElem<[@T]> {
    function.def @compute(%arr: !array.array<5 x !llzk.tvar<@T>>) -> !struct.struct<@ReadArrayOpWithUnknownInResultTypeArrElem<[@T]>> {
      %self = struct.new : !struct.struct<@ReadArrayOpWithUnknownInResultTypeArrElem<[@T]>>
      function.return %self : !struct.struct<@ReadArrayOpWithUnknownInResultTypeArrElem<[@T]>>
    }
    function.def @constrain(%self: !struct.struct<@ReadArrayOpWithUnknownInResultTypeArrElem<[@T]>>, %arr: !array.array<5 x !llzk.tvar<@T>>) {
      %k = arith.constant 0 : index
      // expected-error@+2 {{'array.read' op references unknown symbol "@Unknown"}}
      // expected-error@+1 {{could not find 'struct.def' named "@Unknown"}}
      %v = array.read %arr[%k] : !array.array<5 x !llzk.tvar<@T>>, !struct.struct<@Unknown>
      function.return
    }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  function.def @WriteArrayOp_unknown_base(
      %arr: !array.array<5 x !felt.felt>, // expected-note {{prior use here}}
      %k: index,
      %v: !felt.felt
  ) {
    // See NOTE on line 4.
    // expected-error@+1 {{use of value '%arr' expects different type than prior uses: '!array.array<@Unknown x !felt.felt>' vs '!array.array<5 x !felt.felt>'}}
    array.write %arr[%k] = %v : !array.array<@Unknown x !felt.felt>, !felt.felt
    function.return
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  function.def @WriteArrayOp_unknown_result_tvar(
      %arr: !array.array<5 x !felt.felt>,
      %k: index,
      %v: !felt.felt // expected-note {{prior use here}}
  ) {
    // See NOTE on line 4.
    // expected-error@+1 {{use of value '%v' expects different type than prior uses: '!llzk.tvar<@Unknown>' vs '!felt.felt'}}
    array.write %arr[%k] = %v : !array.array<5 x !felt.felt>, !llzk.tvar<@Unknown>
    function.return
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @WriteArrayOpWithUnknownInResultTypeArrElem<[@T]> {
    function.def @compute(%arr: !array.array<5 x !llzk.tvar<@T>>,
      %v: !struct.struct<@T> // expected-note {{prior use here}}
    ) -> !struct.struct<@WriteArrayOpWithUnknownInResultTypeArrElem<[@T]>> {
      %self = struct.new : !struct.struct<@WriteArrayOpWithUnknownInResultTypeArrElem<[@T]>>
      %k = arith.constant 0 : index
      // See NOTE on line 4.
      // expected-error@+1 {{use of value '%v' expects different type than prior uses: '!struct.struct<@Unknown>' vs '!struct.struct<@T>'}}
      array.write %arr[%k] = %v : !array.array<5 x !llzk.tvar<@T>>, !struct.struct<@Unknown>
      function.return %self : !struct.struct<@WriteArrayOpWithUnknownInResultTypeArrElem<[@T]>>
    }
    function.def @constrain(%self: !struct.struct<@WriteArrayOpWithUnknownInResultTypeArrElem<[@T]>>,
        %arr: !array.array<5 x !llzk.tvar<@T>>,
        %v: !struct.struct<@T>
    ) {
      %k = arith.constant 0 : index
      function.return
    }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  function.def private @array_extract_with_unknown_base(
      %r: !array.array<2,2 x i1> // expected-note {{prior use here}}
  ) {
    // See NOTE on line 4.
    // expected-error@+1 {{use of value '%r' expects different type than prior uses: '!array.array<2,2 x !llzk.tvar<@Unknown>>' vs '!array.array<2,2 x i1>'}}
    %s = array.extract %r[] : !array.array<2,2 x !llzk.tvar<@Unknown>>
    function.return
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  function.def @array_len_with_unknown_base(
      %a: !array.array<2,3 x !felt.felt>, // expected-note {{prior use here}}
      %0: index
  ) -> index {
    // See NOTE on line 4.
    // expected-error@+1 {{use of value '%a' expects different type than prior uses: '!array.array<2,@Unknown x !felt.felt>' vs '!array.array<2,3 x !felt.felt>'}}
    %x = array.len %a, %0 : !array.array<2,@Unknown x !felt.felt>
    function.return %x: index
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @EmitEqOp_With_Unknown_In_LHS {
    function.def @constrain(%self: !struct.struct<@EmitEqOp_With_Unknown_In_LHS>) {
      %a = array.new : !array.array<5 x !felt.felt> // expected-note {{prior use here}}
      %b = array.new : !array.array<5 x !felt.felt>
      // See NOTE on line 4.
      // expected-error@+1 {{use of value '%a' expects different type than prior uses: '!array.array<@Unknown x !felt.felt>' vs '!array.array<5 x !felt.felt>'}}
      llzk.emit_eq %a, %b : !array.array<@Unknown x !felt.felt>
      function.return
    }

    function.def @compute() -> !struct.struct<@EmitEqOp_With_Unknown_In_LHS> {
      %self = struct.new : !struct.struct<@EmitEqOp_With_Unknown_In_LHS>
      function.return %self : !struct.struct<@EmitEqOp_With_Unknown_In_LHS>
    }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @EmitEqOp_With_Unknown_In_RHS {
    function.def @constrain(%self: !struct.struct<@EmitEqOp_With_Unknown_In_RHS>) {
      %a = array.new : !array.array<5 x !felt.felt>
      %b = array.new : !array.array<5 x !felt.felt> // expected-note {{prior use here}}
      // See NOTE on line 4.
      // expected-error@+1 {{use of value '%b' expects different type than prior uses: '!array.array<@Unknown x !felt.felt>' vs '!array.array<5 x !felt.felt>'}}
      llzk.emit_eq %a, %b : !array.array<5 x !felt.felt>, !array.array<@Unknown x !felt.felt>
      function.return
    }

    function.def @compute() -> !struct.struct<@EmitEqOp_With_Unknown_In_RHS> {
      %self = struct.new : !struct.struct<@EmitEqOp_With_Unknown_In_RHS>
      function.return %self : !struct.struct<@EmitEqOp_With_Unknown_In_RHS>
    }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @EmitInOp_With_Unknown_In_LHS {
    function.def @constrain(%self: !struct.struct<@EmitInOp_With_Unknown_In_LHS>) {
      %a = array.new : !array.array<5 x !felt.felt> // expected-note {{prior use here}}
      %b = felt.const 3
      // See NOTE on line 4.
      // expected-error@+1 {{use of value '%a' expects different type than prior uses: '!array.array<@Unknown x !felt.felt>' vs '!array.array<5 x !felt.felt>'}}
      llzk.emit_in %a, %b : !array.array<@Unknown x !felt.felt>
      function.return
    }

    function.def @compute() -> !struct.struct<@EmitInOp_With_Unknown_In_LHS> {
      %self = struct.new : !struct.struct<@EmitInOp_With_Unknown_In_LHS>
      function.return %self : !struct.struct<@EmitInOp_With_Unknown_In_LHS>
    }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @EmitInOp_With_Unknown_In_RHS {
    function.def @constrain(%self: !struct.struct<@EmitInOp_With_Unknown_In_RHS>) {
      %a = array.new : !array.array<5 x !felt.felt>
      %b = felt.const 3 // expected-note {{prior use here}}
      // See NOTE on line 4.
      // expected-error@+1 {{use of value '%b' expects different type than prior uses: '!llzk.tvar<@Unknown>' vs '!felt.felt'}}
      llzk.emit_in %a, %b : !array.array<5 x !felt.felt>, !llzk.tvar<@Unknown>
      function.return
    }

    function.def @compute() -> !struct.struct<@EmitInOp_With_Unknown_In_RHS> {
      %self = struct.new : !struct.struct<@EmitInOp_With_Unknown_In_RHS>
      function.return %self : !struct.struct<@EmitInOp_With_Unknown_In_RHS>
    }
  }
}
