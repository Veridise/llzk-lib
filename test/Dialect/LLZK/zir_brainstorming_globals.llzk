// RUN: llzk-opt -I %S -split-input-file -verify-diagnostics %s | FileCheck --enable-var-scope %s

// This demonstrates one way that zirgen runtime checks for globals could be handled. Requires boilerplate
// code to track if globals have been initialized and requires LLZK to have zero-initialization semantics.
module attributes {veridise.lang = "llzk"} {
  struct.def @NondetReg<[]> {
    struct.field @"$super" : !felt.felt
    struct.field @reg : !felt.felt
    function.def @compute(%arg0: !felt.felt) -> !struct.struct<@NondetReg<[]>> {
      %self = struct.new : <@NondetReg<[]>>
      struct.writef %self[@reg] = %arg0 : <@NondetReg<[]>>, !felt.felt
      struct.writef %self[@"$super"] = %arg0 : <@NondetReg<[]>>, !felt.felt
      function.return %self : !struct.struct<@NondetReg<[]>>
    }
    function.def @constrain(%arg0: !struct.struct<@NondetReg<[]>>, %arg1: !felt.felt) {
      function.return
    }
  }

  // BEGIN: BOILERPLATE
  struct.def @Global<[@Type]> {
    struct.field @val : !poly.tvar<@Type>
    struct.field @init : i1

    function.def @compute(%v: !poly.tvar<@Type>) -> !struct.struct<@Global<[@Type]>> {
      %self = struct.new : !struct.struct<@Global<[@Type]>>
      struct.writef %self[@val] = %v : !struct.struct<@Global<[@Type]>>, !poly.tvar<@Type>
      %true = arith.constant true
      struct.writef %self[@init] = %true : !struct.struct<@Global<[@Type]>>, i1
      function.return %self : !struct.struct<@Global<[@Type]>>
    }

    function.def @constrain(%self: !struct.struct<@Global<[@Type]>>, %v: !poly.tvar<@Type>) {
      function.return
    }
  }
  // END: BOILERPLATE

  module @globals {
    llzk.global @A : !struct.struct<@Global<[!struct.struct<@NondetReg<[]>>]>>
  }

  struct.def @Top {
    function.def @compute(%arg0: !felt.felt) -> !struct.struct<@Top> {
      %self = struct.new : <@Top>
      %0 = function.call @NondetReg::@compute(%arg0) : (!felt.felt) -> !struct.struct<@NondetReg<[]>>
      // BEGIN: BOILERPLATE - Ensure it was NOT previously written
      %g = llzk.readg @globals::@A : !struct.struct<@Global<[!struct.struct<@NondetReg<[]>>]>>
      %i = struct.readf %g[@init] : !struct.struct<@Global<[!struct.struct<@NondetReg<[]>>]>>, i1
      %f = arith.constant false // default, 0-initialized value of 'i1' field
      %x = arith.cmpi eq, %i, %f : i1
      llzk.assert %x, "Invalid set of \"global\"" // mirror zirgen message
      %1 = function.call @Global::@compute(%0) : (!struct.struct<@NondetReg<[]>>) -> !struct.struct<@Global<[!struct.struct<@NondetReg<[]>>]>>
      // END: BOILERPLATE
      llzk.writeg @globals::@A = %1 : !struct.struct<@Global<[!struct.struct<@NondetReg<[]>>]>>
      function.return %self : !struct.struct<@Top>
    }
    function.def @constrain(%arg0: !struct.struct<@Top>, %arg1: !felt.felt) {
      %g = llzk.readg @globals::@A : !struct.struct<@Global<[!struct.struct<@NondetReg<[]>>]>>
      // BEGIN: BOILERPLATE - Ensure it was previously written
      %i = struct.readf %g[@init] : !struct.struct<@Global<[!struct.struct<@NondetReg<[]>>]>>, i1
      %f = arith.constant false // default, 0-initialized value of 'i1' field
      %x = arith.cmpi ne, %i, %f : i1
      llzk.assert %x, "Read before write \"global\"" // mirror zirgen message
      %0 = struct.readf %g[@val] : !struct.struct<@Global<[!struct.struct<@NondetReg<[]>>]>>, !struct.struct<@NondetReg<[]>>
      // END: BOILERPLATE
      %1 = struct.readf %0[@reg] : !struct.struct<@NondetReg<[]>>, !felt.felt
      function.return
    }
  }
}

// CHECK-LABEL: struct.def @NondetReg<[]> {
// CHECK-NEXT:    struct.field @"$super" : !felt.felt
// CHECK-NEXT:    struct.field @reg : !felt.felt
// CHECK-NEXT:    function.def @compute(%[[V0:[0-9a-zA-Z_\.]+]]: !felt.felt) -> !struct.struct<@NondetReg<[]>> attributes {function.allow_witness} {
// CHECK-NEXT:      %[[V1:[0-9a-zA-Z_\.]+]] = struct.new : <@NondetReg<[]>>
// CHECK-NEXT:      struct.writef %[[V1]][@reg] = %[[V0]] : <@NondetReg<[]>>, !felt.felt
// CHECK-NEXT:      struct.writef %[[V1]][@"$super"] = %[[V0]] : <@NondetReg<[]>>, !felt.felt
// CHECK-NEXT:      function.return %[[V1]] : !struct.struct<@NondetReg<[]>>
// CHECK-NEXT:    }
// CHECK-NEXT:    function.def @constrain(%[[V2:[0-9a-zA-Z_\.]+]]: !struct.struct<@NondetReg<[]>>, %[[V3:[0-9a-zA-Z_\.]+]]: !felt.felt) attributes {function.allow_constraint} {
// CHECK-NEXT:      function.return
// CHECK-NEXT:    }
// CHECK-NEXT:  }
//
// CHECK-LABEL: struct.def @Global<[@Type]> {
// CHECK-NEXT:    struct.field @val : !poly.tvar<@Type>
// CHECK-NEXT:    struct.field @init : i1
// CHECK-NEXT:    function.def @compute(%[[V0:[0-9a-zA-Z_\.]+]]: !poly.tvar<@Type>) -> !struct.struct<@Global<[@Type]>> attributes {function.allow_witness} {
// CHECK-NEXT:      %[[V1:[0-9a-zA-Z_\.]+]] = struct.new : <@Global<[@Type]>>
// CHECK-NEXT:      struct.writef %[[V1]][@val] = %[[V0]] : <@Global<[@Type]>>, !poly.tvar<@Type>
// CHECK-NEXT:      %[[V2:[0-9a-zA-Z_\.]+]] = arith.constant true
// CHECK-NEXT:      struct.writef %[[V1]][@init] = %[[V2]] : <@Global<[@Type]>>, i1
// CHECK-NEXT:      function.return %[[V1]] : !struct.struct<@Global<[@Type]>>
// CHECK-NEXT:    }
// CHECK-NEXT:    function.def @constrain(%[[V3:[0-9a-zA-Z_\.]+]]: !struct.struct<@Global<[@Type]>>, %[[V4:[0-9a-zA-Z_\.]+]]: !poly.tvar<@Type>) attributes {function.allow_constraint} {
// CHECK-NEXT:      function.return
// CHECK-NEXT:    }
// CHECK-NEXT:  }
//
// CHECK-LABEL: module @globals {
// CHECK-NEXT:    llzk.global @A : !struct.struct<@Global<[!struct.struct<@NondetReg<[]>>]>>
// CHECK-NEXT:  }
//
// CHECK-LABEL: struct.def @Top {
// CHECK-NEXT:    function.def @compute(%[[V0:[0-9a-zA-Z_\.]+]]: !felt.felt) -> !struct.struct<@Top> attributes {function.allow_witness} {
// CHECK-NEXT:      %[[V1:[0-9a-zA-Z_\.]+]] = struct.new : <@Top>
// CHECK-NEXT:      %[[V2:[0-9a-zA-Z_\.]+]] = function.call @NondetReg::@compute(%[[V0]]) : (!felt.felt) -> !struct.struct<@NondetReg<[]>>
// CHECK-NEXT:      %[[V3:[0-9a-zA-Z_\.]+]] = llzk.readg @globals::@A : !struct.struct<@Global<[!struct.struct<@NondetReg<[]>>]>>
// CHECK-NEXT:      %[[V4:[0-9a-zA-Z_\.]+]] = struct.readf %[[V3]][@init] : <@Global<[!struct.struct<@NondetReg<[]>>]>>, i1
// CHECK-NEXT:      %[[V5:[0-9a-zA-Z_\.]+]] = arith.constant false
// CHECK-NEXT:      %[[V6:[0-9a-zA-Z_\.]+]] = arith.cmpi eq, %[[V4]], %[[V5]] : i1
// CHECK-NEXT:      llzk.assert %[[V6]], "Invalid set of \22global\22"
// CHECK-NEXT:      %[[V7:[0-9a-zA-Z_\.]+]] = function.call @Global::@compute(%[[V2]]) : (!struct.struct<@NondetReg<[]>>) -> !struct.struct<@Global<[!struct.struct<@NondetReg<[]>>]>>
// CHECK-NEXT:      llzk.writeg @globals::@A = %[[V7]] : !struct.struct<@Global<[!struct.struct<@NondetReg<[]>>]>>
// CHECK-NEXT:      function.return %[[V1]] : !struct.struct<@Top>
// CHECK-NEXT:    }
// CHECK-NEXT:    function.def @constrain(%[[V8:[0-9a-zA-Z_\.]+]]: !struct.struct<@Top>, %[[V9:[0-9a-zA-Z_\.]+]]: !felt.felt) attributes {function.allow_constraint} {
// CHECK-NEXT:      %[[V10:[0-9a-zA-Z_\.]+]] = llzk.readg @globals::@A : !struct.struct<@Global<[!struct.struct<@NondetReg<[]>>]>>
// CHECK-NEXT:      %[[V11:[0-9a-zA-Z_\.]+]] = struct.readf %[[V10]][@init] : <@Global<[!struct.struct<@NondetReg<[]>>]>>, i1
// CHECK-NEXT:      %[[V12:[0-9a-zA-Z_\.]+]] = arith.constant false
// CHECK-NEXT:      %[[V13:[0-9a-zA-Z_\.]+]] = arith.cmpi ne, %[[V11]], %[[V12]] : i1
// CHECK-NEXT:      llzk.assert %[[V13]], "Read before write \22global\22"
// CHECK-NEXT:      %[[V14:[0-9a-zA-Z_\.]+]] = struct.readf %[[V10]][@val] : <@Global<[!struct.struct<@NondetReg<[]>>]>>, !struct.struct<@NondetReg<[]>>
// CHECK-NEXT:      %[[V15:[0-9a-zA-Z_\.]+]] = struct.readf %[[V14]][@reg] : <@NondetReg<[]>>, !felt.felt
// CHECK-NEXT:      function.return
// CHECK-NEXT:    }
// CHECK-NEXT:  }
