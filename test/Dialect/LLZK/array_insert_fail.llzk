// RUN: llzk-opt %s -split-input-file -verify-diagnostics

function.def @more_indices_than_source(%z: !array.type<2,2,2 x !string.string>) {
  %r = array.new : !array.type<44,2 x !string.string>
  %0 = arith.constant 0 : index
  // expected-error@+1 {{'array.insert' op cannot select more dimensions than exist in the source array}}
  array.insert %r[%0,%0,%0] = %z : !array.type<44,2 x !string.string>, !array.type<2,2,2 x !string.string>
  function.return
}

// -----
function.def @should_use_array.write(%elem: !string.string) {
  %r = array.new : !array.type<44,2 x !string.string>
  %0 = arith.constant 0 : index
  array.write %r[%0,%0] = %elem : !array.type<44,2 x !string.string>, !string.string
  // expected-error@+1 {{custom op 'array.insert' invalid kind of Type specified}}
  array.insert %r[%0,%0] = %elem : !array.type<44,2 x !string.string>, !string.string
  function.return
}
// -----
function.def @insufficient_indices_none(%0: !felt.felt, %1: !felt.felt, %z: !array.type<2 x !felt.felt>) {
  %r = array.new : !array.type<4,2 x !felt.felt>
  // expected-error@+1 {{'array.insert' op has insufficient indexed dimensions: expected 1 but found 0}}
  array.insert %r[] = %z : !array.type<4,2 x !felt.felt>, !array.type<2 x !felt.felt>
  function.return
}
// -----
function.def @insufficient_indices(%a: i1, %b: i1, %c: i1, %d: i1, %e: i1, %f: i1) {
  %a0 = array.new : !array.type<2,3,2,3 x i1>

  %0 = arith.constant 0 : index

  %a1 = array.new %a, %b, %c, %d, %e, %f : !array.type<2,3 x i1>
  // expected-error@+1 {{'array.insert' op has insufficient indexed dimensions: expected 2 but found 1}}
  array.insert %a0[%0] = %a1 : !array.type<2,3,2,3 x i1>, !array.type<2,3 x i1>
  function.return
}
// -----
function.def @excess_indices(%a: i1, %b: i1, %c: i1, %d: i1, %e: i1, %f: i1) {
  %a0 = array.new : !array.type<2,3,2,3 x i1>

  %0 = arith.constant 0 : index

  %a1 = array.new %a, %b, %c, %d, %e, %f : !array.type<2,3 x i1>
  // expected-error@+1 {{'array.insert' op has too many indexed dimensions: expected 2 but found 3}}
  array.insert %a0[%0, %0, %0] = %a1 : !array.type<2,3,2,3 x i1>, !array.type<2,3 x i1>
  function.return
}
// -----
function.def @incorrect_elem_type(%z: !array.type<2 x index>) {
  %r = array.new : !array.type<44,2 x !felt.felt>
  %0 = arith.constant 0 : index
  // expected-error@+1 {{'array.insert' op incorrect array element type; expected: '!felt.felt', found: 'index'}}
  array.insert %r[%0] = %z : !array.type<44,2 x !felt.felt>, !array.type<2 x index>
  function.return
}
// -----
function.def @dimensions_not_unify(%z: !array.type<5 x !felt.felt>) {
  %r = array.new : !array.type<44,2 x !felt.felt>
  %0 = arith.constant 0 : index
  // expected-error@+1 {{'array.insert' op cannot unify array dimensions [2] with [5]}}
  array.insert %r[%0] = %z : !array.type<44,2 x !felt.felt>, !array.type<5 x !felt.felt>
  function.return
}
