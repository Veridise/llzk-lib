// RUN: llzk-opt %s -split-input-file -verify-diagnostics

function.def @more_indices_than_source(%z: !array.array<2,2,2 x !llzk.string>) {
  %r = array.new : !array.array<44,2 x !llzk.string>
  %0 = arith.constant 0 : index
  // expected-error@+1 {{'array.insert' op cannot select more dimensions than exist in the source array}}
  array.insert %r[%0,%0,%0] = %z : !array.array<44,2 x !llzk.string>, !array.array<2,2,2 x !llzk.string>
  function.return
}

// -----
function.def @should_use_array.write(%elem: !llzk.string) {
  %r = array.new : !array.array<44,2 x !llzk.string>
  %0 = arith.constant 0 : index
  array.write %r[%0,%0] = %elem : !array.array<44,2 x !llzk.string>, !llzk.string
  // expected-error@+1 {{custom op 'array.insert' invalid kind of Type specified}}
  array.insert %r[%0,%0] = %elem : !array.array<44,2 x !llzk.string>, !llzk.string
  function.return
}
// -----
function.def @insufficient_indices_none(%0: !llzk.felt, %1: !llzk.felt, %z: !array.array<2 x !llzk.felt>) {
  %r = array.new : !array.array<4,2 x !llzk.felt>
  // expected-error@+1 {{'array.insert' op has insufficient indexed dimensions: expected 1 but found 0}}
  array.insert %r[] = %z : !array.array<4,2 x !llzk.felt>, !array.array<2 x !llzk.felt>
  function.return
}
// -----
function.def @insufficient_indices(%a: i1, %b: i1, %c: i1, %d: i1, %e: i1, %f: i1) {
  %a0 = array.new : !array.array<2,3,2,3 x i1>

  %0 = arith.constant 0 : index

  %a1 = array.new %a, %b, %c, %d, %e, %f : !array.array<2,3 x i1>
  // expected-error@+1 {{'array.insert' op has insufficient indexed dimensions: expected 2 but found 1}}
  array.insert %a0[%0] = %a1 : !array.array<2,3,2,3 x i1>, !array.array<2,3 x i1>
  function.return
}
// -----
function.def @excess_indices(%a: i1, %b: i1, %c: i1, %d: i1, %e: i1, %f: i1) {
  %a0 = array.new : !array.array<2,3,2,3 x i1>

  %0 = arith.constant 0 : index

  %a1 = array.new %a, %b, %c, %d, %e, %f : !array.array<2,3 x i1>
  // expected-error@+1 {{'array.insert' op has too many indexed dimensions: expected 2 but found 3}}
  array.insert %a0[%0, %0, %0] = %a1 : !array.array<2,3,2,3 x i1>, !array.array<2,3 x i1>
  function.return
}
// -----
function.def @incorrect_elem_type(%z: !array.array<2 x index>) {
  %r = array.new : !array.array<44,2 x !llzk.felt>
  %0 = arith.constant 0 : index
  // expected-error@+1 {{'array.insert' op incorrect array element type; expected: '!llzk.felt', found: 'index'}}
  array.insert %r[%0] = %z : !array.array<44,2 x !llzk.felt>, !array.array<2 x index>
  function.return
}
// -----
function.def @dimensions_not_unify(%z: !array.array<5 x !llzk.felt>) {
  %r = array.new : !array.array<44,2 x !llzk.felt>
  %0 = arith.constant 0 : index
  // expected-error@+1 {{'array.insert' op cannot unify array dimensions [2] with [5]}}
  array.insert %r[%0] = %z : !array.array<44,2 x !llzk.felt>, !array.array<5 x !llzk.felt>
  function.return
}
