// RUN: llzk-opt %s -split-input-file -verify-diagnostics

function.def @arr_constant_index() -> !llzk.array<3 x index> {
  %a = arith.constant 45 : index
  %b = arith.constant 214 : index // expected-note {{prior use here}}
  %c = arith.constant 2 : index
  %r = array.new %a, %b, %c : !llzk.array<3 x index>
  // expected-error@+1 {{use of value '%b' expects different type than prior uses: '!llzk.array<3 x index>' vs 'index'}}
  array.write %b[%c] = %b : !llzk.array<3 x index>, index
  function.return %r: !llzk.array<3 x index>
}
// -----

function.def @arr_mix_index(%a: index, %b: index) -> !llzk.array<4 x index> {
  %c = arith.constant 324 : index
  %d = arith.constant 999 : index
  %i = arith.constant 0 : index
  %r = array.new %a, %b, %c, %d: !llzk.array<4 x index> // expected-note {{prior use here}}
  // expected-error@+1 {{use of value '%r' expects different type than prior uses: 'index' vs '!llzk.array<4 x index>'}}
  array.write %r[%r] = %b : !llzk.array<4 x index>, index
  function.return %r: !llzk.array<4 x index>
}
// -----

function.def @arr_mix_index(%a: index, %b: index) -> !llzk.array<4 x index> {
  %c = arith.constant 324 : index
  %d = arith.constant 999 : index
  %i = arith.constant 0 : index
  %f = llzk.constfelt 6744573456376 // expected-note {{prior use here}}
  %r = array.new %a, %b, %c, %d: !llzk.array<4 x index>
  // expected-error@+1 {{use of value '%f' expects different type than prior uses: 'index' vs '!llzk.felt'}}
  array.write %r[%f] = %b : !llzk.array<4 x index>, index
  function.return %r: !llzk.array<4 x index>
}
// -----

function.def @arr_mix_index(%a: !llzk.felt, %b: !llzk.felt) -> !llzk.array<4 x !llzk.felt> {
  %c = llzk.constfelt 324
  %d = llzk.constfelt 999
  %i = arith.constant 0 : index
  %v = arith.constant 5751 : index // expected-note {{prior use here}}
  %r = array.new %a, %b, %c, %d: !llzk.array<4 x !llzk.felt>
  // expected-error@+1 {{use of value '%v' expects different type than prior uses: '!llzk.felt' vs 'index'}}
  array.write %r[%i] = %v : !llzk.array<4 x !llzk.felt>, !llzk.felt
  function.return %r: !llzk.array<4 x !llzk.felt>
}
// -----

function.def @arr_wrong_array_elem_type(%a: !llzk.felt) -> !llzk.array<1 x !llzk.felt> {
  %i = arith.constant 0 : index
  %v = arith.constant 632 : index
  %r = array.new %a: !llzk.array<1 x !llzk.felt>
  // expected-error@+1 {{'array.write' op failed to verify that rvalue type matches with arr_ref element type}}
  array.write %r[%i] = %v : !llzk.array<1 x !llzk.felt>, index
  function.return %r: !llzk.array<1 x !llzk.felt>
}

// -----

function.def @write_too_few_indices() {
  %0 = arith.constant 0 : index
  %1 = arith.constant 1 : index

  %r = array.new : !llzk.array<2,4,3 x index>
  // expected-error@+1 {{'array.write' op has insufficient indexed dimensions: expected 3 but found 2}}
  array.write %r[%1,%0] = %0 : !llzk.array<2,4,3 x index>, index
}

// -----

function.def @write_too_many_indices() {
  %0 = arith.constant 0 : index
  %1 = arith.constant 1 : index

  %r = array.new : !llzk.array<2,4,3 x index>
  // expected-error@+1 {{'array.write' op has too many indexed dimensions: expected 3 but found 4}}
  array.write %r[%1,%0,%1,%0] = %0 : !llzk.array<2,4,3 x index>, index
}
