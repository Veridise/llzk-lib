// RUN: llzk-opt -split-input-file -verify-diagnostics %s

// TESTS: invalid type for a global
module attributes {veridise.lang = "llzk"} {
  // expected-error@+1 {{'llzk.global' op attribute 'type' failed to satisfy constraint: type attribute of any LLZK type except non-constant types}}
  llzk.global @g : !llzk.tvar<@X>
}
// -----
module attributes {veridise.lang = "llzk"} {
  // expected-error@+1 {{'llzk.global' op attribute 'type' failed to satisfy constraint: type attribute of any LLZK type except non-constant types}}
  llzk.global @g : i64
}
// -----
module attributes {veridise.lang = "llzk"} {
  // expected-error@+1 {{'llzk.global' op attribute 'type' failed to satisfy constraint: type attribute of any LLZK type except non-constant types}}
  llzk.global @g : !array.array<@b x i1>
}
// -----
module attributes {veridise.lang = "llzk"} {
  // expected-error@+1 {{'llzk.global' op attribute 'type' failed to satisfy constraint: type attribute of any LLZK type except non-constant types}}
  llzk.global @g : !array.array<affine_map<(i)->(i)> x i1>
}
// -----
module attributes {veridise.lang = "llzk"} {
  // expected-error@+1 {{'llzk.global' op attribute 'type' failed to satisfy constraint: type attribute of any LLZK type except non-constant types}}
  llzk.global @g : !llzk.struct<@Component01<[affine_map<(i)->(i)>]>>
  llzk.struct @Component01<[@N]> {
    function.def @compute() -> !llzk.struct<@Component01<[@N]>> {
      %self = llzk.new_struct : !llzk.struct<@Component01<[@N]>>
      function.return %self : !llzk.struct<@Component01<[@N]>>
    }
    function.def @constrain(%self: !llzk.struct<@Component01<[@N]>>) { function.return }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  // expected-error@+1 {{'llzk.global' op attribute 'type' failed to satisfy constraint: type attribute of any LLZK type except non-constant types}}
  llzk.global @g : !llzk.struct<@Component01<[@any_symbol_here_is_illegal]>>
  llzk.struct @Component01<[@N]> {
    function.def @compute() -> !llzk.struct<@Component01<[@N]>> {
      %self = llzk.new_struct : !llzk.struct<@Component01<[@N]>>
      function.return %self : !llzk.struct<@Component01<[@N]>>
    }
    function.def @constrain(%self: !llzk.struct<@Component01<[@N]>>) { function.return }
  }
}
// -----

// TEST: 'felt' type global initialized with unparsable attribute
module attributes {veridise.lang = "llzk"} {
  // expected-error@+2 {{custom op 'llzk.global' expected integer value}}
  // expected-error@+1 {{custom op 'llzk.global' failed to parse LLZK_FeltConstAttr parameter 'value' which is to be a `::llvm::APInt`}}
  llzk.global const @g : !felt.felt = unparseable
}
// -----

// TEST: any other type global initialized with unparsable attribute
module attributes {veridise.lang = "llzk"} {
  // expected-error@+1 {{expected attribute value}}
  llzk.global const @g : index = unparseable
}
// -----

// TEST: 'felt' type global initialized with incorrect attribute kind
module attributes {veridise.lang = "llzk"} {
  // expected-error@+2 {{custom op 'llzk.global' expected integer value}}
  // expected-error@+1 {{custom op 'llzk.global' failed to parse LLZK_FeltConstAttr parameter 'value' which is to be a `::llvm::APInt`}}
  llzk.global const @g : !felt.felt = "Hello"
}
// -----

// TESTS: any other type global initialized with incorrect attribute kind
module attributes {veridise.lang = "llzk"} {
  // expected-error@+1 {{integer constant out of range for attribute}}
  llzk.global const @g : i1 = 123
}
// -----
module attributes {veridise.lang = "llzk"} {
  // expected-error@+1 {{integer constant out of range for attribute}}
  llzk.global const @g : i1 = -2
}
// -----
module attributes {veridise.lang = "llzk"} {
  // expected-error@+1 {{'llzk.global' op with type 'i1' expected builtin.bool or builtin.integer attribute value but found builtin.array}}
  llzk.global const @g : i1 = [7,8,0]
}
// -----
module attributes {veridise.lang = "llzk"} {
  // expected-error@+1 {{'llzk.global' op with type 'index' expected builtin.index attribute value but found builtin.bool}}
  llzk.global const @g : index = false
}
// -----
module attributes {veridise.lang = "llzk"} {
  // expected-error@+1 {{'llzk.global' op with type 'index' expected builtin.index attribute value but found builtin.string}}
  llzk.global const @g : index = "hello"
}
// -----
module attributes {veridise.lang = "llzk"} {
  // expected-error@+1 {{integer literal not valid for specified type}}
  llzk.global const @g : !string.string = 65723456
}
// -----
module attributes {veridise.lang = "llzk"} {
  // expected-error@+1 {{'llzk.global' op with type '!array.array<2 x !string.string>' expected builtin.array attribute value but found builtin.string}}
  llzk.global const @g : !array.array<2 x !string.string> = "a string and something else"
}
// -----
module attributes {veridise.lang = "llzk"} {
  // expected-error@+1 {{'llzk.global' op with type '!array.array<2 x !string.string>' expected builtin.array attribute value to contain 2 elements but found 0}}
  llzk.global const @g : !array.array<2 x !string.string> = []
}
// -----
module attributes {veridise.lang = "llzk"} {
  // expected-error@+1 {{'llzk.global' op with type '!array.array<2 x !string.string>' expected builtin.string array element but found builtin.integer}}
  llzk.global const @g : !array.array<2 x !string.string> = [7, 8]
}
// -----
module attributes {veridise.lang = "llzk"} {
  // expected-error@+1 {{'llzk.global' op with type '!array.array<2 x !string.string>' expected builtin.array attribute value to contain 2 elements but found 3}}
  llzk.global const @g : !array.array<2 x !string.string> = ["too", "many", "strings"]
}
// -----
module attributes {veridise.lang = "llzk"} {
  // expected-error@+2 {{'llzk.global' op with type '!array.array<4 x !string.string>' expected builtin.string array element but found builtin.integer}}
  // expected-error@+1 {{'llzk.global' op with type '!array.array<4 x !string.string>' expected builtin.string array element but found builtin.affine_map}}
  llzk.global const @g : !array.array<4 x !string.string> = ["a string and something else", false, affine_map<(i)->(i)>, -100]
}
// -----
module attributes {veridise.lang = "llzk"} {
  // expected-error@+1 {{'llzk.global' op with type '!array.array<2,2,1,2 x !felt.felt>' expected builtin.array attribute value to contain 8 elements but found 4}}
  llzk.global const @g : !array.array<2,2,1,2 x !felt.felt> = [6, 12134, 45523, 15623]
}
// -----

// TEST: 'felt' type global initialized with incorrect attribute kind
module attributes {veridise.lang = "llzk"} {
  // expected-error@+2 {{custom op 'llzk.global' expected integer value}}
  // expected-error@+1 {{custom op 'llzk.global' failed to parse LLZK_FeltConstAttr parameter 'value' which is to be a `::llvm::APInt`}}
  llzk.global const @g : !felt.felt = "Hello"
}
// -----

// TEST: global with "const" not initialized
module attributes {veridise.lang = "llzk"} {
  // expected-error@+1 {{'llzk.global' op marked as 'const' must be assigned a value}}
  llzk.global const @g : !string.string
}
// -----

// TEST: GlobalReadOp with unknown symbol name
module attributes {veridise.lang = "llzk"} {
  llzk.global @g : !string.string = "Hello"
  llzk.struct @Component02 {
    function.def @compute() -> !llzk.struct<@Component02> {
      // expected-error@+1 {{'llzk.readg' op references unknown symbol "@unknown"}}
      %t = llzk.readg @unknown : !string.string
      %self = llzk.new_struct : !llzk.struct<@Component02>
      function.return %self : !llzk.struct<@Component02>
    }
    function.def @constrain(%self: !llzk.struct<@Component02>) { function.return }
  }
}
// -----

// TEST: GlobalReadOp with type that does not match the GlobalDefOp
module attributes {veridise.lang = "llzk"} {
  llzk.global @g : !felt.felt = 6546
  llzk.struct @Component03 {
    function.def @compute() -> !llzk.struct<@Component03> {
      // expected-error@+1 {{'llzk.readg' op has wrong type; expected '!felt.felt', got '!string.string'}}
      %t = llzk.readg @g : !string.string
      %self = llzk.new_struct : !llzk.struct<@Component03>
      function.return %self : !llzk.struct<@Component03>
    }
    function.def @constrain(%self: !llzk.struct<@Component03>) { function.return }
  }
}
// -----

// TEST: GlobalWriteOp with unknown symbol name
module attributes {veridise.lang = "llzk"} {
  llzk.global @g : !felt.felt = 125
  llzk.struct @Component04 {
    function.def @compute() -> !llzk.struct<@Component04> {
      %c = felt.const 99999
      // expected-error@+1 {{'llzk.writeg' op references unknown symbol "@unknown"}}
      llzk.writeg @unknown = %c : !felt.felt
      %self = llzk.new_struct : !llzk.struct<@Component04>
      function.return %self : !llzk.struct<@Component04>
    }
    function.def @constrain(%self: !llzk.struct<@Component04>) { function.return }
  }
}
// -----

// TEST: GlobalWriteOp with type that does not match the GlobalDefOp
module attributes {veridise.lang = "llzk"} {
  llzk.global @g : index = 125
  llzk.struct @Component05 {
    function.def @compute() -> !llzk.struct<@Component05> {
      %c = felt.const 99999
      // expected-error@+1 {{'llzk.writeg' op has wrong type; expected 'index', got '!felt.felt'}}
      llzk.writeg @g = %c : !felt.felt
      %self = llzk.new_struct : !llzk.struct<@Component05>
      function.return %self : !llzk.struct<@Component05>
    }
    function.def @constrain(%self: !llzk.struct<@Component05>) { function.return }
  }
}
// -----

// TEST: GlobalWriteOp with type that does not Value
module attributes {veridise.lang = "llzk"} {
  llzk.global @g : !felt.felt = 125
  llzk.struct @Component06 {
    function.def @compute() -> !llzk.struct<@Component06> {
      %c = arith.constant true // expected-note {{prior use here}}
      // expected-error@+1 {{use of value '%c' expects different type than prior uses: '!felt.felt' vs 'i1'}}
      llzk.writeg @g = %c : !felt.felt
      %self = llzk.new_struct : !llzk.struct<@Component06>
      function.return %self : !llzk.struct<@Component06>
    }
    function.def @constrain(%self: !llzk.struct<@Component06>) { function.return }
  }
}
// -----

// TEST: GlobalWriteOp targeting a "const" GlobalDefOp
module attributes {veridise.lang = "llzk"} {
  llzk.global const @t : i1 = true
  llzk.struct @Component07 {
    function.def @compute() -> !llzk.struct<@Component07> {
      %c = arith.constant false
      // expected-error@+1 {{'llzk.writeg' op cannot target 'llzk.global' marked as 'const'}}
      llzk.writeg @t = %c : i1
      %self = llzk.new_struct : !llzk.struct<@Component07>
      function.return %self : !llzk.struct<@Component07>
    }
    function.def @constrain(%self: !llzk.struct<@Component07>) { function.return }
  }
}
// -----

// TEST: Struct parameter conflicts with a global name
module attributes {veridise.lang = "llzk"} {
  llzk.global const @N : index = 3 // expected-note {{symbol already defined here}}
  // expected-error@+1 {{'llzk.struct' op parameter name "@N" conflicts with an existing symbol}}
  llzk.struct @Component08<[@N]> {
    function.def @compute() -> !llzk.struct<@Component08<[@N]>> {
      %self = llzk.new_struct : !llzk.struct<@Component08<[@N]>>
      function.return %self : !llzk.struct<@Component08<[@N]>>
    }
    function.def @constrain(%self: !llzk.struct<@Component08<[@N]>>) { function.return }
  }
}
// -----

// TEST: `writeg` cannot be used in a "constrain" function
module attributes {veridise.lang = "llzk"} {
  llzk.global @t : i1 = true
  llzk.struct @Component09 {
    function.def @compute() -> !llzk.struct<@Component09> {
      %self = llzk.new_struct : !llzk.struct<@Component09>
      function.return %self : !llzk.struct<@Component09>
    }
    function.def @constrain(%self: !llzk.struct<@Component09>) {
      %c = arith.constant false
      // expected-error@+1 {{'llzk.writeg' op only valid within a 'function.def' with 'function.allow_witness' attribute}}
      llzk.writeg @t = %c : i1
      function.return
    }
  }
}
// -----

// TEST: `writeg` cannot be used in a free function
module attributes {veridise.lang = "llzk"} {
  llzk.global @g : !felt.felt
  function.def @test() {
    %c = felt.const 99999
    // expected-error@+1 {{'llzk.writeg' op only valid within a 'function.def' with 'function.allow_witness' attribute}}
    llzk.writeg @g = %c : !felt.felt
    function.return
  }
}
