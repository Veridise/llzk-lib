// RUN: llzk-opt -split-input-file %s | FileCheck %s

// TEST: uninitialized globals
module attributes {veridise.lang = "llzk"} {
  global.def @s : !string.string
  global.def @i : index
  global.def @b : i1
  global.def @f : !felt.felt

  global.def @ax : !array.type<3 x !string.string>
  global.def @ab : !array.type<2,2 x i1>
  global.def @af : !array.type<6,5,3 x !felt.felt>
  global.def @ai : !array.type<3,3 x index>

  global.def @c : !struct.type<@Component01B>
  global.def @c2s : !struct.type<@Component01A<[!string.string]>>
  global.def @c2i : !struct.type<@Component01A<[23423]>>
  global.def @c2a : !struct.type<@Component01A<[!array.type<2,2 x i1>]>>

  struct.def @Component01A<[@Z]> {
    function.def @compute() -> !struct.type<@Component01A<[@Z]>> {
      %self = struct.new : !struct.type<@Component01A<[@Z]>>
      function.return %self : !struct.type<@Component01A<[@Z]>>
    }
    function.def @constrain(%self: !struct.type<@Component01A<[@Z]>>) { function.return }
  }
  struct.def @Component01B {
    function.def @compute() -> !struct.type<@Component01B> {
      %self = struct.new : !struct.type<@Component01B>
      function.return %self : !struct.type<@Component01B>
    }
    function.def @constrain(%self: !struct.type<@Component01B>) { function.return }
  }
}
// CHECK-DAG: global.def @s : !string.string
// CHECK-DAG: global.def @i : index
// CHECK-DAG: global.def @b : i1
// CHECK-DAG: global.def @f : !felt.felt
// CHECK-DAG: global.def @ax : !array.type<3 x !string.string>
// CHECK-DAG: global.def @ab : !array.type<2,2 x i1>
// CHECK-DAG: global.def @af : !array.type<6,5,3 x !felt.felt>
// CHECK-DAG: global.def @ai : !array.type<3,3 x index>
// CHECK-DAG: global.def @c2s : !struct.type<@Component01A<[!string.string]>>
// CHECK-DAG: global.def @c2i : !struct.type<@Component01A<[23423]>>
// CHECK-DAG: global.def @c2a : !struct.type<@Component01A<[!array.type<2,2 x i1>]>>
// -----

// TEST: initialized globals
module attributes {veridise.lang = "llzk"} {
  global.def const @s : !string.string = "Hello World!"
  global.def @i : index = 1

  global.def const @bf : i1 = false
  global.def const @t : i1 = true
  global.def @b0 : i1 = 0
  global.def const @b1 : i1 = 1

  global.def @f : !felt.felt = 35

  global.def @a1i : !array.type<1 x index> = [2]
  global.def @a2s : !array.type<2 x !string.string> = ["hello", "world"]
  global.def @a4b : !array.type<2,2 x i1> = [0,1,1,0]
  global.def @a4f : !array.type<2,1,1,2 x !felt.felt> = [6, 12134, 45523, 15623]
}
// CHECK-DAG: global.def const @s : !string.string = "Hello World!"
// CHECK-DAG: global.def @i : index = 1
// CHECK-DAG: global.def const @bf : i1 = false
// CHECK-DAG: global.def const @t : i1 = true
// CHECK-DAG: global.def @b0 : i1 = false
// CHECK-DAG: global.def const @b1 : i1 = true
// CHECK-DAG: global.def @f : !felt.felt = 35
// CHECK-DAG: global.def @a1i : !array.type<1 x index> = [2]
// CHECK-DAG: global.def @a2s : !array.type<2 x !string.string> = ["hello", "world"]
// CHECK-DAG: global.def @a4b : !array.type<2,2 x i1> = [0, 1, 1, 0]
// CHECK-DAG: global.def @a4f : !array.type<2,1,1,2 x !felt.felt> = [6, 12134, 45523, 15623]
// -----

// TEST: GlobalReadOp in "compute()"
module attributes {veridise.lang = "llzk"} {
  global.def const @g : !felt.felt = 23452
  struct.def @Component02 {
    function.def @compute() -> !struct.type<@Component02> {
      %t = global.read @g : !felt.felt
      %self = struct.new : !struct.type<@Component02>
      function.return %self : !struct.type<@Component02>
    }
    function.def @constrain(%self: !struct.type<@Component02>) { function.return }
  }
}
// CHECK-LABEL: global.def const @g : !felt.felt = 23452
// CHECK-LABEL: struct.def @Component02 {
// CHECK-NEXT:    function.def @compute() -> !struct.type<@Component02> attributes {function.allow_witness} {
// CHECK-NEXT:      %[[V0:[0-9a-zA-Z_\.]+]] = global.read @g : !felt.felt
// CHECK-NEXT:      %[[V1:[0-9a-zA-Z_\.]+]] = struct.new : <@Component02>
// CHECK-NEXT:      function.return %[[V1]] : !struct.type<@Component02>
// CHECK-NEXT:    }
// CHECK-NEXT:    function.def @constrain(%[[V2:[0-9a-zA-Z_\.]+]]: !struct.type<@Component02>) attributes {function.allow_constraint} {
// CHECK-NEXT:      function.return
// CHECK-NEXT:    }
// CHECK-NEXT:  }
// -----

// TEST: GlobalReadOp in "constrain()"
module attributes {veridise.lang = "llzk"} {
  global.def const @g : i1 = false
  struct.def @Component03 {
    function.def @compute() -> !struct.type<@Component03> {
      %self = struct.new : !struct.type<@Component03>
      function.return %self : !struct.type<@Component03>
    }
    function.def @constrain(%self: !struct.type<@Component03>) {
      %t = global.read @g : i1
      function.return
    }
  }
}
// CHECK-LABEL: global.def const @g : i1 = false
// CHECK-LABEL: struct.def @Component03 {
// CHECK-NEXT:    function.def @compute() -> !struct.type<@Component03> attributes {function.allow_witness} {
// CHECK-NEXT:      %[[V0:[0-9a-zA-Z_\.]+]] = struct.new : <@Component03>
// CHECK-NEXT:      function.return %[[V0]] : !struct.type<@Component03>
// CHECK-NEXT:    }
// CHECK-NEXT:    function.def @constrain(%[[V1:[0-9a-zA-Z_\.]+]]: !struct.type<@Component03>) attributes {function.allow_constraint} {
// CHECK-NEXT:      %[[V2:[0-9a-zA-Z_\.]+]] = global.read @g : i1
// CHECK-NEXT:      function.return
// CHECK-NEXT:    }
// CHECK-NEXT:  }
// -----

// TEST: GlobalReadOp in free function
module attributes {veridise.lang = "llzk"} {
  global.def const @g : !string.string = "Hello"
  function.def @test() -> !string.string {
    %t = global.read @g : !string.string
    function.return %t : !string.string
  }
}
// CHECK-LABEL: global.def const @g : !string.string = "Hello"
// CHECK-LABEL: function.def @test() -> !string.string {
// CHECK-NEXT:    %[[V0:[0-9a-zA-Z_\.]+]] = global.read @g : !string.string
// CHECK-NEXT:    function.return %[[V0]] : !string.string
// CHECK-NEXT:  }
// -----

// TEST: GlobalWriteOp (can only be within "compute()")
module attributes {veridise.lang = "llzk"} {
  global.def @g : !felt.felt = 125
  struct.def @Component04 {
    function.def @compute() -> !struct.type<@Component04> {
      %c = felt.const 99999
      global.write @g = %c : !felt.felt
      %self = struct.new : !struct.type<@Component04>
      function.return %self : !struct.type<@Component04>
    }
    function.def @constrain(%self: !struct.type<@Component04>) { function.return }
  }
}
// CHECK-LABEL: global.def @g : !felt.felt = 125
// CHECK-LABEL: struct.def @Component04 {
// CHECK-NEXT:    function.def @compute() -> !struct.type<@Component04> attributes {function.allow_witness} {
// CHECK-NEXT:      %[[V0:[0-9a-zA-Z_\.]+]] = felt.const 99999
// CHECK-NEXT:      global.write @g = %[[V0]] : !felt.felt
// CHECK-NEXT:      %[[V1:[0-9a-zA-Z_\.]+]] = struct.new : <@Component04>
// CHECK-NEXT:      function.return %[[V1]] : !struct.type<@Component04>
// CHECK-NEXT:    }
// CHECK-NEXT:    function.def @constrain(%[[V2:[0-9a-zA-Z_\.]+]]: !struct.type<@Component04>) attributes {function.allow_constraint} {
// CHECK-NEXT:      function.return
// CHECK-NEXT:    }
// CHECK-NEXT:  }
// -----

// TEST: Using a global value as a type parameter
module attributes {veridise.lang = "llzk"} {
  global.def const @N : index = 3

  function.def @b(%x: !array.type<@N x !felt.felt>) {
    function.return
  }

  struct.def @Component05A<[@Z]> {
    function.def @compute() -> !struct.type<@Component05A<[@Z]>> {
      %self = struct.new : !struct.type<@Component05A<[@Z]>>
      function.return %self : !struct.type<@Component05A<[@Z]>>
    }
    function.def @constrain(%self: !struct.type<@Component05A<[@Z]>>) { function.return }
  }
  struct.def @Component05B {
    struct.field @f : !struct.type<@Component05A<[@N]>>

    function.def @compute() -> !struct.type<@Component05B> {
      %self = struct.new : !struct.type<@Component05B>
      function.return %self : !struct.type<@Component05B>
    }
    function.def @constrain(%self: !struct.type<@Component05B>) { function.return }
  }
}
// CHECK-LABEL: module attributes {veridise.lang = "llzk"} {
// CHECK-NEXT:    global.def const @N : index = 3
// CHECK-NEXT:    function.def @b(%[[V0:[0-9a-zA-Z_\.]+]]: !array.type<@N x !felt.felt>) {
// CHECK-NEXT:      function.return
// CHECK-NEXT:    }
// CHECK-NEXT:    struct.def @Component05A<[@Z]> {
// CHECK-NEXT:      function.def @compute() -> !struct.type<@Component05A<[@Z]>> attributes {function.allow_witness} {
// CHECK-NEXT:        %[[V1:[0-9a-zA-Z_\.]+]] = struct.new : <@Component05A<[@Z]>>
// CHECK-NEXT:        function.return %[[V1]] : !struct.type<@Component05A<[@Z]>>
// CHECK-NEXT:      }
// CHECK-NEXT:      function.def @constrain(%[[V2:[0-9a-zA-Z_\.]+]]: !struct.type<@Component05A<[@Z]>>) attributes {function.allow_constraint} {
// CHECK-NEXT:        function.return
// CHECK-NEXT:      }
// CHECK-NEXT:    }
// CHECK-NEXT:    struct.def @Component05B {
// CHECK-NEXT:      struct.field @f : !struct.type<@Component05A<[@N]>>
// CHECK-NEXT:      function.def @compute() -> !struct.type<@Component05B> attributes {function.allow_witness} {
// CHECK-NEXT:        %[[V3:[0-9a-zA-Z_\.]+]] = struct.new : <@Component05B>
// CHECK-NEXT:        function.return %[[V3]] : !struct.type<@Component05B>
// CHECK-NEXT:      }
// CHECK-NEXT:      function.def @constrain(%[[V4:[0-9a-zA-Z_\.]+]]: !struct.type<@Component05B>) attributes {function.allow_constraint} {
// CHECK-NEXT:        function.return
// CHECK-NEXT:      }
// CHECK-NEXT:    }
// CHECK-NEXT:  }
// -----

// TEST: Read and write involving a struct-type global
module attributes {veridise.lang = "llzk"} {
  global.def @gvar : !struct.type<@Component06<[4]>>

  struct.def @Component06<[@Z]> {
    function.def @compute() -> !struct.type<@Component06<[@Z]>> {
      %self = struct.new : !struct.type<@Component06<[@Z]>>
      function.return %self : !struct.type<@Component06<[@Z]>>
    }
    function.def @constrain(%self: !struct.type<@Component06<[@Z]>>) { function.return }
  }

  struct.def @Main {
    function.def @compute() -> !struct.type<@Main> {
      %s = function.call @Component06::@compute() : () -> !struct.type<@Component06<[4]>>
      global.write @gvar = %s : !struct.type<@Component06<[4]>>
      %self = struct.new : !struct.type<@Main>
      function.return %self : !struct.type<@Main>
    }
    function.def @constrain(%self: !struct.type<@Main>) {
      %s = global.read @gvar : !struct.type<@Component06<[4]>>
      function.return
    }
  }
}
// CHECK-LABEL:   global.def @gvar : !struct.type<@Component06<[4]>>
//
// CHECK-LABEL:   struct.def @Component06<[@Z]> {
// CHECK-NEXT:      function.def @compute() -> !struct.type<@Component06<[@Z]>> attributes {function.allow_witness} {
// CHECK-NEXT:        %[[V0:[0-9a-zA-Z_\.]+]] = struct.new : <@Component06<[@Z]>>
// CHECK-NEXT:        function.return %[[V0]] : !struct.type<@Component06<[@Z]>>
// CHECK-NEXT:      }
// CHECK-NEXT:      function.def @constrain(%[[V1:[0-9a-zA-Z_\.]+]]: !struct.type<@Component06<[@Z]>>) attributes {function.allow_constraint} {
// CHECK-NEXT:        function.return
// CHECK-NEXT:      }
// CHECK-NEXT:    }
//
// CHECK-LABEL:   struct.def @Main {
// CHECK-NEXT:      function.def @compute() -> !struct.type<@Main> attributes {function.allow_witness} {
// CHECK-NEXT:        %[[V0:[0-9a-zA-Z_\.]+]] = function.call @Component06::@compute() : () -> !struct.type<@Component06<[4]>>
// CHECK-NEXT:        global.write @gvar = %[[V0]] : !struct.type<@Component06<[4]>>
// CHECK-NEXT:        %[[V1:[0-9a-zA-Z_\.]+]] = struct.new : <@Main>
// CHECK-NEXT:        function.return %[[V1]] : !struct.type<@Main>
// CHECK-NEXT:      }
// CHECK-NEXT:      function.def @constrain(%[[V2:[0-9a-zA-Z_\.]+]]: !struct.type<@Main>) attributes {function.allow_constraint} {
// CHECK-NEXT:        %[[V3:[0-9a-zA-Z_\.]+]] = global.read @gvar : !struct.type<@Component06<[4]>>
// CHECK-NEXT:        function.return
// CHECK-NEXT:      }
// CHECK-NEXT:    }
