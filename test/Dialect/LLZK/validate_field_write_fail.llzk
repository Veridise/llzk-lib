// RUN: llzk-opt -split-input-file -llzk-validate-field-writes -verify-diagnostics %s

module attributes {veridise.lang = "llzk"} {
  llzk.struct @MultipleWrites {
    llzk.field @f : !felt.felt

    function.def @compute(%1: !felt.felt) -> !llzk.struct<@MultipleWrites> {
      %self = new_struct : !llzk.struct<@MultipleWrites>
      // expected-note@+1 {{earlier write here}}
      writef %self[@f] = %1 : !llzk.struct<@MultipleWrites>, !felt.felt
      // expected-warning@+1 {{found multiple writes to 'llzk.field' named "@f"}}
      writef %self[@f] = %1 : !llzk.struct<@MultipleWrites>, !felt.felt
      function.return %self : !llzk.struct<@MultipleWrites>
    }

    function.def @constrain(%self: !llzk.struct<@MultipleWrites>, %1: !felt.felt)  { function.return }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  llzk.struct @MissingWrite {
    llzk.field @a : index
    llzk.field @b : index
    llzk.field @c : index

    // expected-warning@+1 {{'function.def' op "@compute" missing write to 'llzk.field' named "@b"}}
    function.def @compute(%1: index) -> !llzk.struct<@MissingWrite> {
      %self = new_struct : !llzk.struct<@MissingWrite>
      writef %self[@a] = %1 : !llzk.struct<@MissingWrite>, index
      writef %self[@c] = %1 : !llzk.struct<@MissingWrite>, index
      function.return %self : !llzk.struct<@MissingWrite>
    }

    function.def @constrain(%self: !llzk.struct<@MissingWrite>, %1: index)  { function.return }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  llzk.struct @NoWrites {
    llzk.field @a : !felt.felt
    llzk.field @b : !felt.felt
    llzk.field @c : !felt.felt

    // expected-warning@+3 {{'function.def' op "@compute" missing write to 'llzk.field' named "@a"}}
    // expected-warning@+2 {{'function.def' op "@compute" missing write to 'llzk.field' named "@b"}}
    // expected-warning@+1 {{'function.def' op "@compute" missing write to 'llzk.field' named "@c"}}
    function.def @compute() -> !llzk.struct<@NoWrites> {
      %self = new_struct : !llzk.struct<@NoWrites>
      function.return %self : !llzk.struct<@NoWrites>
    }

    function.def @constrain(%self: !llzk.struct<@NoWrites>)  { function.return }
  }
}
