// RUN: llzk-opt -I %input_dir -split-input-file -llzk-inline-includes -llzk-print-constraint-dependency-graphs %s 2>&1 | FileCheck --enable-var-scope %s

module attributes {veridise.lang = "llzk"} {
  llzk.struct @NoConstraints {
    function.def @compute(%a: !felt.felt) -> !llzk.struct<@NoConstraints> {
      %self = llzk.new_struct : !llzk.struct<@NoConstraints>
      function.return %self : !llzk.struct<@NoConstraints>
    }

    function.def @constrain(%self: !llzk.struct<@NoConstraints>, %a: !felt.felt) {
      function.return
    }
  }
}

// CHECK-LABEL: @NoConstraints ConstraintDependencyGraph { }

// -----

module attributes {veridise.lang = "llzk"} {

  function.def @global_add(%a: !felt.felt, %b: !felt.felt) -> !felt.felt {
    %c = felt.add %a, %b
    function.return %c : !felt.felt
  }

  llzk.struct @Adder {
    llzk.field @sum : !felt.felt {llzk.pub}

    function.def @compute(%a: !felt.felt, %b: !felt.felt) -> !llzk.struct<@Adder> {
      %self = llzk.new_struct : !llzk.struct<@Adder>
      %sum = function.call @global_add(%a, %b) : (!felt.felt, !felt.felt) -> (!felt.felt)
      llzk.writef %self[@sum] = %sum : !llzk.struct<@Adder>, !felt.felt
      function.return %self : !llzk.struct<@Adder>
    }

    function.def @constrain(%self: !llzk.struct<@Adder>, %a: !felt.felt, %b: !felt.felt) {
      %sum = llzk.readf %self[@sum] : !llzk.struct<@Adder>, !felt.felt
      %c = function.call @global_add(%a, %b) : (!felt.felt, !felt.felt) -> (!felt.felt)
      llzk.emit_eq %sum, %c : !felt.felt
      function.return
    }
  }
}

// CHECK-LABEL: @Adder ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@sum], %arg1, %arg2 }
// CHECK-NEXT: }

// -----

module attributes {veridise.lang = "llzk"} {

  function.def @global_add(%a: !felt.felt, %b: !felt.felt) -> !felt.felt {
    %c = felt.add %a, %b
    function.return %c : !felt.felt
  }

  llzk.struct @Adder2 {
    llzk.field @sum : !felt.felt {llzk.pub}

    function.def @compute(%a: !felt.felt, %b: !felt.felt) -> !llzk.struct<@Adder2> {
      %self = llzk.new_struct : !llzk.struct<@Adder2>
      %sum = function.call @global_add(%a, %b) : (!felt.felt, !felt.felt) -> (!felt.felt)
      llzk.writef %self[@sum] = %sum : !llzk.struct<@Adder2>, !felt.felt
      function.return %self : !llzk.struct<@Adder2>
    }

    function.def @constrain(%self: !llzk.struct<@Adder2>, %a: !felt.felt, %b: !felt.felt) {
      %sum = llzk.readf %self[@sum] : !llzk.struct<@Adder2>, !felt.felt
      %c = function.call @global_add(%a, %b) : (!felt.felt, !felt.felt) -> (!felt.felt)
      %d = felt.add %c, %c
      llzk.emit_eq %sum, %d : !felt.felt
      function.return
    }
  }
}

// CHECK-LABEL: @Adder2 ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@sum], %arg1, %arg2 }
// CHECK-NEXT: }

// -----

module attributes {veridise.lang = "llzk"} {
  llzk.struct @ComponentB {
    function.def @constrain(%self: !llzk.struct<@ComponentB>, %a: !felt.felt, %b: !array.array<5 x !felt.felt>) {
      llzk.emit_in %b, %a : !array.array<5 x !felt.felt>
      function.return
    }

    function.def @compute(%a: !felt.felt, %b: !array.array<5 x !felt.felt>) -> !llzk.struct<@ComponentB> {
      %self = llzk.new_struct : !llzk.struct<@ComponentB>
      function.return %self : !llzk.struct<@ComponentB>
    }
  }
}

// CHECK-LABEL: @ComponentB ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg1, %arg2 }
// CHECK-NEXT: }

// -----

module attributes {veridise.lang = "llzk"} {

  llzk.struct @EnsureZero {
    function.def @compute(%a: !felt.felt) -> !llzk.struct<@EnsureZero> {
      %self = llzk.new_struct : !llzk.struct<@EnsureZero>
      function.return %self : !llzk.struct<@EnsureZero>
    }

    function.def @constrain(%self: !llzk.struct<@EnsureZero>, %a: !felt.felt) {
      %zero = felt.const 0
      llzk.emit_eq %a, %zero : !felt.felt
      function.return
    }
  }

  llzk.struct @EnsureBothZero {
    function.def @compute(%a: !felt.felt, %b: !felt.felt) -> !llzk.struct<@EnsureBothZero> {
      %self = llzk.new_struct : !llzk.struct<@EnsureBothZero>
      function.return %self : !llzk.struct<@EnsureBothZero>
    }

    // NOTE: This test case demonstrates where this analysis is currently overly conservative,
    // as it does not determine that %a and %b must be precisely equal to 0.
    function.def @constrain(%self: !llzk.struct<@EnsureBothZero>, %a: !felt.felt, %b: !felt.felt) {
      %zero = felt.const 0
      llzk.emit_eq %a, %b : !felt.felt
      llzk.emit_eq %a, %zero : !felt.felt
      function.return
    }
  }
}

// CHECK-LABEL: @EnsureZero ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg1, <felt.const: 0> }
// CHECK-NEXT: }
// CHECK-NEXT: @EnsureBothZero ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg1, %arg2 },
// CHECK-NEXT:     { %arg1, <felt.const: 0> }
// CHECK-NEXT: }

// -----

module attributes {veridise.lang = "llzk"} {

  llzk.struct @Passthrough {
    llzk.field @out : !felt.felt {llzk.pub}

    function.def @compute(%in: !felt.felt) -> !llzk.struct<@Passthrough> {
      %self = llzk.new_struct : !llzk.struct<@Passthrough>
      llzk.writef %self[@out] = %in : !llzk.struct<@Passthrough>, !felt.felt
      function.return %self : !llzk.struct<@Passthrough>
    }

    function.def @constrain(%self: !llzk.struct<@Passthrough>, %in: !felt.felt) {
      %out = llzk.readf %self[@out] : !llzk.struct<@Passthrough>, !felt.felt
      llzk.emit_eq %in, %out : !felt.felt
      function.return
    }
  }

  llzk.struct @EnsureIsZero {
    llzk.field @p : !llzk.struct<@Passthrough>

    function.def @compute(%in: !felt.felt) -> !llzk.struct<@EnsureIsZero> {
      %self = llzk.new_struct : !llzk.struct<@EnsureIsZero>
      %p_0 = function.call @Passthrough::@compute(%in) : (!felt.felt) -> (!llzk.struct<@Passthrough>)
      llzk.writef %self[@p] = %p_0 : !llzk.struct<@EnsureIsZero>, !llzk.struct<@Passthrough>
      function.return %self : !llzk.struct<@EnsureIsZero>
    }

    function.def @constrain(%self: !llzk.struct<@EnsureIsZero>, %in: !felt.felt) {
      %zero = felt.const 0
      %p_0 = llzk.readf %self[@p] : !llzk.struct<@EnsureIsZero>, !llzk.struct<@Passthrough>
      function.call @Passthrough::@constrain(%p_0, %zero) : (!llzk.struct<@Passthrough>, !felt.felt) -> ()
      %out = llzk.readf %p_0[@out] : !llzk.struct<@Passthrough>, !felt.felt
      llzk.emit_eq %in, %out : !felt.felt
      function.return
    }
  }
}

// CHECK-LABEL: @Passthrough ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@out], %arg1 }
// CHECK-NEXT: }
// CHECK-NEXT: @EnsureIsZero ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@p][@out], %arg1 },
// CHECK-NEXT:     { %arg0[@p][@out], <felt.const: 0> }
// CHECK-NEXT: }

// -----

module attributes {veridise.lang = "llzk"} {
  llzk.struct @ArrayCheck {
    function.def @constrain(%self: !llzk.struct<@ArrayCheck>, %in: !array.array<5 x !felt.felt>) {
      %fval = felt.const 7
      %idx = arith.constant 3 : index
      %elem = array.read %in[%idx] : !array.array<5 x !felt.felt>, !felt.felt
      llzk.emit_eq %elem, %fval : !felt.felt
      function.return
    }

    function.def @compute(%in: !array.array<5 x !felt.felt>) -> !llzk.struct<@ArrayCheck> {
      %self = llzk.new_struct : !llzk.struct<@ArrayCheck>
      function.return %self : !llzk.struct<@ArrayCheck>
    }
  }
}

// CHECK-LABEL: @ArrayCheck ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg1[3], <felt.const: 7> }
// CHECK-NEXT: }

// -----

module attributes {veridise.lang = "llzk"} {
  llzk.struct @ArrayForCheck {
    function.def @constrain(%self: !llzk.struct<@ArrayForCheck>, %in: !array.array<5 x !felt.felt>) {
      %lb = arith.constant 0 : index
      %up = arith.constant 5 : index
      %step = arith.constant 1 : index
      %fval = felt.const 7
      scf.for %iv = %lb to %up step %step {
        %elem = array.read %in[%iv] : !array.array<5 x !felt.felt>, !felt.felt
        llzk.emit_eq %elem, %fval : !felt.felt
      }
      function.return
    }

    function.def @compute(%in: !array.array<5 x !felt.felt>) -> !llzk.struct<@ArrayForCheck> {
      %self = llzk.new_struct : !llzk.struct<@ArrayForCheck>
      function.return %self : !llzk.struct<@ArrayForCheck>
    }
  }
}

// CHECK-LABEL: @ArrayForCheck ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg1[0:5], <felt.const: 7> }
// CHECK-NEXT: }

// -----

module attributes {veridise.lang = "llzk"} {

  llzk.struct @ConstConstraints {
    function.def @compute(%a: !felt.felt, %b: !felt.felt) -> !llzk.struct<@ConstConstraints> {
      %self = llzk.new_struct : !llzk.struct<@ConstConstraints>
      function.return %self : !llzk.struct<@ConstConstraints>
    }

    function.def @constrain(%self: !llzk.struct<@ConstConstraints>, %a: !felt.felt, %b: !felt.felt) {
      %c = felt.const 1
      llzk.emit_eq %a, %c : !felt.felt
      llzk.emit_eq %b, %c : !felt.felt
      function.return
    }
  }
}

// CHECK-LABEL: @ConstConstraints ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg1, <felt.const: 1> },
// CHECK-NEXT:     { %arg2, <felt.const: 1> }
// CHECK-NEXT: }

// -----

module attributes {veridise.lang = "llzk"} {
  // Constrains arr[0] === @A and arr[2] === @B, leaving arr[1] unconstrained
  llzk.struct @ArrayConstrain<[@A, @B]> {
    function.def @compute(%arr: !array.array<3 x !felt.felt>) -> !llzk.struct<@ArrayConstrain<[@A, @B]>> {
      %self = llzk.new_struct : !llzk.struct<@ArrayConstrain<[@A, @B]>>
      function.return %self : !llzk.struct<@ArrayConstrain<[@A, @B]>>
    }

    function.def @constrain(%self: !llzk.struct<@ArrayConstrain<[@A, @B]>>, %arr: !array.array<3 x !felt.felt>) {
      %const0 = llzk.read_const @A : !felt.felt
      %const2 = llzk.read_const @B : !felt.felt
      %0 = arith.constant 0 : index
      %2 = arith.constant 2 : index
      %elem0 = array.read %arr[%0] : !array.array<3 x !felt.felt>, !felt.felt
      %elem2 = array.read %arr[%2] : !array.array<3 x !felt.felt>, !felt.felt
      llzk.emit_eq %elem0, %const0 : !felt.felt
      llzk.emit_eq %elem2, %const2 : !felt.felt
      function.return
    }
  }

  llzk.struct @MatrixConstrain {
    llzk.field @check0 : !llzk.struct<@ArrayConstrain<[7, 11]>>
    llzk.field @check1 : !llzk.struct<@ArrayConstrain<[13, 17]>>

    function.def @compute(%mat: !array.array<2,3 x !felt.felt>) -> !llzk.struct<@MatrixConstrain> {
      %self = llzk.new_struct : !llzk.struct<@MatrixConstrain>
      function.return %self : !llzk.struct<@MatrixConstrain>
    }

    function.def @constrain(%self: !llzk.struct<@MatrixConstrain>, %mat: !array.array<2,3 x !felt.felt>) {
      // split into two arrays and constrain each
      %check0 = llzk.readf %self[@check0] : !llzk.struct<@MatrixConstrain>, !llzk.struct<@ArrayConstrain<[7, 11]>>
      %0 = arith.constant 0 : index
      %arr0 = array.extract %mat[%0] : !array.array<2,3 x !felt.felt>
      function.call @ArrayConstrain::@constrain(%check0, %arr0) : (!llzk.struct<@ArrayConstrain<[7, 11]>>, !array.array<3 x !felt.felt>) -> ()

      %check1 = llzk.readf %self[@check1] : !llzk.struct<@MatrixConstrain>, !llzk.struct<@ArrayConstrain<[13, 17]>>
      %1 = arith.constant 1 : index
      %arr1 = array.extract %mat[%1] : !array.array<2,3 x !felt.felt>
      function.call @ArrayConstrain::@constrain(%check1, %arr1) : (!llzk.struct<@ArrayConstrain<[13, 17]>>, !array.array<3 x !felt.felt>) -> ()
      function.return
    }
  }
}

// COM: Propagation of template constants is not currently implemented and
// COM: should likely be relegated to a specialized transformation pass.
// CHECK-LABEL: @ArrayConstrain ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg1[0], @ArrayConstrain<[@A]> },
// CHECK-NEXT:     { %arg1[2], @ArrayConstrain<[@B]> }
// CHECK-NEXT: }
// CHECK-NEXT: @MatrixConstrain ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg1[0][0], @ArrayConstrain<[@A]> },
// CHECK-NEXT:     { %arg1[0][2], @ArrayConstrain<[@B]> },
// CHECK-NEXT:     { %arg1[1][0], @ArrayConstrain<[@A]> },
// CHECK-NEXT:     { %arg1[1][2], @ArrayConstrain<[@B]> }
// CHECK-NEXT: }

// -----

module attributes {veridise.lang = "llzk"} {
  // Constrains arr[1] === 7, leaving other indices unconstrained
  llzk.struct @ArrayConstrain {
    function.def @compute(%arr: !array.array<3 x !felt.felt>) -> !llzk.struct<@ArrayConstrain> {
      %self = llzk.new_struct : !llzk.struct<@ArrayConstrain>
      function.return %self : !llzk.struct<@ArrayConstrain>
    }

    function.def @constrain(%self: !llzk.struct<@ArrayConstrain>, %arr: !array.array<3 x !felt.felt>) {
      %const = felt.const 7
      %1 = arith.constant 1 : index
      %elem = array.read %arr[%1] : !array.array<3 x !felt.felt>, !felt.felt
      llzk.emit_eq %elem, %const : !felt.felt
      function.return
    }
  }

  llzk.struct @MatrixConstrain {
    llzk.field @check : !llzk.struct<@ArrayConstrain>

    llzk.field @a : !felt.felt
    llzk.field @b : !felt.felt
    llzk.field @c : !felt.felt
    llzk.field @d : !felt.felt
    llzk.field @e : !felt.felt
    llzk.field @f : !felt.felt

    function.def @compute() -> !llzk.struct<@MatrixConstrain> {
      %self = llzk.new_struct : !llzk.struct<@MatrixConstrain>
      function.return %self : !llzk.struct<@MatrixConstrain>
    }

    function.def @constrain(%self: !llzk.struct<@MatrixConstrain>) {
      %a = llzk.readf %self[@a] : !llzk.struct<@MatrixConstrain>, !felt.felt
      %b = llzk.readf %self[@b] : !llzk.struct<@MatrixConstrain>, !felt.felt
      %c = llzk.readf %self[@c] : !llzk.struct<@MatrixConstrain>, !felt.felt
      %d = llzk.readf %self[@d] : !llzk.struct<@MatrixConstrain>, !felt.felt
      %e = llzk.readf %self[@e] : !llzk.struct<@MatrixConstrain>, !felt.felt
      %f = llzk.readf %self[@f] : !llzk.struct<@MatrixConstrain>, !felt.felt

      %mat = array.new %a, %b, %c, %d, %e, %f : !array.array<2,3 x !felt.felt>

      %check = llzk.readf %self[@check] : !llzk.struct<@MatrixConstrain>, !llzk.struct<@ArrayConstrain>
      %0 = arith.constant 0 : index
      %arr = array.extract %mat[%0] : !array.array<2,3 x !felt.felt>
      function.call @ArrayConstrain::@constrain(%check, %arr) : (!llzk.struct<@ArrayConstrain>, !array.array<3 x !felt.felt>) -> ()

      function.return
    }
  }
}

// CHECK-LABEL: @ArrayConstrain ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg1[1], <felt.const: 7> }
// CHECK-NEXT: }
// CHECK-NEXT: @MatrixConstrain ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@b], <felt.const: 7> }
// CHECK-NEXT: }

// -----

module attributes {veridise.lang = "llzk"} {
  llzk.struct @UnknownArrayConstrain<[@N]> {
    function.def @compute(%arr: !array.array<@N x !felt.felt>) -> !llzk.struct<@UnknownArrayConstrain<[@N]>> {
      %self = llzk.new_struct : !llzk.struct<@UnknownArrayConstrain<[@N]>>
      function.return %self : !llzk.struct<@UnknownArrayConstrain<[@N]>>
    }

    function.def @constrain(%self: !llzk.struct<@UnknownArrayConstrain<[@N]>>, %arr: !array.array<@N x !felt.felt>) {
      %const = felt.const 7
      %1 = arith.constant 1 : index
      %elem = array.read %arr[%1] : !array.array<@N x !felt.felt>, !felt.felt
      llzk.emit_eq %elem, %const : !felt.felt
      function.return
    }
  }
}

// CHECK-LABEL: @UnknownArrayConstrain ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg1[1], <felt.const: 7> }
// CHECK-NEXT: }

// -----

module attributes {veridise.lang = "llzk"} {
  llzk.struct @UnknownArrayConstrain<[@N]> {
    function.def @compute(%arr: !array.array<@N x !felt.felt>) -> !llzk.struct<@UnknownArrayConstrain<[@N]>> {
      %self = llzk.new_struct : !llzk.struct<@UnknownArrayConstrain<[@N]>>
      function.return %self : !llzk.struct<@UnknownArrayConstrain<[@N]>>
    }

    function.def @constrain(%self: !llzk.struct<@UnknownArrayConstrain<[@N]>>, %arr: !array.array<@N x !felt.felt>) {
      %const = felt.const 7
      %1 = arith.constant 1 : index
      %elem = array.read %arr[%1] : !array.array<@N x !felt.felt>, !felt.felt
      llzk.emit_eq %elem, %const : !felt.felt
      function.return
    }
  }

  llzk.struct @UnknownMatrixConstrain<[@M, @N]> {
    llzk.field @check : !llzk.struct<@UnknownArrayConstrain<[@N]>>

    function.def @compute(%mat: !array.array<@M,@N x !felt.felt>) -> !llzk.struct<@UnknownMatrixConstrain<[@M, @N]>> {
      %self = llzk.new_struct : !llzk.struct<@UnknownMatrixConstrain<[@M, @N]>>
      function.return %self : !llzk.struct<@UnknownMatrixConstrain<[@M, @N]>>
    }

    function.def @constrain(%self: !llzk.struct<@UnknownMatrixConstrain<[@M, @N]>>, %mat: !array.array<@M,@N x !felt.felt>) {
      %check = llzk.readf %self[@check] : !llzk.struct<@UnknownMatrixConstrain<[@M, @N]>>, !llzk.struct<@UnknownArrayConstrain<[@N]>>
      %0 = arith.constant 0 : index
      %arr = array.extract %mat[%0] : !array.array<@M,@N x !felt.felt>
      function.call @UnknownArrayConstrain::@constrain(%check, %arr) : (!llzk.struct<@UnknownArrayConstrain<[@N]>>, !array.array<@N x !felt.felt>) -> ()
      function.return
    }
  }
}

// CHECK-LABEL: @UnknownArrayConstrain ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg1[1], <felt.const: 7> }
// CHECK-NEXT: }
// CHECK-NEXT: @UnknownMatrixConstrain ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg1[0][1], <felt.const: 7> }
// CHECK-NEXT: }

// -----

module attributes {veridise.lang = "llzk"} {
  include.from "Dialect/LLZK/zir_example_0.llzk" as @zir
}

// CHECK-LABEL: @zir::@risc0::@ValU32 ConstraintDependencyGraph { }
// CHECK-NEXT: @zir::@risc0::@Reg ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@reg], %arg1 }
// CHECK-NEXT: }
// CHECK-NEXT: @zir::@risc0::@Div ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@reciprocal], %arg0[@synthetic_return], %arg1, %arg2 },
// CHECK-NEXT:     { %arg0[@reciprocal], <felt.const: 1> },
// CHECK-NEXT:     { %arg2, <felt.const: 1> }
// CHECK-NEXT: }

// -----

module attributes {veridise.lang = "llzk"} {
  include.from "Dialect/LLZK/zir_example_1.llzk" as @zir
}

// CHECK-LABEL: @zir::@std::@risc0::@ValU32 ConstraintDependencyGraph { }
// CHECK-NEXT: @zir::@std::@risc0::@Reg ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@reg], %arg1 }
// CHECK-NEXT: }
// CHECK-NEXT: @zir::@std::@risc0::@Div ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@reciprocal], %arg0[@synthetic_return], %arg1, %arg2 },
// CHECK-NEXT:     { %arg0[@reciprocal], <felt.const: 1> },
// CHECK-NEXT:     { %arg2, <felt.const: 1> }
// CHECK-NEXT: }
// CHECK-NEXT: @zir::@C1 ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@z][@reg], %arg1, %arg2 }
// CHECK-NEXT: }
// CHECK-NEXT: @zir::@C2 ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@fc1][@z][@reg], %arg1[@low], %arg1[@high] },
// CHECK-NEXT:     { %arg0[@fc1][@z][@reg], <felt.const: 0> }
// CHECK-NEXT: }

// -----

module attributes {veridise.lang = "llzk"} {
  include.from "Dialect/LLZK/zir_example_2.llzk" as @zir
}

// CHECK-LABEL: @zir::@std::@risc0::@ValU32 ConstraintDependencyGraph { }
// CHECK-NEXT: @zir::@std::@risc0::@Reg ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@reg], %arg1 }
// CHECK-NEXT: }
// CHECK-NEXT: @zir::@std::@risc0::@Div ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@reciprocal], %arg0[@synthetic_return], %arg1, %arg2 },
// CHECK-NEXT:     { %arg0[@reciprocal], <felt.const: 1> },
// CHECK-NEXT:     { %arg2, <felt.const: 1> }
// CHECK-NEXT: }
// CHECK-NEXT: @zir::@AssertBit ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg1, <felt.const: 0>, <felt.const: 1> }
// CHECK-NEXT: }
// CHECK-NEXT: @zir::@NondetBitReg ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@reg], %arg0[@synthetic_return] },
// CHECK-NEXT:     { %arg0[@reg], <felt.const: 0>, <felt.const: 1> }
// CHECK-NEXT: }
// CHECK-NEXT: @zir::@ArgU16 ConstraintDependencyGraph { }
// CHECK-NEXT: @zir::@NondetU16Reg ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@arg][@count], <felt.const: 1> },
// CHECK-NEXT:     { %arg0[@arg][@val], %arg0[@synthetic_return] }
// CHECK-NEXT: }
// CHECK-NEXT: @zir::@U16Reg ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@ret][@arg][@count], <felt.const: 1> },
// CHECK-NEXT:     { %arg0[@ret][@arg][@val], %arg0[@ret][@synthetic_return], %arg0[@synthetic_return], %arg1 }
// CHECK-NEXT: }
// CHECK-NEXT: @zir::@AddrDecomposeBits ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@low0][@reg], %arg0[@low0][@synthetic_return], %arg0[@low1][@reg], %arg0[@low1][@synthetic_return], %arg0[@low2], %arg0[@upperDiff][@ret][@arg][@val], %arg0[@upperDiff][@ret][@synthetic_return], %arg0[@upperDiff][@synthetic_return], %arg0[@med14][@arg][@val], %arg0[@med14][@synthetic_return], %arg0[@addr], %arg0[@synthetic_return], %arg1[@low], %arg1[@high], %arg2 },
// CHECK-NEXT:     { %arg0[@low0][@reg], <felt.const: 0>, <felt.const: 1> },
// CHECK-NEXT:     { %arg0[@low0][@synthetic_return], <felt.const: 0>, <felt.const: 1>, <felt.const: 2> },
// CHECK-NEXT:     { %arg0[@low1][@reg], <felt.const: 0>, <felt.const: 1> },
// CHECK-NEXT:     { %arg0[@low1][@synthetic_return], <felt.const: 0>, <felt.const: 1>, <felt.const: 2> },
// CHECK-NEXT:     { %arg0[@low2], <felt.const: 2>, <felt.const: 4> },
// CHECK-NEXT:     { %arg0[@upperDiff][@ret][@arg][@count], <felt.const: 1> },
// CHECK-NEXT:     { %arg0[@upperDiff][@ret][@arg][@val], <felt.const: 1>, <felt.const: 49151>, <felt.const: 65535> },
// CHECK-NEXT:     { %arg0[@upperDiff][@ret][@synthetic_return], <felt.const: 1>, <felt.const: 49151>, <felt.const: 65535> },
// CHECK-NEXT:     { %arg0[@upperDiff][@synthetic_return], <felt.const: 1>, <felt.const: 49151>, <felt.const: 65535> },
// CHECK-NEXT:     { %arg0[@med14][@arg][@count], <felt.const: 1> },
// CHECK-NEXT:     { %arg0[@med14][@synthetic_return], <felt.const: 4>, <felt.const: 16384> },
// CHECK-NEXT:     { %arg0[@addr], <felt.const: 16384> },
// CHECK-NEXT:     { %arg1[@low], <felt.const: 4> },
// CHECK-NEXT:     { %arg1[@high], <felt.const: 1>, <felt.const: 16384>, <felt.const: 49151>, <felt.const: 65535> },
// CHECK-NEXT:     { %arg2, <felt.const: 1>, <felt.const: 49151>, <felt.const: 65535> }
// CHECK-NEXT: }

// -----

module attributes {veridise.lang = "llzk"} {
  include.from "Dialect/LLZK/zir_example_3.llzk" as @zir
}

// COM: @zir::@B is empty since it uses an uninitialized array
// CHECK-LABEL: @zir::@Head ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@$super], %arg1[0] }
// CHECK-NEXT: }
// CHECK-NEXT: @zir::@A ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@$super][@$super], @A<[@N]> }
// CHECK-NEXT: }
// CHECK-NEXT: @zir::@B ConstraintDependencyGraph { }
// CHECK-NEXT: @zir::@Top ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@a][@$super][@$super], %arg0[@b][@$super][@$super][@$super][@$super] },
// CHECK-NEXT:     { %arg0[@a][@$super][@$super], @A<[@N]> }
// CHECK-NEXT: }
// CHECK-NEXT: @zir::@VoidComponent ConstraintDependencyGraph { }

// -----

// This check ensures that calling the same function from multiple different
// contexts will converge correctly.
module attributes {veridise.lang = "llzk"} {
  include.from "Inputs/bits.llzk" as @bits
}

// CHECK-LABEL: @bits::@Component ConstraintDependencyGraph { }
// CHECK-NEXT: @bits::@NondetReg ConstraintDependencyGraph { }
// CHECK-NEXT: @bits::@NondetExtReg ConstraintDependencyGraph { }
// CHECK-NEXT: @bits::@EqzExt ConstraintDependencyGraph { }
// CHECK-NEXT: @bits::@Reg ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@reg][@$super], %arg1 }
// CHECK-NEXT: }
// COM: ExtReg is unconstrained due to a front-end bug
// CHECK-NEXT: @bits::@ExtReg ConstraintDependencyGraph { }
// CHECK-NEXT: @bits::@Div ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@reciprocal], %arg2 },
// CHECK-NEXT:     { %arg0[@reciprocal], <felt.const: 1> },
// CHECK-NEXT:     { %arg2, <felt.const: 1> }
// CHECK-NEXT: }
// CHECK-NEXT: @bits::@Log ConstraintDependencyGraph { }
// CHECK-NEXT: @bits::@Abort ConstraintDependencyGraph { }
// CHECK-NEXT: @bits::@Assert ConstraintDependencyGraph { }
// CHECK-NEXT: @bits::@AssertBit ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg1, <felt.const: 0>, <felt.const: 1> }
// CHECK-NEXT: }
// CHECK-NEXT: @bits::@NondetBitReg ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@reg][@$super], <felt.const: 0>, <felt.const: 1> }
// CHECK-NEXT: }
// CHECK-NEXT: @bits::@BitReg ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@reg][@$super][@$super], %arg1 },
// CHECK-NEXT:     { %arg0[@reg][@reg][@$super], <felt.const: 0>, <felt.const: 1> }
// CHECK-NEXT: }
// CHECK-NEXT: @bits::@AssertTwit ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg1, <felt.const: 0>, <felt.const: 1>, <felt.const: 2>, <felt.const: 3> }
// CHECK-NEXT: }
// CHECK-NEXT: @bits::@BitAnd ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@$temp][@reg][@$super], %arg1, %arg2 }
// CHECK-NEXT: }
// CHECK-NEXT: @bits::@BitOr ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@$temp][@reg][@$super], %arg1, %arg2 },
// CHECK-NEXT:     { %arg0[@$temp][@reg][@$super], <felt.const: 1> },
// CHECK-NEXT:     { %arg1, <felt.const: 1> },
// CHECK-NEXT:     { %arg2, <felt.const: 1> }
// CHECK-NEXT: }
// CHECK-NEXT: @bits::@NondetTwitReg ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@reg][@$super], <felt.const: 0>, <felt.const: 1>, <felt.const: 2>, <felt.const: 3> }
// CHECK-NEXT: }
// CHECK-NEXT: @bits::@NondetFakeTwitReg ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@reg1][@reg][@$super], <felt.const: 0>, <felt.const: 1> },
// CHECK-NEXT:     { %arg0[@$temp_1][@reciprocal], <felt.const: 1>, <felt.const: 2> },
// CHECK-NEXT:     { %arg0[@$temp_0][@$temp][@reg][@$super], %arg0[@$temp][@$temp][@reg][@$super], %arg1 },
// CHECK-NEXT:     { %arg0[@$temp_0][@$temp][@reg][@$super], <felt.const: 2> },
// CHECK-NEXT:     { %arg0[@reg0][@reg][@$super], <felt.const: 0>, <felt.const: 1> },
// CHECK-NEXT:     { %arg0[@$temp][@$temp][@reg][@$super], <felt.const: 1> },
// CHECK-NEXT:     { %arg1, <felt.const: 1>, <felt.const: 2> }
// CHECK-NEXT: }
// CHECK-NEXT: @bits::@TwitReg ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@reg][@$super][@$super], %arg1 },
// CHECK-NEXT:     { %arg0[@reg][@reg][@$super], <felt.const: 0>, <felt.const: 1>, <felt.const: 2>, <felt.const: 3> }
// CHECK-NEXT: }
// CHECK-NEXT: @bits::@FakeTwitReg ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@reg][@$super], %arg0[@reg][@$temp_0][@$temp][@reg][@$super], %arg0[@reg][@$temp][@$temp][@reg][@$super], %arg1 },
// CHECK-NEXT:     { %arg0[@reg][@reg1][@reg][@$super], <felt.const: 0>, <felt.const: 1> },
// CHECK-NEXT:     { %arg0[@reg][@$temp_1][@reciprocal], <felt.const: 1>, <felt.const: 2> },
// CHECK-NEXT:     { %arg0[@reg][@$temp_0][@$temp][@reg][@$super], <felt.const: 1>, <felt.const: 2> },
// CHECK-NEXT:     { %arg0[@reg][@reg0][@reg][@$super], <felt.const: 0>, <felt.const: 1> },
// CHECK-NEXT:     { %arg0[@reg][@$temp][@$temp][@reg][@$super], <felt.const: 1>, <felt.const: 2> },
// CHECK-NEXT:     { %arg1, <felt.const: 1>, <felt.const: 2> }
// CHECK-NEXT: }

// -----

// Testing external calls

module attributes {veridise.lang = "llzk"} {

  function.def private @extern_add(%a: !felt.felt, %b: !felt.felt) -> !felt.felt attributes {extern}

  llzk.struct @ExternAdder {
    llzk.field @sum : !felt.felt {llzk.pub}

    function.def @compute(%a: !felt.felt, %b: !felt.felt) -> !llzk.struct<@ExternAdder> {
      %self = llzk.new_struct : !llzk.struct<@ExternAdder>
      function.return %self : !llzk.struct<@ExternAdder>
    }

    function.def @constrain(%self: !llzk.struct<@ExternAdder>, %a: !felt.felt, %b: !felt.felt) {
      %sum = llzk.readf %self[@sum] : !llzk.struct<@ExternAdder>, !felt.felt
      %c = function.call @extern_add(%a, %b) : (!felt.felt, !felt.felt) -> (!felt.felt)
      // We make no assumptions about what %c could be, so this constraint is
      // effectively `%sum === {}`, so the CDG will be empty.
      llzk.emit_eq %sum, %c : !felt.felt
      function.return
    }
  }
}

// CHECK-LABEL: @ExternAdder ConstraintDependencyGraph { }

// -----

module attributes {veridise.lang = "llzk"} {

  function.def @global_add(%a: !felt.felt, %b: !felt.felt) -> !felt.felt {
    %c = felt.add %a, %b
    function.return %c : !felt.felt
  }

  function.def private @irrelevant() attributes {extern}

  llzk.struct @Adder2 {
    llzk.field @sum : !felt.felt {llzk.pub}

    function.def @compute(%a: !felt.felt, %b: !felt.felt) -> !llzk.struct<@Adder2> {
      %self = llzk.new_struct : !llzk.struct<@Adder2>
      %sum = function.call @global_add(%a, %b) : (!felt.felt, !felt.felt) -> (!felt.felt)
      llzk.writef %self[@sum] = %sum : !llzk.struct<@Adder2>, !felt.felt
      function.return %self : !llzk.struct<@Adder2>
    }

    function.def @constrain(%self: !llzk.struct<@Adder2>, %a: !felt.felt, %b: !felt.felt) {
      %sum = llzk.readf %self[@sum] : !llzk.struct<@Adder2>, !felt.felt
      %c = function.call @global_add(%a, %b) : (!felt.felt, !felt.felt) -> (!felt.felt)
      %d = felt.add %c, %c
      function.call @irrelevant() : () -> () // testing to see that this doesn't erase the prior values
      llzk.emit_eq %sum, %d : !felt.felt
      function.return
    }
  }
}

// CHECK-LABEL: @Adder2 ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@sum], %arg1, %arg2 }
// CHECK-NEXT: }

// -----

// Test that allowing Signals to emit_eq doesn't break the constraint analysis
!Signal = !llzk.struct<@Signal>
module attributes {veridise.lang = "llzk"} {
  // pre-defined
  llzk.struct @Signal {
    llzk.field @reg : !felt.felt {llzk.pub}

    function.def @compute(%v: !felt.felt) -> !llzk.struct<@Signal> {
      %self = llzk.new_struct : !llzk.struct<@Signal>
      llzk.writef %self[@reg] = %v : !llzk.struct<@Signal>, !felt.felt
      function.return %self : !llzk.struct<@Signal>
    }

    function.def @constrain(%self: !llzk.struct<@Signal>, %v: !felt.felt) {
      function.return
    }
  }

  llzk.struct @Component00 {
    llzk.field @f : !Signal

    function.def @compute(%0: !Signal) -> !llzk.struct<@Component00> {
      %self = llzk.new_struct : !llzk.struct<@Component00>
      llzk.writef %self[@f] = %0 : !llzk.struct<@Component00>, !Signal
      function.return %self : !llzk.struct<@Component00>
    }
    function.def @constrain(%self: !llzk.struct<@Component00>, %0: !Signal) {
      %b = llzk.readf %self[@f] : !llzk.struct<@Component00>, !Signal
      // Signal is the only struct allowed for emit equal
      llzk.emit_eq %b, %0 : !Signal
      function.return
    }
  }

  llzk.struct @Component01 {
    llzk.field @f : !array.array<2 x !Signal>

    function.def @compute(%0: !array.array<2 x !Signal>) -> !llzk.struct<@Component01> {
      %self = llzk.new_struct : !llzk.struct<@Component01>
      llzk.writef %self[@f] = %0 : !llzk.struct<@Component01>, !array.array<2 x !Signal>
      function.return %self : !llzk.struct<@Component01>
    }
    function.def @constrain(%self: !llzk.struct<@Component01>, %0: !array.array<2 x !Signal>) {
      %b = llzk.readf %self[@f] : !llzk.struct<@Component01>, !array.array<2 x !Signal>
      // Signal is the only struct allowed for emit equal
      llzk.emit_eq %b, %0 : !array.array<2 x !Signal>
      function.return
    }
  }
}

// CHECK-LABEL: @Signal ConstraintDependencyGraph { }
// CHECK-NEXT:  @Component00 ConstraintDependencyGraph {
// CHECK-NEXT:      { %arg0[@f], %arg1 }
// CHECK-NEXT:  }
// CHECK-NEXT:  @Component01 ConstraintDependencyGraph {
// CHECK-NEXT:      { %arg0[@f], %arg1 }
// CHECK-NEXT:  }
