// RUN: llzk-opt -I %input_dir -split-input-file -llzk-inline-includes -llzk-print-constraint-dependency-graphs %s 2>&1 | FileCheck --enable-var-scope %s

module attributes {veridise.lang = "llzk"} {
  struct.def @NoConstraints {
    function.def @compute(%a: !felt.felt) -> !struct.struct<@NoConstraints> {
      %self = struct.new : !struct.struct<@NoConstraints>
      function.return %self : !struct.struct<@NoConstraints>
    }

    function.def @constrain(%self: !struct.struct<@NoConstraints>, %a: !felt.felt) {
      function.return
    }
  }
}

// CHECK-LABEL: @NoConstraints ConstraintDependencyGraph { }

// -----

module attributes {veridise.lang = "llzk"} {

  function.def @global_add(%a: !felt.felt, %b: !felt.felt) -> !felt.felt {
    %c = felt.add %a, %b
    function.return %c : !felt.felt
  }

  struct.def @Adder {
    struct.field @sum : !felt.felt {llzk.pub}

    function.def @compute(%a: !felt.felt, %b: !felt.felt) -> !struct.struct<@Adder> {
      %self = struct.new : !struct.struct<@Adder>
      %sum = function.call @global_add(%a, %b) : (!felt.felt, !felt.felt) -> (!felt.felt)
      struct.writef %self[@sum] = %sum : !struct.struct<@Adder>, !felt.felt
      function.return %self : !struct.struct<@Adder>
    }

    function.def @constrain(%self: !struct.struct<@Adder>, %a: !felt.felt, %b: !felt.felt) {
      %sum = struct.readf %self[@sum] : !struct.struct<@Adder>, !felt.felt
      %c = function.call @global_add(%a, %b) : (!felt.felt, !felt.felt) -> (!felt.felt)
      constrain.eq %sum, %c : !felt.felt
      function.return
    }
  }
}

// CHECK-LABEL: @Adder ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@sum], %arg1, %arg2 }
// CHECK-NEXT: }

// -----

module attributes {veridise.lang = "llzk"} {

  function.def @global_add(%a: !felt.felt, %b: !felt.felt) -> !felt.felt {
    %c = felt.add %a, %b
    function.return %c : !felt.felt
  }

  struct.def @Adder2 {
    struct.field @sum : !felt.felt {llzk.pub}

    function.def @compute(%a: !felt.felt, %b: !felt.felt) -> !struct.struct<@Adder2> {
      %self = struct.new : !struct.struct<@Adder2>
      %sum = function.call @global_add(%a, %b) : (!felt.felt, !felt.felt) -> (!felt.felt)
      struct.writef %self[@sum] = %sum : !struct.struct<@Adder2>, !felt.felt
      function.return %self : !struct.struct<@Adder2>
    }

    function.def @constrain(%self: !struct.struct<@Adder2>, %a: !felt.felt, %b: !felt.felt) {
      %sum = struct.readf %self[@sum] : !struct.struct<@Adder2>, !felt.felt
      %c = function.call @global_add(%a, %b) : (!felt.felt, !felt.felt) -> (!felt.felt)
      %d = felt.add %c, %c
      constrain.eq %sum, %d : !felt.felt
      function.return
    }
  }
}

// CHECK-LABEL: @Adder2 ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@sum], %arg1, %arg2 }
// CHECK-NEXT: }

// -----

module attributes {veridise.lang = "llzk"} {
  struct.def @ComponentB {
    function.def @constrain(%self: !struct.struct<@ComponentB>, %a: !felt.felt, %b: !array.array<5 x !felt.felt>) {
      constrain.in %b, %a : !array.array<5 x !felt.felt>
      function.return
    }

    function.def @compute(%a: !felt.felt, %b: !array.array<5 x !felt.felt>) -> !struct.struct<@ComponentB> {
      %self = struct.new : !struct.struct<@ComponentB>
      function.return %self : !struct.struct<@ComponentB>
    }
  }
}

// CHECK-LABEL: @ComponentB ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg1, %arg2 }
// CHECK-NEXT: }

// -----

module attributes {veridise.lang = "llzk"} {

  struct.def @EnsureZero {
    function.def @compute(%a: !felt.felt) -> !struct.struct<@EnsureZero> {
      %self = struct.new : !struct.struct<@EnsureZero>
      function.return %self : !struct.struct<@EnsureZero>
    }

    function.def @constrain(%self: !struct.struct<@EnsureZero>, %a: !felt.felt) {
      %zero = felt.const 0
      constrain.eq %a, %zero : !felt.felt
      function.return
    }
  }

  struct.def @EnsureBothZero {
    function.def @compute(%a: !felt.felt, %b: !felt.felt) -> !struct.struct<@EnsureBothZero> {
      %self = struct.new : !struct.struct<@EnsureBothZero>
      function.return %self : !struct.struct<@EnsureBothZero>
    }

    // NOTE: This test case demonstrates where this analysis is currently overly conservative,
    // as it does not determine that %a and %b must be precisely equal to 0.
    function.def @constrain(%self: !struct.struct<@EnsureBothZero>, %a: !felt.felt, %b: !felt.felt) {
      %zero = felt.const 0
      constrain.eq %a, %b : !felt.felt
      constrain.eq %a, %zero : !felt.felt
      function.return
    }
  }
}

// CHECK-LABEL: @EnsureZero ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg1, <felt.const: 0> }
// CHECK-NEXT: }
// CHECK-NEXT: @EnsureBothZero ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg1, %arg2 },
// CHECK-NEXT:     { %arg1, <felt.const: 0> }
// CHECK-NEXT: }

// -----

module attributes {veridise.lang = "llzk"} {

  struct.def @Passthrough {
    struct.field @out : !felt.felt {llzk.pub}

    function.def @compute(%in: !felt.felt) -> !struct.struct<@Passthrough> {
      %self = struct.new : !struct.struct<@Passthrough>
      struct.writef %self[@out] = %in : !struct.struct<@Passthrough>, !felt.felt
      function.return %self : !struct.struct<@Passthrough>
    }

    function.def @constrain(%self: !struct.struct<@Passthrough>, %in: !felt.felt) {
      %out = struct.readf %self[@out] : !struct.struct<@Passthrough>, !felt.felt
      constrain.eq %in, %out : !felt.felt
      function.return
    }
  }

  struct.def @EnsureIsZero {
    struct.field @p : !struct.struct<@Passthrough>

    function.def @compute(%in: !felt.felt) -> !struct.struct<@EnsureIsZero> {
      %self = struct.new : !struct.struct<@EnsureIsZero>
      %p_0 = function.call @Passthrough::@compute(%in) : (!felt.felt) -> (!struct.struct<@Passthrough>)
      struct.writef %self[@p] = %p_0 : !struct.struct<@EnsureIsZero>, !struct.struct<@Passthrough>
      function.return %self : !struct.struct<@EnsureIsZero>
    }

    function.def @constrain(%self: !struct.struct<@EnsureIsZero>, %in: !felt.felt) {
      %zero = felt.const 0
      %p_0 = struct.readf %self[@p] : !struct.struct<@EnsureIsZero>, !struct.struct<@Passthrough>
      function.call @Passthrough::@constrain(%p_0, %zero) : (!struct.struct<@Passthrough>, !felt.felt) -> ()
      %out = struct.readf %p_0[@out] : !struct.struct<@Passthrough>, !felt.felt
      constrain.eq %in, %out : !felt.felt
      function.return
    }
  }
}

// CHECK-LABEL: @Passthrough ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@out], %arg1 }
// CHECK-NEXT: }
// CHECK-NEXT: @EnsureIsZero ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@p][@out], %arg1 },
// CHECK-NEXT:     { %arg0[@p][@out], <felt.const: 0> }
// CHECK-NEXT: }

// -----

module attributes {veridise.lang = "llzk"} {
  struct.def @ArrayCheck {
    function.def @constrain(%self: !struct.struct<@ArrayCheck>, %in: !array.array<5 x !felt.felt>) {
      %fval = felt.const 7
      %idx = arith.constant 3 : index
      %elem = array.read %in[%idx] : !array.array<5 x !felt.felt>, !felt.felt
      constrain.eq %elem, %fval : !felt.felt
      function.return
    }

    function.def @compute(%in: !array.array<5 x !felt.felt>) -> !struct.struct<@ArrayCheck> {
      %self = struct.new : !struct.struct<@ArrayCheck>
      function.return %self : !struct.struct<@ArrayCheck>
    }
  }
}

// CHECK-LABEL: @ArrayCheck ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg1[3], <felt.const: 7> }
// CHECK-NEXT: }

// -----

module attributes {veridise.lang = "llzk"} {
  struct.def @ArrayForCheck {
    function.def @constrain(%self: !struct.struct<@ArrayForCheck>, %in: !array.array<5 x !felt.felt>) {
      %lb = arith.constant 0 : index
      %up = arith.constant 5 : index
      %step = arith.constant 1 : index
      %fval = felt.const 7
      scf.for %iv = %lb to %up step %step {
        %elem = array.read %in[%iv] : !array.array<5 x !felt.felt>, !felt.felt
        constrain.eq %elem, %fval : !felt.felt
      }
      function.return
    }

    function.def @compute(%in: !array.array<5 x !felt.felt>) -> !struct.struct<@ArrayForCheck> {
      %self = struct.new : !struct.struct<@ArrayForCheck>
      function.return %self : !struct.struct<@ArrayForCheck>
    }
  }
}

// CHECK-LABEL: @ArrayForCheck ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg1[0:5], <felt.const: 7> }
// CHECK-NEXT: }

// -----

module attributes {veridise.lang = "llzk"} {

  struct.def @ConstConstraints {
    function.def @compute(%a: !felt.felt, %b: !felt.felt) -> !struct.struct<@ConstConstraints> {
      %self = struct.new : !struct.struct<@ConstConstraints>
      function.return %self : !struct.struct<@ConstConstraints>
    }

    function.def @constrain(%self: !struct.struct<@ConstConstraints>, %a: !felt.felt, %b: !felt.felt) {
      %c = felt.const 1
      constrain.eq %a, %c : !felt.felt
      constrain.eq %b, %c : !felt.felt
      function.return
    }
  }
}

// CHECK-LABEL: @ConstConstraints ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg1, <felt.const: 1> },
// CHECK-NEXT:     { %arg2, <felt.const: 1> }
// CHECK-NEXT: }

// -----

module attributes {veridise.lang = "llzk"} {
  // Constrains arr[0] === @A and arr[2] === @B, leaving arr[1] unconstrained
  struct.def @ArrayConstrain<[@A, @B]> {
    function.def @compute(%arr: !array.array<3 x !felt.felt>) -> !struct.struct<@ArrayConstrain<[@A, @B]>> {
      %self = struct.new : !struct.struct<@ArrayConstrain<[@A, @B]>>
      function.return %self : !struct.struct<@ArrayConstrain<[@A, @B]>>
    }

    function.def @constrain(%self: !struct.struct<@ArrayConstrain<[@A, @B]>>, %arr: !array.array<3 x !felt.felt>) {
      %const0 = poly.read_const @A : !felt.felt
      %const2 = poly.read_const @B : !felt.felt
      %0 = arith.constant 0 : index
      %2 = arith.constant 2 : index
      %elem0 = array.read %arr[%0] : !array.array<3 x !felt.felt>, !felt.felt
      %elem2 = array.read %arr[%2] : !array.array<3 x !felt.felt>, !felt.felt
      constrain.eq %elem0, %const0 : !felt.felt
      constrain.eq %elem2, %const2 : !felt.felt
      function.return
    }
  }

  struct.def @MatrixConstrain {
    struct.field @check0 : !struct.struct<@ArrayConstrain<[7, 11]>>
    struct.field @check1 : !struct.struct<@ArrayConstrain<[13, 17]>>

    function.def @compute(%mat: !array.array<2,3 x !felt.felt>) -> !struct.struct<@MatrixConstrain> {
      %self = struct.new : !struct.struct<@MatrixConstrain>
      function.return %self : !struct.struct<@MatrixConstrain>
    }

    function.def @constrain(%self: !struct.struct<@MatrixConstrain>, %mat: !array.array<2,3 x !felt.felt>) {
      // split into two arrays and constrain each
      %check0 = struct.readf %self[@check0] : !struct.struct<@MatrixConstrain>, !struct.struct<@ArrayConstrain<[7, 11]>>
      %0 = arith.constant 0 : index
      %arr0 = array.extract %mat[%0] : !array.array<2,3 x !felt.felt>
      function.call @ArrayConstrain::@constrain(%check0, %arr0) : (!struct.struct<@ArrayConstrain<[7, 11]>>, !array.array<3 x !felt.felt>) -> ()

      %check1 = struct.readf %self[@check1] : !struct.struct<@MatrixConstrain>, !struct.struct<@ArrayConstrain<[13, 17]>>
      %1 = arith.constant 1 : index
      %arr1 = array.extract %mat[%1] : !array.array<2,3 x !felt.felt>
      function.call @ArrayConstrain::@constrain(%check1, %arr1) : (!struct.struct<@ArrayConstrain<[13, 17]>>, !array.array<3 x !felt.felt>) -> ()
      function.return
    }
  }
}

// COM: Propagation of template constants is not currently implemented and
// COM: should likely be relegated to a specialized transformation pass.
// CHECK-LABEL: @ArrayConstrain ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg1[0], @ArrayConstrain<[@A]> },
// CHECK-NEXT:     { %arg1[2], @ArrayConstrain<[@B]> }
// CHECK-NEXT: }
// CHECK-NEXT: @MatrixConstrain ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg1[0][0], @ArrayConstrain<[@A]> },
// CHECK-NEXT:     { %arg1[0][2], @ArrayConstrain<[@B]> },
// CHECK-NEXT:     { %arg1[1][0], @ArrayConstrain<[@A]> },
// CHECK-NEXT:     { %arg1[1][2], @ArrayConstrain<[@B]> }
// CHECK-NEXT: }

// -----

module attributes {veridise.lang = "llzk"} {
  // Constrains arr[1] === 7, leaving other indices unconstrained
  struct.def @ArrayConstrain {
    function.def @compute(%arr: !array.array<3 x !felt.felt>) -> !struct.struct<@ArrayConstrain> {
      %self = struct.new : !struct.struct<@ArrayConstrain>
      function.return %self : !struct.struct<@ArrayConstrain>
    }

    function.def @constrain(%self: !struct.struct<@ArrayConstrain>, %arr: !array.array<3 x !felt.felt>) {
      %const = felt.const 7
      %1 = arith.constant 1 : index
      %elem = array.read %arr[%1] : !array.array<3 x !felt.felt>, !felt.felt
      constrain.eq %elem, %const : !felt.felt
      function.return
    }
  }

  struct.def @MatrixConstrain {
    struct.field @check : !struct.struct<@ArrayConstrain>

    struct.field @a : !felt.felt
    struct.field @b : !felt.felt
    struct.field @c : !felt.felt
    struct.field @d : !felt.felt
    struct.field @e : !felt.felt
    struct.field @f : !felt.felt

    function.def @compute() -> !struct.struct<@MatrixConstrain> {
      %self = struct.new : !struct.struct<@MatrixConstrain>
      function.return %self : !struct.struct<@MatrixConstrain>
    }

    function.def @constrain(%self: !struct.struct<@MatrixConstrain>) {
      %a = struct.readf %self[@a] : !struct.struct<@MatrixConstrain>, !felt.felt
      %b = struct.readf %self[@b] : !struct.struct<@MatrixConstrain>, !felt.felt
      %c = struct.readf %self[@c] : !struct.struct<@MatrixConstrain>, !felt.felt
      %d = struct.readf %self[@d] : !struct.struct<@MatrixConstrain>, !felt.felt
      %e = struct.readf %self[@e] : !struct.struct<@MatrixConstrain>, !felt.felt
      %f = struct.readf %self[@f] : !struct.struct<@MatrixConstrain>, !felt.felt

      %mat = array.new %a, %b, %c, %d, %e, %f : !array.array<2,3 x !felt.felt>

      %check = struct.readf %self[@check] : !struct.struct<@MatrixConstrain>, !struct.struct<@ArrayConstrain>
      %0 = arith.constant 0 : index
      %arr = array.extract %mat[%0] : !array.array<2,3 x !felt.felt>
      function.call @ArrayConstrain::@constrain(%check, %arr) : (!struct.struct<@ArrayConstrain>, !array.array<3 x !felt.felt>) -> ()

      function.return
    }
  }
}

// CHECK-LABEL: @ArrayConstrain ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg1[1], <felt.const: 7> }
// CHECK-NEXT: }
// CHECK-NEXT: @MatrixConstrain ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@b], <felt.const: 7> }
// CHECK-NEXT: }

// -----

module attributes {veridise.lang = "llzk"} {
  struct.def @UnknownArrayConstrain<[@N]> {
    function.def @compute(%arr: !array.array<@N x !felt.felt>) -> !struct.struct<@UnknownArrayConstrain<[@N]>> {
      %self = struct.new : !struct.struct<@UnknownArrayConstrain<[@N]>>
      function.return %self : !struct.struct<@UnknownArrayConstrain<[@N]>>
    }

    function.def @constrain(%self: !struct.struct<@UnknownArrayConstrain<[@N]>>, %arr: !array.array<@N x !felt.felt>) {
      %const = felt.const 7
      %1 = arith.constant 1 : index
      %elem = array.read %arr[%1] : !array.array<@N x !felt.felt>, !felt.felt
      constrain.eq %elem, %const : !felt.felt
      function.return
    }
  }
}

// CHECK-LABEL: @UnknownArrayConstrain ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg1[1], <felt.const: 7> }
// CHECK-NEXT: }

// -----

module attributes {veridise.lang = "llzk"} {
  struct.def @UnknownArrayConstrain<[@N]> {
    function.def @compute(%arr: !array.array<@N x !felt.felt>) -> !struct.struct<@UnknownArrayConstrain<[@N]>> {
      %self = struct.new : !struct.struct<@UnknownArrayConstrain<[@N]>>
      function.return %self : !struct.struct<@UnknownArrayConstrain<[@N]>>
    }

    function.def @constrain(%self: !struct.struct<@UnknownArrayConstrain<[@N]>>, %arr: !array.array<@N x !felt.felt>) {
      %const = felt.const 7
      %1 = arith.constant 1 : index
      %elem = array.read %arr[%1] : !array.array<@N x !felt.felt>, !felt.felt
      constrain.eq %elem, %const : !felt.felt
      function.return
    }
  }

  struct.def @UnknownMatrixConstrain<[@M, @N]> {
    struct.field @check : !struct.struct<@UnknownArrayConstrain<[@N]>>

    function.def @compute(%mat: !array.array<@M,@N x !felt.felt>) -> !struct.struct<@UnknownMatrixConstrain<[@M, @N]>> {
      %self = struct.new : !struct.struct<@UnknownMatrixConstrain<[@M, @N]>>
      function.return %self : !struct.struct<@UnknownMatrixConstrain<[@M, @N]>>
    }

    function.def @constrain(%self: !struct.struct<@UnknownMatrixConstrain<[@M, @N]>>, %mat: !array.array<@M,@N x !felt.felt>) {
      %check = struct.readf %self[@check] : !struct.struct<@UnknownMatrixConstrain<[@M, @N]>>, !struct.struct<@UnknownArrayConstrain<[@N]>>
      %0 = arith.constant 0 : index
      %arr = array.extract %mat[%0] : !array.array<@M,@N x !felt.felt>
      function.call @UnknownArrayConstrain::@constrain(%check, %arr) : (!struct.struct<@UnknownArrayConstrain<[@N]>>, !array.array<@N x !felt.felt>) -> ()
      function.return
    }
  }
}

// CHECK-LABEL: @UnknownArrayConstrain ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg1[1], <felt.const: 7> }
// CHECK-NEXT: }
// CHECK-NEXT: @UnknownMatrixConstrain ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg1[0][1], <felt.const: 7> }
// CHECK-NEXT: }

// -----

module attributes {veridise.lang = "llzk"} {
  include.from "Dialect/LLZK/zir_example_0.llzk" as @zir
}

// CHECK-LABEL: @zir::@risc0::@ValU32 ConstraintDependencyGraph { }
// CHECK-NEXT: @zir::@risc0::@Reg ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@reg], %arg1 }
// CHECK-NEXT: }
// CHECK-NEXT: @zir::@risc0::@Div ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@reciprocal], %arg0[@synthetic_return], %arg1, %arg2 },
// CHECK-NEXT:     { %arg0[@reciprocal], <felt.const: 1> },
// CHECK-NEXT:     { %arg2, <felt.const: 1> }
// CHECK-NEXT: }

// -----

module attributes {veridise.lang = "llzk"} {
  include.from "Dialect/LLZK/zir_example_1.llzk" as @zir
}

// CHECK-LABEL: @zir::@std::@risc0::@ValU32 ConstraintDependencyGraph { }
// CHECK-NEXT: @zir::@std::@risc0::@Reg ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@reg], %arg1 }
// CHECK-NEXT: }
// CHECK-NEXT: @zir::@std::@risc0::@Div ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@reciprocal], %arg0[@synthetic_return], %arg1, %arg2 },
// CHECK-NEXT:     { %arg0[@reciprocal], <felt.const: 1> },
// CHECK-NEXT:     { %arg2, <felt.const: 1> }
// CHECK-NEXT: }
// CHECK-NEXT: @zir::@C1 ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@z][@reg], %arg1, %arg2 }
// CHECK-NEXT: }
// CHECK-NEXT: @zir::@C2 ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@fc1][@z][@reg], %arg1[@low], %arg1[@high] },
// CHECK-NEXT:     { %arg0[@fc1][@z][@reg], <felt.const: 0> }
// CHECK-NEXT: }

// -----

module attributes {veridise.lang = "llzk"} {
  include.from "Dialect/LLZK/zir_example_2.llzk" as @zir
}

// CHECK-LABEL: @zir::@std::@risc0::@ValU32 ConstraintDependencyGraph { }
// CHECK-NEXT: @zir::@std::@risc0::@Reg ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@reg], %arg1 }
// CHECK-NEXT: }
// CHECK-NEXT: @zir::@std::@risc0::@Div ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@reciprocal], %arg0[@synthetic_return], %arg1, %arg2 },
// CHECK-NEXT:     { %arg0[@reciprocal], <felt.const: 1> },
// CHECK-NEXT:     { %arg2, <felt.const: 1> }
// CHECK-NEXT: }
// CHECK-NEXT: @zir::@AssertBit ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg1, <felt.const: 0>, <felt.const: 1> }
// CHECK-NEXT: }
// CHECK-NEXT: @zir::@NondetBitReg ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@reg], %arg0[@synthetic_return] },
// CHECK-NEXT:     { %arg0[@reg], <felt.const: 0>, <felt.const: 1> }
// CHECK-NEXT: }
// CHECK-NEXT: @zir::@ArgU16 ConstraintDependencyGraph { }
// CHECK-NEXT: @zir::@NondetU16Reg ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@arg][@count], <felt.const: 1> },
// CHECK-NEXT:     { %arg0[@arg][@val], %arg0[@synthetic_return] }
// CHECK-NEXT: }
// CHECK-NEXT: @zir::@U16Reg ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@ret][@arg][@count], <felt.const: 1> },
// CHECK-NEXT:     { %arg0[@ret][@arg][@val], %arg0[@ret][@synthetic_return], %arg0[@synthetic_return], %arg1 }
// CHECK-NEXT: }
// CHECK-NEXT: @zir::@AddrDecomposeBits ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@low0][@reg], %arg0[@low0][@synthetic_return], %arg0[@low1][@reg], %arg0[@low1][@synthetic_return], %arg0[@low2], %arg0[@upperDiff][@ret][@arg][@val], %arg0[@upperDiff][@ret][@synthetic_return], %arg0[@upperDiff][@synthetic_return], %arg0[@med14][@arg][@val], %arg0[@med14][@synthetic_return], %arg0[@addr], %arg0[@synthetic_return], %arg1[@low], %arg1[@high], %arg2 },
// CHECK-NEXT:     { %arg0[@low0][@reg], <felt.const: 0>, <felt.const: 1> },
// CHECK-NEXT:     { %arg0[@low0][@synthetic_return], <felt.const: 0>, <felt.const: 1>, <felt.const: 2> },
// CHECK-NEXT:     { %arg0[@low1][@reg], <felt.const: 0>, <felt.const: 1> },
// CHECK-NEXT:     { %arg0[@low1][@synthetic_return], <felt.const: 0>, <felt.const: 1>, <felt.const: 2> },
// CHECK-NEXT:     { %arg0[@low2], <felt.const: 2>, <felt.const: 4> },
// CHECK-NEXT:     { %arg0[@upperDiff][@ret][@arg][@count], <felt.const: 1> },
// CHECK-NEXT:     { %arg0[@upperDiff][@ret][@arg][@val], <felt.const: 1>, <felt.const: 49151>, <felt.const: 65535> },
// CHECK-NEXT:     { %arg0[@upperDiff][@ret][@synthetic_return], <felt.const: 1>, <felt.const: 49151>, <felt.const: 65535> },
// CHECK-NEXT:     { %arg0[@upperDiff][@synthetic_return], <felt.const: 1>, <felt.const: 49151>, <felt.const: 65535> },
// CHECK-NEXT:     { %arg0[@med14][@arg][@count], <felt.const: 1> },
// CHECK-NEXT:     { %arg0[@med14][@synthetic_return], <felt.const: 4>, <felt.const: 16384> },
// CHECK-NEXT:     { %arg0[@addr], <felt.const: 16384> },
// CHECK-NEXT:     { %arg1[@low], <felt.const: 4> },
// CHECK-NEXT:     { %arg1[@high], <felt.const: 1>, <felt.const: 16384>, <felt.const: 49151>, <felt.const: 65535> },
// CHECK-NEXT:     { %arg2, <felt.const: 1>, <felt.const: 49151>, <felt.const: 65535> }
// CHECK-NEXT: }

// -----

module attributes {veridise.lang = "llzk"} {
  include.from "Dialect/LLZK/zir_example_3.llzk" as @zir
}

// COM: @zir::@B is empty since it uses an uninitialized array
// CHECK-LABEL: @zir::@Head ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@$super], %arg1[0] }
// CHECK-NEXT: }
// CHECK-NEXT: @zir::@A ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@$super][@$super], @A<[@N]> }
// CHECK-NEXT: }
// CHECK-NEXT: @zir::@B ConstraintDependencyGraph { }
// CHECK-NEXT: @zir::@Top ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@a][@$super][@$super], %arg0[@b][@$super][@$super][@$super][@$super] },
// CHECK-NEXT:     { %arg0[@a][@$super][@$super], @A<[@N]> }
// CHECK-NEXT: }
// CHECK-NEXT: @zir::@VoidComponent ConstraintDependencyGraph { }

// -----

// This check ensures that calling the same function from multiple different
// contexts will converge correctly.
module attributes {veridise.lang = "llzk"} {
  include.from "Inputs/bits.llzk" as @bits
}

// CHECK-LABEL: @bits::@Component ConstraintDependencyGraph { }
// CHECK-NEXT: @bits::@NondetReg ConstraintDependencyGraph { }
// CHECK-NEXT: @bits::@NondetExtReg ConstraintDependencyGraph { }
// CHECK-NEXT: @bits::@EqzExt ConstraintDependencyGraph { }
// CHECK-NEXT: @bits::@Reg ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@reg][@$super], %arg1 }
// CHECK-NEXT: }
// COM: ExtReg is unconstrained due to a front-end bug
// CHECK-NEXT: @bits::@ExtReg ConstraintDependencyGraph { }
// CHECK-NEXT: @bits::@Div ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@reciprocal], %arg2 },
// CHECK-NEXT:     { %arg0[@reciprocal], <felt.const: 1> },
// CHECK-NEXT:     { %arg2, <felt.const: 1> }
// CHECK-NEXT: }
// CHECK-NEXT: @bits::@Log ConstraintDependencyGraph { }
// CHECK-NEXT: @bits::@Abort ConstraintDependencyGraph { }
// CHECK-NEXT: @bits::@Assert ConstraintDependencyGraph { }
// CHECK-NEXT: @bits::@AssertBit ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg1, <felt.const: 0>, <felt.const: 1> }
// CHECK-NEXT: }
// CHECK-NEXT: @bits::@NondetBitReg ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@reg][@$super], <felt.const: 0>, <felt.const: 1> }
// CHECK-NEXT: }
// CHECK-NEXT: @bits::@BitReg ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@reg][@$super][@$super], %arg1 },
// CHECK-NEXT:     { %arg0[@reg][@reg][@$super], <felt.const: 0>, <felt.const: 1> }
// CHECK-NEXT: }
// CHECK-NEXT: @bits::@AssertTwit ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg1, <felt.const: 0>, <felt.const: 1>, <felt.const: 2>, <felt.const: 3> }
// CHECK-NEXT: }
// CHECK-NEXT: @bits::@BitAnd ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@$temp][@reg][@$super], %arg1, %arg2 }
// CHECK-NEXT: }
// CHECK-NEXT: @bits::@BitOr ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@$temp][@reg][@$super], %arg1, %arg2 },
// CHECK-NEXT:     { %arg0[@$temp][@reg][@$super], <felt.const: 1> },
// CHECK-NEXT:     { %arg1, <felt.const: 1> },
// CHECK-NEXT:     { %arg2, <felt.const: 1> }
// CHECK-NEXT: }
// CHECK-NEXT: @bits::@NondetTwitReg ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@reg][@$super], <felt.const: 0>, <felt.const: 1>, <felt.const: 2>, <felt.const: 3> }
// CHECK-NEXT: }
// CHECK-NEXT: @bits::@NondetFakeTwitReg ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@reg1][@reg][@$super], <felt.const: 0>, <felt.const: 1> },
// CHECK-NEXT:     { %arg0[@$temp_1][@reciprocal], <felt.const: 1>, <felt.const: 2> },
// CHECK-NEXT:     { %arg0[@$temp_0][@$temp][@reg][@$super], %arg0[@$temp][@$temp][@reg][@$super], %arg1 },
// CHECK-NEXT:     { %arg0[@$temp_0][@$temp][@reg][@$super], <felt.const: 2> },
// CHECK-NEXT:     { %arg0[@reg0][@reg][@$super], <felt.const: 0>, <felt.const: 1> },
// CHECK-NEXT:     { %arg0[@$temp][@$temp][@reg][@$super], <felt.const: 1> },
// CHECK-NEXT:     { %arg1, <felt.const: 1>, <felt.const: 2> }
// CHECK-NEXT: }
// CHECK-NEXT: @bits::@TwitReg ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@reg][@$super][@$super], %arg1 },
// CHECK-NEXT:     { %arg0[@reg][@reg][@$super], <felt.const: 0>, <felt.const: 1>, <felt.const: 2>, <felt.const: 3> }
// CHECK-NEXT: }
// CHECK-NEXT: @bits::@FakeTwitReg ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@reg][@$super], %arg0[@reg][@$temp_0][@$temp][@reg][@$super], %arg0[@reg][@$temp][@$temp][@reg][@$super], %arg1 },
// CHECK-NEXT:     { %arg0[@reg][@reg1][@reg][@$super], <felt.const: 0>, <felt.const: 1> },
// CHECK-NEXT:     { %arg0[@reg][@$temp_1][@reciprocal], <felt.const: 1>, <felt.const: 2> },
// CHECK-NEXT:     { %arg0[@reg][@$temp_0][@$temp][@reg][@$super], <felt.const: 1>, <felt.const: 2> },
// CHECK-NEXT:     { %arg0[@reg][@reg0][@reg][@$super], <felt.const: 0>, <felt.const: 1> },
// CHECK-NEXT:     { %arg0[@reg][@$temp][@$temp][@reg][@$super], <felt.const: 1>, <felt.const: 2> },
// CHECK-NEXT:     { %arg1, <felt.const: 1>, <felt.const: 2> }
// CHECK-NEXT: }

// -----

// Testing external calls

module attributes {veridise.lang = "llzk"} {

  function.def private @extern_add(%a: !felt.felt, %b: !felt.felt) -> !felt.felt attributes {extern}

  struct.def @ExternAdder {
    struct.field @sum : !felt.felt {llzk.pub}

    function.def @compute(%a: !felt.felt, %b: !felt.felt) -> !struct.struct<@ExternAdder> {
      %self = struct.new : !struct.struct<@ExternAdder>
      function.return %self : !struct.struct<@ExternAdder>
    }

    function.def @constrain(%self: !struct.struct<@ExternAdder>, %a: !felt.felt, %b: !felt.felt) {
      %sum = struct.readf %self[@sum] : !struct.struct<@ExternAdder>, !felt.felt
      %c = function.call @extern_add(%a, %b) : (!felt.felt, !felt.felt) -> (!felt.felt)
      // We make no assumptions about what %c could be, so this constraint is
      // effectively `%sum === {}`, so the CDG will be empty.
      constrain.eq %sum, %c : !felt.felt
      function.return
    }
  }
}

// CHECK-LABEL: @ExternAdder ConstraintDependencyGraph { }

// -----

module attributes {veridise.lang = "llzk"} {

  function.def @global_add(%a: !felt.felt, %b: !felt.felt) -> !felt.felt {
    %c = felt.add %a, %b
    function.return %c : !felt.felt
  }

  function.def private @irrelevant() attributes {extern}

  struct.def @Adder2 {
    struct.field @sum : !felt.felt {llzk.pub}

    function.def @compute(%a: !felt.felt, %b: !felt.felt) -> !struct.struct<@Adder2> {
      %self = struct.new : !struct.struct<@Adder2>
      %sum = function.call @global_add(%a, %b) : (!felt.felt, !felt.felt) -> (!felt.felt)
      struct.writef %self[@sum] = %sum : !struct.struct<@Adder2>, !felt.felt
      function.return %self : !struct.struct<@Adder2>
    }

    function.def @constrain(%self: !struct.struct<@Adder2>, %a: !felt.felt, %b: !felt.felt) {
      %sum = struct.readf %self[@sum] : !struct.struct<@Adder2>, !felt.felt
      %c = function.call @global_add(%a, %b) : (!felt.felt, !felt.felt) -> (!felt.felt)
      %d = felt.add %c, %c
      function.call @irrelevant() : () -> () // testing to see that this doesn't erase the prior values
      constrain.eq %sum, %d : !felt.felt
      function.return
    }
  }
}

// CHECK-LABEL: @Adder2 ConstraintDependencyGraph {
// CHECK-NEXT:     { %arg0[@sum], %arg1, %arg2 }
// CHECK-NEXT: }

// -----

// Test that allowing Signals to constrain.eq doesn't break the constraint analysis
!Signal = !struct.struct<@Signal>
module attributes {veridise.lang = "llzk"} {
  // pre-defined
  struct.def @Signal {
    struct.field @reg : !felt.felt {llzk.pub}

    function.def @compute(%v: !felt.felt) -> !struct.struct<@Signal> {
      %self = struct.new : !struct.struct<@Signal>
      struct.writef %self[@reg] = %v : !struct.struct<@Signal>, !felt.felt
      function.return %self : !struct.struct<@Signal>
    }

    function.def @constrain(%self: !struct.struct<@Signal>, %v: !felt.felt) {
      function.return
    }
  }

  struct.def @Component00 {
    struct.field @f : !Signal

    function.def @compute(%0: !Signal) -> !struct.struct<@Component00> {
      %self = struct.new : !struct.struct<@Component00>
      struct.writef %self[@f] = %0 : !struct.struct<@Component00>, !Signal
      function.return %self : !struct.struct<@Component00>
    }
    function.def @constrain(%self: !struct.struct<@Component00>, %0: !Signal) {
      %b = struct.readf %self[@f] : !struct.struct<@Component00>, !Signal
      // Signal is the only struct allowed for emit equal
      constrain.eq %b, %0 : !Signal
      function.return
    }
  }

  struct.def @Component01 {
    struct.field @f : !array.array<2 x !Signal>

    function.def @compute(%0: !array.array<2 x !Signal>) -> !struct.struct<@Component01> {
      %self = struct.new : !struct.struct<@Component01>
      struct.writef %self[@f] = %0 : !struct.struct<@Component01>, !array.array<2 x !Signal>
      function.return %self : !struct.struct<@Component01>
    }
    function.def @constrain(%self: !struct.struct<@Component01>, %0: !array.array<2 x !Signal>) {
      %b = struct.readf %self[@f] : !struct.struct<@Component01>, !array.array<2 x !Signal>
      // Signal is the only struct allowed for emit equal
      constrain.eq %b, %0 : !array.array<2 x !Signal>
      function.return
    }
  }
}

// CHECK-LABEL: @Signal ConstraintDependencyGraph { }
// CHECK-NEXT:  @Component00 ConstraintDependencyGraph {
// CHECK-NEXT:      { %arg0[@f], %arg1 }
// CHECK-NEXT:  }
// CHECK-NEXT:  @Component01 ConstraintDependencyGraph {
// CHECK-NEXT:      { %arg0[@f], %arg1 }
// CHECK-NEXT:  }
